//	http://football.kulichki.net/england/2019/teams/arsenal.htm
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	УРЛ = "";
	Если ВвестиСтроку(УРЛ, "Импорт игроков") И ЗначениеЗаполнено(УРЛ) Тогда
		Если ИмпортИгроков(ПараметрКоманды, УРЛ) Тогда
			Оповестить(, ПараметрКоманды);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИмпортИгроков(Команда, Гиперссылка)
	Итого	= 0;
	Текст	= СерверныйФункции.СкачатьПоСсылке(Гиперссылка);
	Если НЕ ПустаяСтрока(Текст) Тогда
		Контрол = Новый ФорматированныйДокумент;
		Контрол.УстановитьHTML(Текст, Новый Структура);
		
		Начало	= Контрол.НайтиТекст("№");
		Конец	= Контрол.НайтиТекст("Соперник");
		Если Начало = Неопределено Тогда
			Возврат Ложь;
		ИначеЕсли Конец = Неопределено Тогда
			мЭлементы = Контрол.СформироватьЭлементы(Начало.ЗакладкаКонца);
		Иначе
			мЭлементы = Контрол.СформироватьЭлементы(Начало.ЗакладкаНачала, Конец.ЗакладкаНачала);
		КонецЕсли;
		
		ШалостьУдалась	= Ложь;
		сИгрок	= Новый Структура("Команда,Период,Номер,ДатаРождения,Наименование,Амплуа,Игрок", Команда, Дата(2018, 8, 1), 0, Дата(1,1,1), "");
		Для Каждого тЭлемент Из мЭлементы Цикл
			Попытка
				Слово = СокрЛП(тЭлемент.Текст);
			Исключение
				Продолжить;
			КонецПопытки;
			
			Если Слово = "№" Тогда
				ШалостьУдалась = Истина;
				
			ИначеЕсли ШалостьУдалась Тогда
				Если Слово = "Вратари" Тогда
					сИгрок.Амплуа = Справочники.Амплуа.НайтиПоНаименованию("Вратарь");
					
				ИначеЕсли Слово = "Защитники" Тогда
					сИгрок.Амплуа = Справочники.Амплуа.НайтиПоНаименованию("Защитник");
					
				ИначеЕсли Слово = "Полузащитники" Тогда
					сИгрок.Амплуа = Справочники.Амплуа.НайтиПоНаименованию("Полузащитник");
					
				Иначе	//Если Слово = "Нападающие" Тогда
					сИгрок.Амплуа = Справочники.Амплуа.НайтиПоНаименованию("Нападающий");
					
				КонецЕсли;
				
				ШалостьУдалась = Ложь;
			
			ИначеЕсли сИгрок.Номер = 0 Тогда
				Попытка
					сИгрок.Номер = Число(Слово);
				Исключение
				КонецПопытки;
				
			ИначеЕсли сИгрок.Номер > 0
			И ПустаяСтрока(сИгрок.Наименование)
			Тогда
				Если СтрДлина(Слово) > 5 Тогда
					сИгрок.Наименование = Слово;
				Иначе
					сИгрок.Номер = 0;
					Продолжить;
				КонецЕсли;
				
			ИначеЕсли НЕ ПустаяСтрока(сИгрок.Наименование)
			И Год(сИгрок.ДатаРождения) < 2
			Тогда
				Попытка
					сИгрок.ДатаРождения = Дата(Сред(Слово, 7) + Сред(Слово, 4, 2) + Лев(Слово, 2));
				Исключение
					Продолжить;
				КонецПопытки;
				
				Если Год(сИгрок.ДатаРождения) > 1 Тогда
					сИгрок.Игрок = Справочники.Игроки.НайтиПоНаименованию(сИгрок.Наименование);
					Если ПустаяСтрока(сИгрок.Игрок) Тогда
						НовыйИгрок = Справочники.Игроки.СоздатьЭлемент();
						ЗаполнитьЗначенияСвойств(НовыйИгрок, сИгрок);
						НовыйИгрок.Записать();
						
						сИгрок.Игрок = НовыйИгрок.Ссылка;
					КонецЕсли;
					
					НЗ = РегистрыСведений.ПеремещенияИгроков.СоздатьНаборЗаписей();
					НЗ.Отбор.Игрок.Установить(сИгрок.Игрок);
					НЗ.Прочитать();
					Если НЗ.Количество() = 0 Тогда
						Попытка
							Запись = РегистрыСведений.ПеремещенияИгроков.СоздатьМенеджерЗаписи();
							ЗаполнитьЗначенияСвойств(Запись, сИгрок);
							Запись.Записать(Ложь);
							
							Итого = Итого + 1;
						Исключение
						КонецПопытки;
					КонецЕсли;
					
					сИгрок.Номер		= 0;
					сИгрок.ДатаРождения	= Дата(1,1,1);
					сИгрок.Наименование	= "";
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат (Итого > 0);
КонецФункции
