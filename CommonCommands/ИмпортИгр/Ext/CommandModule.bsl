
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	УРЛ = УРЛ(ПараметрКоманды);
	ПоказатьВводСтроки(Новый ОписаниеОповещения("Импорт", ЭтотОбъект, ПараметрКоманды), УРЛ, "Импорт игр");
КонецПроцедуры

&НаСервере
Функция УРЛ(Тур)
	Ответ = СерверныйФункции.КомментарийКакУРЛ(Тур.Комментарий);
	Если ПустаяСтрока(Ответ) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	Чемпионаты.Владелец.Комментарий КАК Комментарий,
		                      |	Чемпионаты.ДатаОкончания КАК ДатаОкончания,
		                      |	Туры.Код КАК Код,
		                      |	2 КАК Порядок
		                      |ИЗ
		                      |	Справочник.Туры КАК Туры
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты КАК Чемпионаты
		                      |		ПО Туры.Владелец = Чемпионаты.Ссылка
		                      |ГДЕ
		                      |	Туры.Ссылка = &Тур
		                      |	И Чемпионаты.Владелец.Комментарий ПОДОБНО ""%http%""
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	Чемпионаты.Комментарий,
		                      |	Чемпионаты.ДатаОкончания,
		                      |	Туры.Код,
		                      |	1
		                      |ИЗ
		                      |	Справочник.Туры КАК Туры
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты КАК Чемпионаты
		                      |		ПО Туры.Владелец = Чемпионаты.Ссылка
		                      |ГДЕ
		                      |	Туры.Ссылка = &Тур
		                      |	И Чемпионаты.Комментарий ПОДОБНО ""%http%""
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	Порядок");
		Запрос.УстановитьПараметр("Тур",			Тур);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока ПустаяСтрока(Ответ) И Выборка.Следующий() Цикл
			Год		= ФорматЧГ(Выборка.ДатаОкончания);
			Код		= ФорматЧГ(Выборка.Код);
			Ответ	= СерверныйФункции.КомментарийКакУРЛ(Выборка.Комментарий);
			Если ПустаяСтрока(Ответ) Тогда
				//
			ИначеЕсли СтрНайти(Ответ, "/%2/") > 0 И СтрНайти(Ответ, "/%1/") > 0 Тогда
				Ответ = СтрШаблон(Ответ, Год, Код);
			ИначеЕсли СтрНайти(Ответ, "/%1/") > 0 Тогда
				Ответ = СтрШаблон(Ответ, Код);
			ИначеЕсли СтрНайти(Ответ, "%") = 0 Тогда
				Если СтрНайти(Ответ, Год) = 0
				И СтрНайти(Ответ, "/" + Код + "/") = 0
				Тогда
					Ответ = СтрШаблон(Ответ + ?(Год(Выборка.ДатаОкончания) > 2010, "%1/%2/", "%1/%2.htm"), Год, Код);
				ИначеЕсли СтрНайти(Ответ, Год) > 0 Тогда
					Ответ = СтрШаблон(Ответ + ?(Год(Выборка.ДатаОкончания) > 2010, "%1/", "%1.htm"), Код);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура Импорт(УРЛ, Тур) Экспорт
	Если ПустаяСтрока(УРЛ) Тогда
		//
	ИначеЕсли ИмпортНаСервере(УРЛ, Тур) Тогда
		Оповестить("Матчи", Чемпионат(Тур));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИмпортНаСервере(Гиперссылка, Тур)
	Ответ	= Ложь;
	Если ЭтоУРЛ(Гиперссылка) Тогда
		Ответ	= СерверныйФункции.ИмпортИгрПоГиперссылке(Гиперссылка, Тур);
	Иначе
		Ответ	= СерверныйФункции.ИмпортИгрПоСтроке(Гиперссылка, Тур);
	КонецЕсли;
	
	Если Ответ Тогда
		Справочники.Туры.ДатыУстановить(Тур);
		РегистрыСведений.ИтогиПромежуточные.Обновить(Тур, Истина);
		Если ЗначениеЗаполнено(СерверныйФункции.Чемпион(Тур.Владелец)) Тогда
			РегистрыСведений.Итоги.Обновить(Тур.Владелец, Истина);
		КонецЕсли;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция Чемпионат(Тур)
	Возврат Тур.Владелец;
КонецФункции
