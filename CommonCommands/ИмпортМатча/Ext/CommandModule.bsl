
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	УРЛ = УРЛ(ПараметрКоманды);
	ПоказатьВводСтроки(Новый ОписаниеОповещения("Импорт", ЭтотОбъект, ПараметрКоманды), УРЛ, "Импорт матча");
КонецПроцедуры

&НаКлиенте
Процедура Импорт(УРЛ, ПараметрКоманды) Экспорт
	Если ЭтоУРЛ(УРЛ) Тогда
		Если ИмпортНаСервере(УРЛ, ПараметрКоманды) Тогда
			Оповестить("Матч", ПараметрКоманды);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИмпортНаСервере(Гиперссылка, Матч)
	Ответ	= Ложь;
	
	сМатч	= Новый Структура("Матч,Дата,Счет,Команда1,Команда2,Команды,СоставыКоманд", Матч, Дата(1,1,1), "", "", "", Новый Массив, Новый Массив);
	сМатч.Вставить("Тур",		Матч.Тур);
	сМатч.Вставить("Страна",	СтранаНайти(Матч.Тур));
	сМатч.Вставить("Игроки1",	Новый Соответствие);
	сМатч.Вставить("Игроки2",	Новый Соответствие);
	сМатч.Вставить("Стадион");
	Команды		= Новый Массив;
	Команда1	= "";
	Команда2	= "";
	Тренеры		= Ложь;
	События		= Новый Массив;
	Комментарий	= "";
	
	Слова	= ПолучитьЭлементыПоИмени(Гиперссылка);
	Для Каждого Слово Из Слова Цикл
		Если НЕ ЗначениеЗаполнено(сМатч.Дата) Тогда
			Если СтрНайти2(Слово, "стадион") > 0 И СтрЧислоСтрок(Слово) = 1 Тогда
				ШапкаЗаполнить(сМатч, Слово);
			КонецЕсли;
		ИначеЕсли ПустаяСтрока(сМатч.Счет) Тогда
			Если СтрокаСодержитЧисло(Слово) Тогда
				Если КомандыЗаполнить(сМатч, Слово) Тогда
					Для Каждого ТекЭлемент Из сМатч.Команды Цикл
						Если ПустаяСтрока(Команда1) Тогда
							Команда1	= ТекЭлемент.Команда.ПолноеНаименование();
							Команды.Добавить(ТекЭлемент.Команда);
							сМатч.Игроки1	= ИгрокиПолучить(ТекЭлемент.Команда, сМатч.Дата);
							Если НЕ ЗначениеЗаполнено(сМатч.Стадион) И ЗначениеЗаполнено(сМатч.Страна) Тогда
								сМатч.Стадион = СтадионНайти(ТекЭлемент.Команда);
							КонецЕсли;
							
						ИначеЕсли ПустаяСтрока(Команда2) Тогда
							Команда2	= ТекЭлемент.Команда.ПолноеНаименование();
							Команды.Добавить(ТекЭлемент.Команда);
							сМатч.Игроки2	= ИгрокиПолучить(ТекЭлемент.Команда, сМатч.Дата);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрСравнить(Команда1, Слово) = 0 Тогда
			//
		ИначеЕсли СтрСравнить(Команда2, Слово) = 0 Тогда
			//
		ИначеЕсли Тренеры
		И ЭтоКомментарий(Слово, Команда1, Команда2)
		Тогда
			Комментарий	= КомментарийПричесать(Слово)  + Разрыв() + Гиперссылка;
		ИначеЕсли СтрЧислоСтрок(Слово) = 1 Тогда
			Если СтрНайти(Слово, ":") > 0 Тогда
				Если СтрСравнить(СтрУдалить(Слово), "Наказания") = 0 Тогда
					//
				ИначеЕсли СтрСравнить(СтрУдалить(Слово), "Тренеры") = 0 Тогда
					Тренеры	= Истина;
				ИначеЕсли События.Количество() = 0 И СтрокаСодержитЧисло(Слово) Тогда // это голы
					//События	= СобытияПолучить(сМатч, Слово);
				КонецЕсли;
				Продолжить;
			ИначеЕсли Тренеры Тогда
				Продолжить;
			ИначеЕсли СтрНайти(Слово, ",") > 0 Тогда
				Слово = Разделить(Слово, ",").Получить(0);
			КонецЕсли;
			Если СоставыКомандДобавить(Слово, 0, сМатч) Тогда
				Продолжить;
			ИначеЕсли СоставыКомандДобавить(Слово, 1, сМатч) Тогда
				Продолжить;
			Иначе
				Игрок = ИгрокНайти(Слово, Команды);
				Если Команды.Найти(Игрок.Команда) = Неопределено Тогда
					//
				Иначе
					сМатч.СоставыКоманд.Добавить(Игрок);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(сМатч.Счет)
	И Год(сМатч.Дата) > 2000
	И сМатч.Команды.Количество() = 2
	Тогда
		сМатч.Вставить("Комментарий", ?(ПустаяСтрока(Комментарий), Гиперссылка, Комментарий));
		Ответ = МатчЗаписать(сМатч);
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

&НаСервере
Процедура СоставыКомандСортировать(Док)
	Если Док.СоставыКоманд.Количество() = 0 Тогда Возврат;
	ИначеЕсли Док.Команды.Количество() = 0 Тогда Возврат;
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	СоставыКоманд.Игрок КАК Игрок,
	                      |	СоставыКоманд.Команда КАК Команда
	                      |ПОМЕСТИТЬ ВТ
	                      |ИЗ
	                      |	&СоставыКоманд КАК СоставыКоманд
	                      |ГДЕ
	                      |	СоставыКоманд.Команда В(&Команды)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ.Игрок КАК Игрок,
	                      |	ВТ.Команда КАК Команда
	                      |ИЗ
	                      |	ВТ КАК ВТ
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
	                      |		ПО ВТ.Игрок = Игроки.Ссылка
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ВТ.Команда = &Хозяева УБЫВ,
	                      |	Игроки.Амплуа.Код,
	                      |	Игрок
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("СоставыКоманд",		Док.СоставыКоманд.Выгрузить());
	Запрос.УстановитьПараметр("Команды",			Док.Команды.ВыгрузитьКолонку("Команда"));
	Запрос.УстановитьПараметр("Хозяева",			Док.Команды.Получить(0).Команда);
	Док.СоставыКоманд.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаСервере
Процедура ШапкаЗаполнить(Матч, Слова)
	Город	= Неопределено;
	Стадион	= Неопределено;
	Ультима	= "";
	Для Каждого Слово Из Разделить(Слова, ".") Цикл
		Если Год(Матч.Дата) < 2 Тогда
			Матч.Дата = КакДата(Слово);
			
		ИначеЕсли СтрНайти2(Слово, "стадион") > 0 Тогда
			Если ЗначениеЗаполнено(Матч.Матч.Стадион) Тогда
				Стадион	= Матч.Матч.Стадион;
			Иначе
				СтадионИмя	= СтрУдалить(СтрУдалить(НРег(Слово), "стадион"), """");
				Если НЕ ПустаяСтрока(Ультима) И ЗначениеЗаполнено(Матч.Страна) Тогда
					Город	= ГородНайти(Ультима, Матч.Страна);
					Если ЗначениеЗаполнено(Город) Тогда
						Матч.Стадион = СтадионНайти(Город);
					КонецЕсли;
				КонецЕсли;
				Если НЕ ЗначениеЗаполнено(Матч.Стадион) Тогда
					Матч.Стадион	= СтадионНайти(СтадионИмя);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли СтрНайти2(Слово, "зрител") > 0 Тогда
			Для Каждого Строка Из Разделить(Слово) Цикл
				Если СтрокаКакЧисло(Строка) > 0 Тогда
					Если ЗначениеЗаполнено(Стадион) Или ЗначениеЗаполнено(Матч.Стадион) Тогда
						Матч.Вставить("КоличествоЗрителей", СтрокаКакЧисло(Строка));
					КонецЕсли;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Ультима	= Слово;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ЗначениеЗаполнено(Матч.Стадион) Тогда
		Матч.Стадион	= Стадион;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция КомандыЗаполнить(Матч, Слова)
	Голов1			= -1;
	Голов2			= -1;
	
	Для Каждого Слово Из Разделить(СтрЗаменить(Слова, " - ", "="), "=") Цикл
		Если Голов2 >= 0 Тогда
			Прервать;
		ИначеЕсли НЕ ЗначениеЗаполнено(Матч.Команда1) Тогда
			Матч.Команда1 = КомандаНайти(Слово, Матч.Тур);
		ИначеЕсли НЕ ЗначениеЗаполнено(Матч.Команда2) Тогда
			Матч.Команда2 = КомандаНайти(Слово, Матч.Тур);
		ИначеЕсли СтрНайти(Слово, ":") > 0 Тогда
			Для Каждого Голов Из Разделить(Разделить(Слово).Получить(0), ":") Цикл
				Если Голов1 < 0 Тогда
					Голов1		= СтрокаКакЧисло(Голов);
				ИначеЕсли Голов2 < 0 Тогда
					Голов2		= СтрокаКакЧисло(Голов);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	Если Голов1 >= 0 И ЗначениеЗаполнено(Матч.Команда1)
	И Голов2 >= 0 И ЗначениеЗаполнено(Матч.Команда2)
	Тогда
		Матч.Команды.Добавить(Новый Структура("Команда,КоличествоГолов", Матч.Команда1, Голов1));
		Матч.Команды.Добавить(Новый Структура("Команда,КоличествоГолов", Матч.Команда2, Голов2));
		Матч.Счет	= Слова;
		
		Если НЕ ЗначениеЗаполнено(Матч.Стадион)
		И ЗначениеЗаполнено(Матч.Команда1)
		Тогда
			Матч.Стадион = СтадионНайти(Матч.Команда1);
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаСервере
Функция КакДата(Слово)
	Ответ = Дата(1,1,1);
	Если СтрНайти(Слово, " ") = 0 Тогда
		Ответ = СтрокаКакДата(Слово);
	Иначе
		Мезе	= 0;
		Жьорно	= 0;
		Для Каждого Строка Из Разделить(Слово) Цикл
			Если Жьорно = 0 Тогда
				Жьорно	= СтрокаКакЧисло(Строка);
			ИначеЕсли СтрокаКакМесяц(Строка) > 0 Тогда
				Мезе	= СтрокаКакМесяц(Строка);
			КонецЕсли;
		КонецЦикла;
		Если Мезе > 0 И Жьорно > 0 Тогда
			Попытка
				Ответ = Дата(Год(ТекущаяДатаСеанса()), Мезе, Жьорно);
				Если Ответ > ТекущаяДатаСеанса() Тогда
					Ответ = Дата(Год(ТекущаяДатаСеанса()) - 1, Мезе, Жьорно);
				КонецЕсли;
			Исключение КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция УРЛ(Матч)
	Возврат ?(ТипЗнч(Матч)=Тип("ДокументСсылка.Матч"), КомментарийКакУРЛ(Матч.Комментарий), "");
КонецФункции

&НаСервере
Функция ПолучитьЭлементыПоИмени(Гиперссылка)
	Возврат СерверныйФункции.ПолучитьЭлементыПоИмени(Гиперссылка);
КонецФункции

&НаСервере
Функция КомментарийКакУРЛ(Комментарий)
	Возврат СерверныйФункции.КомментарийКакУРЛ(Комментарий);
КонецФункции

&НаСервере
Функция Разрыв(Один=Ложь)
	Возврат ?(Один, Символы.ПС, Символы.ПС + Символы.ПС);
КонецФункции

&НаСервере
Функция Выделить(Слово)
	Возврат Разрыв() + Слово + Разрыв();
КонецФункции

&НаСервере
Функция КомментарийПричесать(Знач Комментарий)
	Если СтрЧислоСтрок(Комментарий) > 1 Тогда
		Комментарий	= СтрПолучитьСтроку(Комментарий, СтрЧислоСтрок(Комментарий));
	КонецЕсли;
	
	Если СтрНайти2(Комментарий, "Фото - Getty Images.") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "Фото - Getty Images.", Разрыв(Истина));
	ИначеЕсли СтрНайти2(Комментарий, "Фото - Getty Images") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "Фото - Getty Images", Разрыв(Истина));
	КонецЕсли;
	Если СтрНайти(Комментарий, "Предыстория.") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "Предыстория.", Выделить("Предыстория"));
	Иначе
		Комментарий	= СтрЗаменить(Комментарий, "Предыстория", Выделить("Предыстория"));
	КонецЕсли;
	Если СтрНайти(Комментарий, "Ход матча.") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "Ход матча.", Выделить("Ход матча"));
	ИначеЕсли СтрНайти(Комментарий, "Ход матча") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "Ход матча", Выделить("Ход матча"));
	Иначе
		Комментарий	= СтрЗаменить(Комментарий, "Ход игры", Выделить("Ход игры"));
	КонецЕсли;
	Если СтрНайти(Комментарий, "   ") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "   ", Разрыв(Истина));
	ИначеЕсли СтрНайти(Комментарий, "  ") > 0 Тогда
		Комментарий	= СтрЗаменить(Комментарий, "  ", Разрыв(Истина));
	Иначе
		Комментарий	= СтрЗаменить(Комментарий, "Лучший игрок матча", Разрыв() + "Лучший игрок матча");
	КонецЕсли;
	Возврат Комментарий;
КонецФункции

&НаСервере
Функция ЭтоКомментарий(Слово, Команда1, Команда2)
	Если СтрДлина(Слово) < 100 Тогда
		Возврат Ложь;
	ИначеЕсли СтрЧислоВхождений(Команда1, " ") + СтрЧислоВхождений(Команда2, " ") > 0 Тогда
		Ответ	= 0;
		мСлова	= Разделить(Команда1 + " " + Команда2);
		Для Каждого Элемент Из мСлова Цикл
			Если СтрДлина(Элемент) > 3 Тогда
				Ответ = Ответ + СтрЧислоВхождений(Слово, Элемент);
			КонецЕсли;
		КонецЦикла;
		Возврат (Ответ > 1);
	КонецЕсли;
	Возврат (СтрЧислоВхождений(Слово, Команда1) + СтрЧислоВхождений(Слово, Команда2) > 1);
КонецФункции

&НаСервере
Функция ГородНайти(Слово, Страна)
	Возврат СерверныйФункции.ГородНайти(Слово, Страна);
КонецФункции

&НаСервере
Функция КомандаНайти(Слово, Страна)
	Возврат СерверныйФункции.КомандаНайти(Слово, Страна);
КонецФункции

&НаСервере
Функция ИгрокиПолучить(Команда, Дата)
	Ответ	= Новый Соответствие;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	Игроки.Наименование КАК Ключ,
	      	               |	Перемещения.Игрок КАК Значение
	      	               |ИЗ
	      	               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ) КАК Перемещения
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
	      	               |		ПО Перемещения.Игрок = Игроки.Ссылка
	      	               |ГДЕ
	      	               |	Перемещения.Команда = &Команда");
	Запрос.УстановитьПараметр("Команда",			Команда);
	Запрос.УстановитьПараметр("Дата",				Дата);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Ответ.Вставить(Выборка.Ключ, Выборка.Значение);
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция ИгрокНайти(Слово, Команды)
	Возврат СерверныйФункции.ИгрокНайти(Слово, Команды);
КонецФункции

&НаСервере
Функция ИгрокПодобрать(Наименование, Команды)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	ПеремещенияИгроков.Игрок КАК Игрок
	                      |ИЗ
	                      |	РегистрСведений.ПеремещенияИгроков КАК ПеремещенияИгроков
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
	                      |		ПО ПеремещенияИгроков.Игрок = Игроки.Ссылка
	                      |ГДЕ
	                      |	ПеремещенияИгроков.Команда В(&Команды)
	                      |	И (Игроки.Наименование ПОДОБНО &Комментарий
	                      |			ИЛИ Игроки.Комментарий ПОДОБНО &Комментарий)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ПеремещенияИгроков.Период УБЫВ,
	                      |	Игроки.Наименование = &Наименование УБЫВ,
	                      |	Игроки.Наименование ПОДОБНО &Комментарий УБЫВ,
	                      |	Игроки.ПометкаУдаления");
	Запрос.УстановитьПараметр("Наименование",		СокрЛП(Наименование));
	Запрос.УстановитьПараметр("Комментарий",		"%" + СокрЛП(Наименование) + "%");
	Запрос.УстановитьПараметр("Команды",			Команды);
	Выборка	= Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Игрок, Справочники.Игроки.ПустаяСсылка());
КонецФункции

&НаСервере
Функция СоставыКомандДобавить(Слово, Индекс, сМатч)
	Игрок = ?(Индекс=0, сМатч.Игроки1.Получить(Слово), сМатч.Игроки2.Получить(Слово));
	Если Игрок = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	сМатч.СоставыКоманд.Добавить(Новый Структура("Игрок,Команда", Игрок, ?(Индекс=0, сМатч.Команда1, сМатч.Команда2)));
	Возврат Истина;
КонецФункции

&НаСервере
Функция СтранаНайти(Тур)
	Возврат СерверныйФункции.СтранаНайти(Тур);
КонецФункции

&НаСервере
Функция СтадионНайти(Параметр)
	Города	= СерверныйФункции.СтадионНайти(Параметр, Истина);
	Возврат ?(Города.Количество() = 1, Города.Получить(0), Справочники.Стадионы.ПустаяСсылка());
КонецФункции

&НаСервере
Функция СобытияПолучить(Матч, Слова)
	События	= Новый Массив;
	Команды	= Новый Массив;
	Команды.Добавить(Матч.Команда1);
	Команды.Добавить(Матч.Команда2);
	Для Каждого Слово Из Разделить(Слова, ".") Цикл
		Прима	= Разделить(Слово, ",");
		Если Прима.Количество() = 2 Тогда
			Игрок	= ИгрокПодобрать(Прима.Получить(0), Команды);
			Время	= СтрокаКакЧисло(Разделить(Прима.Получить(1)).Получить(0));
			Если ЗначениеЗаполнено(Игрок) И Время > 0 Тогда
				События.Добавить(Новый Структура("Игрок,Время", Игрок, Время));
			КонецЕсли;
		Иначе
			События.Очистить();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат События;
КонецФункции

&НаСервере
Функция МатчЗаписать(сМатч)
	Ответ	= Ложь;
	
	Док = сМатч.Матч.ПолучитьОбъект();
	Если Док.ПометкаУдаления Тогда
		Док.УстановитьПометкуУдаления(Ложь);
	КонецЕсли;
	Если НачалоДня(Док.Дата) = сМатч.Дата Тогда
		ЗаполнитьЗначенияСвойств(Док, сМатч,, "Дата");
	Иначе
		ЗаполнитьЗначенияСвойств(Док, сМатч);
	КонецЕсли;
	Если Док.Команды.НайтиСтроки(Новый Структура("Команда", сМатч.Команда1)).Количество() = 0
		Или Док.Команды.НайтиСтроки(Новый Структура("Команда", сМатч.Команда2)).Количество() = 0
		Тогда
		Док.Команды.Очистить();
		Для Каждого ТекЭлемент Из сМатч.Команды Цикл
			ЗаполнитьЗначенияСвойств(Док.Команды.Добавить(), ТекЭлемент);
		КонецЦикла;
		Док.СоставыКоманд.Очистить();
	КонецЕсли;
	Для Каждого ТекЭлемент Из сМатч.СоставыКоманд Цикл
		ЗаполнитьЗначенияСвойств(Док.СоставыКоманд.Добавить(), ТекЭлемент);
	КонецЦикла;
	СоставыКомандСортировать(Док);
	Если Док.ПроверитьЗаполнение() Тогда
		Попытка
			Док.Записать(РежимЗаписиДокумента.Проведение);
			Ответ = Истина;
		Исключение
			ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, Док.Ссылка);
		КонецПопытки;
	Иначе
		ОбщегоНазначения.СообщитьОбОшибке(ОписаниеОшибки(),, Док.Ссылка);
	КонецЕсли;
	Возврат Ответ;
КонецФункции
