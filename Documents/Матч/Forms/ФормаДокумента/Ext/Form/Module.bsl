
&НаКлиенте
Процедура КомандыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Объект.Команды.Количество() > 1;
КонецПроцедуры

&НаСервере
Функция КомандыПолучить()
	Возврат Объект.Команды.Выгрузить().ВыгрузитьКолонку("Команда");
КонецФункции

&НаСервере
Процедура СоставыЗаполнитьНаСервере()
	Объект.СоставыКоманд.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", 		Объект.Дата);
	мКоманды = Новый Массив;
	Если Объект.Команды.Количество() > 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	МатчСоставыКоманд.Игрок КАК Игрок,
		               |	Матч.Дата КАК Дата
		               |ПОМЕСТИТЬ ВТ
		               |ИЗ
		               |	Документ.Матч.СоставыКоманд КАК МатчСоставыКоманд
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
		               |		ПО МатчСоставыКоманд.Ссылка = Матч.Ссылка
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Амплуа КАК Роли
		               |			ПО Игроки.Амплуа = Роли.Ссылка
		               |		ПО МатчСоставыКоманд.Игрок = Игроки.Ссылка
		               |ГДЕ
		               |	Матч.Дата < &Дата
		               |	И Матч.Проведен = ИСТИНА
		               |	И МатчСоставыКоманд.Команда = &Команда
		               |	И Игроки.ПометкаУдаления = ЛОЖЬ
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	МатчСоставыКоманд.Игрок,
		               |	Матч.Дата
		               |
		               |ИМЕЮЩИЕ
		               |	МАКСИМУМ(Матч.Дата) = Матч.Дата
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Перемещения.Игрок КАК Игрок,
		               |	Перемещения.Команда КАК Команда,
		               |	Перемещения.Амплуа КАК Амплуа
		               |ИЗ
		               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(
		               |			&Дата,
		               |			Игрок В
		               |				(ВЫБРАТЬ
		               |					ВТ.Игрок
		               |				ИЗ
		               |					ВТ)) КАК Перемещения
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Амплуа,
		               |	Игрок
		               |АВТОУПОРЯДОЧИВАНИЕ";
		Для Каждого ТекКоманда Из Объект.Команды Цикл
			Запрос.УстановитьПараметр("Команда",	ТекКоманда.Команда);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Количество() = 0 Тогда
				мКоманды.Добавить(ТекКоманда.Команда);
			Иначе
				СоставыКомандДобавить(Выборка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если мКоманды.Количество() > 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Перемещения.Игрок КАК Игрок,
		               |	Перемещения.Команда КАК Команда,
		               |	Перемещения.Амплуа КАК Амплуа,
		               |	Перемещения.Команда = &Гости КАК Порядок
		               |ИЗ
		               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(
		               |			&Дата,
		               |			Игрок.ПометкаУдаления = ЛОЖЬ
		               |				И Команда В (&Команды)) КАК Перемещения
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Амплуа КАК Роли
		               |			ПО Игроки.Амплуа = Роли.Ссылка
		               |		ПО Перемещения.Игрок = Игроки.Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Порядок,
		               |	Амплуа,
		               |	Игрок
		               |АВТОУПОРЯДОЧИВАНИЕ";
		Запрос.УстановитьПараметр("Команды",	мКоманды);
		Запрос.УстановитьПараметр("Гости",	?(Объект.Команды.Количество()=2, Объект.Команды.Получить(1).Команда, Неопределено));
		СоставыКомандДобавить(Запрос.Выполнить().Выбрать());
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоставыКомандДобавить(Выборка)
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.СоставыКоманд.Добавить(), Выборка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ГрафикОбновитьНаСервере()
	График.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата",	Объект.Дата);
	Если ЗначениеЗаполнено(Амплуа) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ДЕНЬ) КАК Количество
		               |ИЗ
		               |	Документ.Матч.СоставыКоманд КАК Матч
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ГОД(Игрок.ДатаРождения) > 1900) КАК Перемещения
		               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Амплуа КАК Роли
		               |			ПО Перемещения.Амплуа = Роли.Ссылка
		               |		ПО Матч.Игрок = Перемещения.Игрок
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		               |		ПО Матч.Игрок = Игроки.Ссылка
		               |ГДЕ
		               |	Матч.Ссылка = &Ссылка
		               |	И Матч.Команда = &Команда
		               |	И ЕСТЬNULL(Роли.Ссылка, Игроки.Амплуа) = &Амплуа";
		Запрос.УстановитьПараметр("Ссылка",	Объект.Ссылка);
		Запрос.УстановитьПараметр("Амплуа",	Амплуа);
		Для Каждого тКоманда Из Объект.Команды Цикл
			Запрос.УстановитьПараметр("Команда",	тКоманда.Команда);
			мСтроки = Запрос.Выполнить().Выгрузить();
			//Если мСтроки.Количество() = 0 Тогда
			//	Запрос.Текст = "ВЫБРАТЬ
			//	               |	РАЗНОСТЬДАТ(Перемещения.Игрок.ДатаРождения, &Дата, ДЕНЬ) КАК Количество
			//	               |ИЗ
			//	               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ГОД(Игрок.ДатаРождения) > 1900) КАК Перемещения
			//	               |ГДЕ
			//	               |	Перемещения.Команда = &Команда
			//	               |	И Перемещения.Амплуа = &Амплуа";
			//	мСтроки = Запрос.Выполнить().Выгрузить();
			//КонецЕсли;
			Для тФаза = 1 По 3 Цикл
				Серия	= График.УстановитьСерию(Биоритмы.ФЭИ(тФаза));
				//Серия.Цвет = СерияЦвет(тФаза);
				
				КолИтого	= 0;
				ВесИтого	= 0;
				Для Каждого тЭлемент Из мСтроки Цикл
					КолИтого	= КолИтого + 1;
					ВесИтого	= ВесИтого + Биоритмы.ФазаРасчитать2(тЭлемент.Количество, тФаза);
				КонецЦикла;
				Если КолИтого > 0 Тогда
					Точка	= График.УстановитьТочку(тКоманда.Команда);
					График.УстановитьЗначение(Точка, Серия, Окр(ВесИтого / КолИтого, 1));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		Для Каждого тКоманда Из Объект.Команды Цикл
			мСтроки = Объект.СоставыКоманд.НайтиСтроки(Новый Структура("Команда", тКоманда.Команда));
			//Если мСтроки.Количество() = 0 Тогда
			//	Запрос.Текст = "ВЫБРАТЬ
			//	               |	Перемещения.Игрок КАК Игрок
			//	               |ИЗ
			//	               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ГОД(Игрок.ДатаРождения) > 1900) КАК Перемещения
			//	               |ГДЕ
			//	               |	Перемещения.Команда = &Команда";
			//	Запрос.УстановитьПараметр("Команда",	тКоманда.Команда);
			//	мСтроки = Запрос.Выполнить().Выгрузить();
			//КонецЕсли;
			Для тФаза = 1 По 3 Цикл
				Серия	= График.УстановитьСерию(Биоритмы.ФЭИ(тФаза));
				
				КолИтого	= 0;
				ВесИтого	= 0;
				Для Каждого тЭлемент Из мСтроки Цикл
					КолИтого	= КолИтого + 1;
					ВесИтого	= ВесИтого + Биоритмы.ФазаРасчитать1(тЭлемент.Игрок.ДатаРождения, тФаза, Объект.Дата);
				КонецЦикла;
				Если КолИтого > 0 Тогда
					Точка	= График.УстановитьТочку(тКоманда.Команда);
					График.УстановитьЗначение(Точка, Серия, Окр(ВесИтого / КолИтого, 1));
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

//СерияЦвет()
//Возврат ЦветаСтиля.ЦветОтрицательногоЧисла

&НаКлиенте
Процедура ГрафикОбновить(Команда)
	Если Объект.Команды.Количество() = 0 Тогда
		Сообщить("Сначала заполните таблицу ""Команды"" ");
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаКоманды;
	ИначеЕсли Команда.Имя = "ГрафикОбновить" Тогда
		ГрафикОбновитьНаСервере();
	ИначеЕсли Команда.Имя = "АнонсОбновить" Тогда
		АнонсОбновитьНаСервере();
	ИначеЕсли Команда.Имя = "СоставыЗаполнить" Тогда
		СоставыЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если Элементы.Сравнение = ТекущаяСтраница Тогда
		//Если График.КоличествоСерий < 3 И Объект.СоставыКоманд.Количество() > 0 Тогда
		//	ГрафикОбновитьНаСервере();
		//	РаздельноПриИзменении();
		//КонецЕсли;
		
	ИначеЕсли Элементы.Анонс = ТекущаяСтраница Тогда
		//Если Элементы.ТабДок.автом = ФиксацияВТаблице.Нет Тогда
			АнонсОбновитьНаСервере();
		//КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Объект.Команды.Количество() = 2 Тогда
		СерверныйФункции.МатчОбработкаПроверкиЗаполнения(Объект, Отказ);
		Если Отказ Тогда
			Твит = Новый СообщениеПользователю;
			Твит.КлючДанных	= Объект.Ссылка;
			Твит.Поле		= "СоставыКоманд.Команда";
			Твит.Текст		= "ТЧ ""СоставыКоманд"" заполнена неверно";
			Твит.Сообщить();
		КонецЕсли;
		
	Иначе
		Отказ	= Истина;
		
		Твит = Новый СообщениеПользователю;
		Твит.КлючДанных	= Объект.Ссылка;
		Твит.Поле		= "Команды";
		Твит.Текст		= "ТЧ ""Команды"" заполнена неверно";
		Твит.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СтадионПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Объект.Стадион.Владелец) Тогда
		мКоманды = Объект.Команды.НайтиСтроки(Новый Структура("Команда", Объект.Стадион.Владелец));
		Если мКоманды.Количество() = 0 Тогда
			Если Объект.Команды.Количество() = 0 Тогда
				тСтрока = Объект.Команды.Добавить();
			Иначе
				тСтрока = Объект.Команды.Получить(0);
			КонецЕсли;
			тСтрока.Команда = Объект.Стадион.Владелец;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтадионПриИзменении(Элемент)
	СтадионПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоставыКомандИгрокПриИзменении(Элемент)
	Элементы.СоставыКоманд.ТекущиеДанные.Команда = СерверныйФункции.Команда(Элементы.СоставыКоманд.ТекущиеДанные.Игрок, Объект.Дата);
КонецПроцедуры

&НаКлиенте
Процедура АмплуаПриИзменении(Элемент)
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РаздельноПриИзменении(Элемент=Неопределено)
	График.ТипДиаграммы	= ?(Раздельно, ТипДиаграммы.ГистограммаОбъемная, ТипДиаграммы.ГистограммаСНакоплениемОбъемная);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		ПрошлыеВстречи.Параметры.УстановитьЗначениеПараметра("Команды", Новый Массив);
		ПрошлыеМатчи.Параметры.УстановитьЗначениеПараметра("Команды", Новый Массив);
	Иначе
		мКоманды = КомандыПолучить();
		ПрошлыеВстречи.Параметры.УстановитьЗначениеПараметра("Команды", мКоманды);
		ПрошлыеМатчи.Параметры.УстановитьЗначениеПараметра("Команды", мКоманды);
	КонецЕсли;
	ПрошлыеВстречи.Параметры.УстановитьЗначениеПараметра("Дата", Объект.Дата);
	ПрошлыеМатчи.Параметры.УстановитьЗначениеПараметра("Дата", НачалоМесяца(ДобавитьМесяц(Объект.Дата, -2)));
	
	ЭтоДерби="ДЕРБИ";
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ГрафикОбновитьНаСервере();
	РаздельноПриИзменении();
	ВидимостьУстановить();
КонецПроцедуры

&НаСервере
Процедура ВидимостьУстановить()
	//Элементы.Анонс.Видимость = (НЕ Объект.Ссылка.Пустая());
	Элементы.Сравнение.Доступность	= (Объект.СоставыКоманд.Количество() > 0);
	Элементы.ЭтоДерби.Видимость		= СерверныйФункции.ЭтоДерби(Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	мКоманды = КомандыПолучить();
	ПрошлыеВстречи.Параметры.УстановитьЗначениеПараметра("Команды", мКоманды);
	ПрошлыеВстречи.Параметры.УстановитьЗначениеПараметра("Дата", Объект.Дата);
	ПрошлыеМатчи.Параметры.УстановитьЗначениеПараметра("Команды", мКоманды);
	ПрошлыеМатчи.Параметры.УстановитьЗначениеПараметра("Дата", НачалоДня(ДобавитьМесяц(Объект.Дата, -12)));
	
	ВидимостьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура СоставыКомандПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если Элемент.ТекущиеДанные.Свойство("Команда")
	И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Команда)
	Тогда
		мСтроки = Объект.Команды.НайтиСтроки(Новый Структура("Команда", Элемент.ТекущиеДанные.Команда));
		Если мСтроки.Количество() = 0 Тогда
			Отказ = Истина;
			
			Сообщить("Игрок из другой команды");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандыПриИзмененииНаСервере()
	Если Объект.СоставыКоманд.Количество() > 0 Тогда
		СписокШиндлера	= Новый Массив;
		мКоманды = Новый Массив;
		Для Каждого тСтрока Из Объект.Команды Цикл
			мКоманды.Добавить(тСтрока.Команда);
			Если тСтрока.НомерСтроки = 1 Тогда
				Объект.Стадион	= СерверныйФункции.Стадион(тСтрока.Команда);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого тСтрока Из Объект.СоставыКоманд Цикл
			Если мКоманды.Найти(тСтрока.Команда) = Неопределено Тогда
				СписокШиндлера.Добавить(тСтрока);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого тЭлемент Из СписокШиндлера Цикл
			Объект.СоставыКоманд.Удалить(тЭлемент);
		КонецЦикла;
		
	Иначе
		Для Каждого тСтрока Из Объект.Команды Цикл
			Если тСтрока.НомерСтроки = 1 Тогда
				Объект.Стадион	= СерверныйФункции.Стадион(тСтрока.Команда);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандыПриИзменении(Элемент)
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Если НЕ ПустаяСтрока(Объект.Стадион) Тогда
			Объект.Стадион = Неопределено;
		КонецЕсли;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Тур) Тогда
		Объект.Тур		= СерверныйФункции.Тур(Элемент.ТекущиеДанные.Команда, Объект.Дата);
	КонецЕсли;
	
	КомандыПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПрошлыеВстречиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элемент.ТекущиеДанные.Матч);
КонецПроцедуры

&НаСервере
Процедура АнонсОбновитьНаСервере()
	Сезон.Очистить();
	Чемпионат = СерверныйФункции.ЧемпионатПолучитьПоследнее(Объект.Команды.Выгрузить().ВыгрузитьКолонку("Команда"));
	//Если ЗначениеЗаполнено(Чемпионат) Тогда
		Для Каждого тСтрока Из Объект.Команды Цикл
			Стат = СерверныйФункции.СтатистикаИгр(тСтрока.Команда, Чемпионат);
			Для Каждого ТекЭлемент Из Стат Цикл
				нСтрока = Сезон.Добавить();
				ЗаполнитьЗначенияСвойств(нСтрока, ТекЭлемент);
				//нСтрока.Ключ	= СтрЗаменить(нСтрока.Ключ, "_", " ");
				нСтрока.Команда	= тСтрока.Команда;
			КонецЦикла;
		КонецЦикла;
	//КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция Стадион(Тим)
	Возврат СерверныйФункции.Стадион(Тим);
КонецФункции

&НаКлиенте
Процедура СтадионНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Объект.Команды.Количество() > 0 Тогда
		Команда = Стадион(Объект.Команды.Получить(0).Команда);
		Если ЗначениеЗаполнено(Команда) И Элемент.СписокВыбора.НайтиПоЗначению(Команда) = Неопределено Тогда
			Элемент.СписокВыбора.Добавить(Команда);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоставыКомандИгрокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Тимз = Новый Массив;
	Для Каждого ТекСтрока Из Объект.Команды Цикл
		Тимз.Добавить(ТекСтрока.Команда);
	КонецЦикла;
	Игроки.ЗагрузитьЗначения(ИгрокиПолучить(Тимз));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИгрокиПолучить(Тимз)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Перемещения.Игрок КАК Игрок,
	                      |	Перемещения.Команда КАК Команда
	                      |ИЗ
	                      |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(
	                      |			,
	                      |			Игрок.ПометкаУдаления = ЛОЖЬ
	                      |				И Команда В (&Команда)) КАК Перемещения
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Команда");
	Запрос.УстановитьПараметр("Команда",	Тимз);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
КонецФункции
