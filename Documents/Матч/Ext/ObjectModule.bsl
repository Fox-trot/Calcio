Перем Хозяева, Гости, КоличествоОчков;


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СчетУстановить(Команды, ?(Проведен, РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписи = РежимЗаписиДокумента.Проведение));
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли НЕ ЗначениеЗаполнено(Стадион) Тогда
		ОбщегоНазначения.УстановитьЗначение(КоличествоЗрителей, 0);
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение И СоставыКоманд.Количество() > 0 Тогда
		СписокШиндлера	= Новый Массив;
		Для Каждого тСтрока Из СоставыКоманд Цикл
			Если Команды.НайтиСтроки(Новый Структура("Команда", тСтрока.Команда)).Количество() = 0 Тогда
				СписокШиндлера.Добавить(тСтрока);
			КонецЕсли;
		КонецЦикла;
		Для Каждого тСтрока Из СписокШиндлера Цикл
			СоставыКоманд.Удалить(тСтрока);
		КонецЦикла;
		
		СоставыКоманд.Свернуть("Игрок,Команда");
	КонецЕсли;
КонецПроцедуры

Процедура СчетУстановить(Команды, Проведение) Экспорт
	Если Команды.Количество() = 2 Тогда
		Хозяева	= Команды.Получить(0);
		Гости	= Команды.Получить(1);
		
		Если ?(ЗначениеЗаполнено(Тур), НЕ Тур.ОлимпийскаяСистема, Ложь) Тогда
			Хозяева.Пенальти	= 0;
			Гости.Пенальти		= 0;
		КонецЕсли;
		
		Если Проведение Тогда
			Если Хозяева.Пенальти + Гости.Пенальти = 0 Тогда
				Счет = СтрШаблон("%1 - %2 %3:%4", Хозяева.Команда, Гости.Команда, Формат(Хозяева.КоличествоГолов, "ЧН=0; ЧГ=0"), Формат(Гости.КоличествоГолов, "ЧН=0; ЧГ=0"));
			Иначе
				Счет = СтрШаблон("%1 - %2 %3:%4 (%5:%6)", Хозяева.Команда, Гости.Команда, Формат(Хозяева.КоличествоГолов, "ЧН=0; ЧГ=0"), Формат(Гости.КоличествоГолов, "ЧН=0; ЧГ=0"), Формат(Хозяева.Пенальти, "ЧН=0; ЧГ=0"), Формат(Гости.Пенальти, "ЧН=0; ЧГ=0"));
			КонецЕсли;
			КоличествоОчков = Справочники.ВидыСпорта.КоличествоОчков(Хозяева.Команда.Владелец);
		Иначе
			Счет = СтрШаблон("%1 - %2", Хозяева.Команда, Гости.Команда);
		КонецЕсли;
		
	ИначеЕсли Команды.Количество() = 1 Тогда
		Счет = СокрЛП(Команды.Получить(0).Команда) + " - ??";
	Иначе
		Счет = "Матч";
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Команды.Количество() = 2 Тогда
		Хозяева	= Команды.Получить(0);
		Гости	= Команды.Получить(1);
		Если Хозяева.Команда = Гости.Команда Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Неверно указаны команды", Отказ, Ссылка);
		КонецЕсли;
	Иначе
		ОбщегоНазначения.СообщитьОбОшибке("Неверно указаны команды", Отказ, Ссылка);
	КонецЕсли;
	Если Отказ Тогда
		//
	ИначеЕсли ЗначениеЗаполнено(Тур) Тогда
		мКоманды	= Команды.Выгрузить().ВыгрузитьКолонку("Команда");
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
		                      |	ТурнирнаяТаблица.Регистратор КАК Ссылка
		                      |ИЗ
		                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		                      |ГДЕ
		                      |	ТурнирнаяТаблица.Тур = &Тур
		                      |	И ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ЛОЖЬ
		                      |	И ТурнирнаяТаблица.Команда В(&Команды)
		                      |	И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч");
		Запрос.УстановитьПараметр("Команды",	мКоманды);
		Запрос.УстановитьПараметр("Тур",		Тур);
		Выборка	= Запрос.Выполнить().Выбрать();
		Пока НЕ Отказ И Выборка.Следующий() Цикл
			Если Выборка.Ссылка <> Ссылка Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрШаблон("Похожий матч уже есть в базе (%1) %2", Выборка.Ссылка, Тур), Отказ, Выборка.Ссылка);
			КонецЕсли;
		КонецЦикла;
	Иначе
		мКоманды	= Команды.Выгрузить().ВыгрузитьКолонку("Команда");
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
		                      |	ТурнирнаяТаблица.Регистратор КАК Ссылка
		                      |ИЗ
		                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		                      |ГДЕ
		                      |	ТурнирнаяТаблица.Период МЕЖДУ &Дата1 И &Дата2
		                      |	И ТурнирнаяТаблица.Команда В(&Команды)
		                      |	И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч");
		Запрос.УстановитьПараметр("Команды",	мКоманды);
		Запрос.УстановитьПараметр("Дата1",		НачалоДня(Дата - Сутки()));
		Запрос.УстановитьПараметр("Дата2",		КонецДня(Дата + Сутки()));
		Выборка	= Запрос.Выполнить().Выбрать();
		Пока НЕ Отказ И Выборка.Следующий() Цикл
			Если Выборка.Ссылка <> Ссылка Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрШаблон("Похожий матч уже есть в базе (%1)", Выборка.Ссылка), Отказ, Выборка.Ссылка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Если НачалоДня(Дата) > НачалоДня(ТекущаяДатаСеанса()) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Дата неверна", Отказ, Ссылка);
		Возврат;
	КонецЕсли;
	
	Для Каждого тСтрока Из Команды Цикл
		Запись = Движения.ТурнирнаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, тСтрока);
		Запись.Период			= НачалоДня(Дата);
		Запись.Тур				= Тур;
		Запись.КоличествоОчков	= КоличествоОчковРасчитать(тСтрока.НомерСтроки > 1, КоличествоОчков);
	КонецЦикла;
	Движения.ТурнирнаяТаблица.Записать();
КонецПроцедуры

Функция КоличествоОчковРасчитать(Секондо, КоличествоОчков)
	Если Хозяева.КоличествоГолов + Хозяева.Пенальти < Гости.КоличествоГолов + Гости.Пенальти Тогда
		Возврат	?(Секондо, КоличествоОчков.Победа, 0);
	ИначеЕсли Хозяева.КоличествоГолов + Хозяева.Пенальти > Гости.КоличествоГолов + Гости.Пенальти Тогда
		Возврат	?(Секондо, 0, КоличествоОчков.Победа);
	КонецЕсли;
	Возврат КоличествоОчков.Ничья;
КонецФункции

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Если ДанныеЗаполнения.Свойство("Команды") Тогда
			Клуб	= Неопределено;
			Для Каждого Команда Из ДанныеЗаполнения.Команды Цикл
				Если ТипЗнч(Команда) = Тип("СправочникСсылка.Команды") Тогда
					ЗаполнитьЗначенияСвойств(Команды.Добавить(), Новый Структура("Команда", Команда));
					Если Клуб = Неопределено Тогда
						Клуб	= Команда;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ЗначениеЗаполнено(Стадион) И ЗначениеЗаполнено(Клуб) Тогда
				Стадион			= СтадионНайти(Клуб);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Туры") Тогда
		Тур		= ДанныеЗаполнения;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Стадионы") Тогда
		Стадион	= ДанныеЗаполнения;
		КомандуДобавить(ДанныеЗаполнения.Владелец);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Команды") Тогда
		Стадион	= СтадионНайти(ДанныеЗаполнения);
		КомандуДобавить(ДанныеЗаполнения);
	КонецЕсли;

	Если ЗначениеЗаполнено(Тур) Тогда
		ТурДата	= ДатаНачала(Тур);
		Если Дата < НачалоДня(ТурДата) Тогда
			Дата = ТурДата;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Счет				= "";
	КоличествоЗрителей	= 0;
	Стадион				= Неопределено;
	Дата				= ?(ЗначениеЗаполнено(Тур), Тур.ДатаОкончания, ТекущаяДатаСеанса());
	
	Для Каждого тСтрока Из Команды Цикл
		тСтрока.КоличествоГолов = 0;
	КонецЦикла;
	Если Команды.Количество() = 2 Тогда
		Темпо	= Команды.Получить(1).Команда;
		Команды.Получить(1).Команда	= Команды.Получить(0).Команда;
		Команды.Получить(0).Команда	= Темпо;
		
		Стадион	= СтадионНайти(Темпо);
	КонецЕсли;
	СоставыКоманд.Очистить();
КонецПроцедуры

Функция СтадионНайти(Параметр, Все=Ложь) Экспорт
	Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		Если ЭтотМатчТурФинал() Тогда
			Возврат СерверныйФункции.СтадионНайти(Параметр, Истина);
		Иначе
			Возврат СерверныйФункции.СтадионНайти(Параметр.Команда, Истина);
		КонецЕсли;
	КонецЕсли;
	Возврат СерверныйФункции.СтадионНайти(Параметр, Все);
КонецФункции

Функция ДатаНачала(Тур)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	МАКСИМУМ(ЕСТЬNULL(ТурнирнаяТаблица.Период, Туры.ДатаНачала)) КАК Дата
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ПО Туры.Ссылка = ТурнирнаяТаблица.Тур
	                      |ГДЕ
	                      |	Туры.Ссылка = &Тур");
	Запрос.УстановитьПараметр("Тур",				Тур);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Дата;
	КонецЕсли;
	Возврат ТекущаяДатаСеанса();
КонецФункции

Процедура КомандуДобавить(ДанныеЗаполнения)
	Если Команды.Количество() > 0 Тогда
		Команды.Очистить();
	КонецЕсли;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Команды") Тогда
		ЗаполнитьЗначенияСвойств(Команды.Добавить(), Новый Структура("Команда", ДанныеЗаполнения));
	КонецЕсли;
КонецПроцедуры

Функция ЭтотМатчТурФинал() Экспорт
	Возврат Тур.ОлимпийскаяСистема И СерверныйФункции.ЭтотМатчТурФинал(Тур);
КонецФункции
