
Процедура Обновить(Ссылка, Замещать=Ложь) Экспорт
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Чемпионаты") Тогда
		Туры	= СерверныйФункции.ТурыЧемпионата(Ссылка, Истина);
		Для Каждого Элемент Из Туры Цикл
			Обновить(Элемент, Замещать);
		КонецЦикла;
		
	ИначеЕсли Ссылка.ОлимпийскаяСистема Тогда
		Очистить(Ссылка);
	ИначеЕсли ЗначениеЗаполнено(Ссылка) Тогда
		Если Замещать Тогда
			Очистить(Ссылка);
		КонецЕсли;
		Записать(Ссылка);
	КонецЕсли;
КонецПроцедуры

Процедура Очистить(Тур) Экспорт
	НЗ		= РегистрыСведений.ИтогиПромежуточные.СоздатьНаборЗаписей();
	НЗ.Отбор.Тур.Установить(Тур);
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 Тогда
		НЗ.Очистить();
		НЗ.ОбменДанными.Загрузка	= Истина;
		НЗ.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура Записать(Тур)
	Выборка	= ТурнирнаяТаблица(Тур);
	Гель	= Новый Структура("Период,Тур,Место", Тур.ДатаОкончания, Тур, 1);
	Пока Выборка.Следующий() Цикл
		НЗ		= РегистрыСведений.ИтогиПромежуточные.СоздатьНаборЗаписей();
		НЗ.Отбор.Тур.Установить(Тур);
		НЗ.Отбор.Команда.Установить(Выборка.Команда);
		НЗ.Прочитать();
		Если НЗ.Количество() = 0 Тогда
			Запись	= НЗ.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			ЗаполнитьЗначенияСвойств(Запись, Гель);
		Иначе
			Для Каждого Запись Из НЗ Цикл
				ОбщегоНазначения.УстановитьЗначение(Запись.Период,				Гель.Период);
				ОбщегоНазначения.УстановитьЗначение(Запись.Место,				Гель.Место);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоОчков,		Выборка.КоличествоОчков);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоИгр,		Выборка.КоличествоИгр);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоВыигрыш,	Выборка.КоличествоВыигрыш);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоНичья,		Выборка.КоличествоНичья);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоПроигрыш,	Выборка.КоличествоПроигрыш);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоЗабито,	Выборка.КоличествоЗабито);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоПропущено,	Выборка.КоличествоПропущено);
			КонецЦикла;
		КонецЕсли;
		Если НЗ.Модифицированность() Тогда
			НЗ.ОбменДанными.Загрузка	= Истина;
			НЗ.Записать();
		КонецЕсли;
		
		Гель.Место	= Гель.Место + 1;
	КонецЦикла;
КонецПроцедуры

Функция ТурнирнаяТаблица(Тур)
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТурнирнаяТаблица.Тур.Владелец КАК Владелец,
	|	ТурнирнаяТаблица.Тур.Код КАК Код
	|ПОМЕСТИТЬ Туры
	|ИЗ
	|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	|ГДЕ
	|	ТурнирнаяТаблица.Тур = &Тур
	|	И ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧемпионатыКоманды.Команда КАК Команда,
	|	СУММА(ВЫБОР
	|			КОГДА ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоИгр,
	|	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) КАК КоличествоЗабито,
	|	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоОчков), 0) КАК КоличествоОчков,
	|	СУММА(ВЫБОР
	|			КОГДА ТурнирнаяТаблица.КоличествоОчков > Противник.КоличествоОчков
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоВыигрыш,
	|	СУММА(ВЫБОР
	|			КОГДА ТурнирнаяТаблица.КоличествоОчков = Противник.КоличествоОчков
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоНичья,
	|	СУММА(ВЫБОР
	|			КОГДА ТурнирнаяТаблица.КоличествоОчков < Противник.КоличествоОчков
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПроигрыш,
	|	ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) КАК КоличествоПропущено
	|ИЗ
	|	Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Туры КАК Туры
	|			ПО ТурнирнаяТаблица.Тур.Владелец = Туры.Владелец
	|				И ТурнирнаяТаблица.Тур.Код <= Туры.Код
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	|			ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	|				И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	|		ПО ЧемпионатыКоманды.Команда = ТурнирнаяТаблица.Команда
	|ГДЕ
	|	ЧемпионатыКоманды.Ссылка В
	|			(ВЫБРАТЬ
	|				Туры.Владелец
	|			ИЗ
	|				Туры КАК Туры)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЧемпионатыКоманды.Команда
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоОчков УБЫВ,
	|	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) - ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) УБЫВ,
	|	КоличествоЗабито УБЫВ,
	|	КоличествоВыигрыш УБЫВ,
	|	Команда
	|АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Тур",			Тур);
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции
