
Процедура Обновить(Чемпионат, Замещать=Ложь) Экспорт
	Если Чемпионат.Владелец.ОлимпийскаяСистема Тогда
		Очистить(Чемпионат);
	ИначеЕсли Замещать Или ЧемпионатЗакончен(Чемпионат) Тогда
		Если Замещать Тогда
			Очистить(Чемпионат);
		КонецЕсли;
		Записать(Чемпионат);
	КонецЕсли;
КонецПроцедуры

Процедура Очистить(Чемпионат) Экспорт
	НЗ		= РегистрыСведений.Итоги.СоздатьНаборЗаписей();
	НЗ.Отбор.Чемпионат.Установить(Чемпионат);
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 Тогда
		НЗ.Очистить();
		НЗ.ОбменДанными.Загрузка	= Истина;
		НЗ.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура Записать(Чемпионат)
	Выборка	= СерверныйФункции.ТурнирнаяТаблица(Чемпионат);
	Гель	= Новый Структура("Период,Чемпионат,Место", Чемпионат.ДатаОкончания, Чемпионат, 1);
	Пока Выборка.Следующий() Цикл
		НЗ		= РегистрыСведений.Итоги.СоздатьНаборЗаписей();
		НЗ.Отбор.Чемпионат.Установить(Чемпионат);
		НЗ.Отбор.Команда.Установить(Выборка.Команда);
		НЗ.Прочитать();
		Если НЗ.Количество() = 0 Тогда
			Запись	= НЗ.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			ЗаполнитьЗначенияСвойств(Запись, Гель);
		Иначе
			Для Каждого Запись Из НЗ Цикл
				ОбщегоНазначения.УстановитьЗначение(Запись.Период,				Гель.Период);
				ОбщегоНазначения.УстановитьЗначение(Запись.Место,				Гель.Место);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоОчков,		Выборка.КоличествоОчков);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоЗабито,	Выборка.КоличествоЗабито);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоПропущено,	Выборка.КоличествоПропущено);
			КонецЦикла;
		КонецЕсли;
		Если НЗ.Модифицированность() Тогда
			НЗ.ОбменДанными.Загрузка	= Истина;
			НЗ.Записать();
		КонецЕсли;
		
		Гель.Место	= Гель.Место + 1;
	КонецЦикла;
КонецПроцедуры

Функция ЧемпионатЗакончен(Чемпионат)
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда Возврат Ложь;
	ИначеЕсли Чемпионат.ДатаОкончания > ТекущаяДатаСеанса() Тогда Возврат Ложь;
	КонецЕсли;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ВТ.Количество КАК Количество
	      	               |ИЗ
	      	               |	(ВЫБРАТЬ
	      	               |		МАКСИМУМ(ТурнирнаяТаблица.Период) КАК Количество
	      	               |	ИЗ
	      	               |		РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |	ГДЕ
	      	               |		ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |		И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	      	               |	
	      	               |	ИМЕЮЩИЕ
	      	               |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) = (КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) - 1) * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда)) КАК ВТ
	      	               |ГДЕ
	      	               |	(ВТ.Количество ЕСТЬ NULL) = ЛОЖЬ");
	Если Чемпионат.Владелец.ОлимпийскаяСистема Тогда
		Запрос.Текст	= "ВЫБРАТЬ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) КАК Команда,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур) КАК Тур,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК Регистратор,
		            	  |	МАКСИМУМ(ТурнирнаяТаблица.Период) КАК Период
		            	  |ПОМЕСТИТЬ Последнее
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Отбор
		            	  |		ПО ТурнирнаяТаблица.Тур.Владелец = Отбор.Тур.Владелец
		            	  |			И (Отбор.Тур.ОлимпийскаяСистема = ЛОЖЬ)
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |	И ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ИСТИНА
		            	  |
		            	  |ИМЕЮЩИЕ
		            	  |	МАКСИМУМ(ТурнирнаяТаблица.Период) >= МАКСИМУМ(ЕСТЬNULL(Отбор.Период, ТурнирнаяТаблица.Период))
		            	  |;
		            	  |
		            	  |////////////////////////////////////////////////////////////////////////////////
		            	  |ВЫБРАТЬ
		            	  |	ТурнирнаяТаблица.Команда КАК Команда
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последнее КАК Последнее
		            	  |		ПО ТурнирнаяТаблица.Период = Последнее.Период
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
						  //|	И (ТурнирнаяТаблица.Команда, Последнее.Тур) В
						  //|			(ВЫБРАТЬ
						  //|				ТурнирнаяТаблица.Команда,
						  //|				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур)
						  //|			ИЗ
						  //|				РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
						  //|			ГДЕ
						  //|				ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
						  //|			СГРУППИРОВАТЬ ПО
						  //|				ТурнирнаяТаблица.Команда)
						  //|	И Последнее.Регистратор >= Последнее.Команда
		            	  |
		            	  |СГРУППИРОВАТЬ ПО
		            	  |	ТурнирнаяТаблица.Команда
		            	  |
		            	  |ИМЕЮЩИЕ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) = 1";
	КонецЕсли;
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции
