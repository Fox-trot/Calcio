
Процедура Обновить(Чемпионат, Замещать=Ложь) Экспорт
	Если Замещать Тогда
		Очистить(Чемпионат);
	КонецЕсли;
	Если ЧемпионатЗакончен(Чемпионат) Тогда
		Записать(Чемпионат);
	КонецЕсли;
КонецПроцедуры

Процедура Очистить(Чемпионат) Экспорт
	НЗ		= РегистрыСведений.Итоги.СоздатьНаборЗаписей();
	НЗ.Отбор.Чемпионат.Установить(Чемпионат);
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 Тогда
		НЗ.Очистить();
		НЗ.ОбменДанными.Загрузка	= Истина;
		НЗ.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура Записать(Чемпионат)
	Выборка		= СерверныйФункции.ТурнирнаяТаблица(Чемпионат);
	Олимпийская	= Чемпионат.Владелец.ОлимпийскаяСистема;
	Гель		= Новый Структура("Период,Чемпионат,Место", Чемпионат.ДатаОкончания, Чемпионат, 1);
	Пока Выборка.Следующий() Цикл
		НЗ		= РегистрыСведений.Итоги.СоздатьНаборЗаписей();
		НЗ.Отбор.Чемпионат.Установить(Чемпионат);
		НЗ.Отбор.Команда.Установить(Выборка.Команда);
		НЗ.Прочитать();
		Если НЗ.Количество() = 0 Тогда
			Запись	= НЗ.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			ЗаполнитьЗначенияСвойств(Запись, Гель);
		Иначе
			Для Каждого Запись Из НЗ Цикл
				ОбщегоНазначения.УстановитьЗначение(Запись.Период,				Гель.Период);
				ОбщегоНазначения.УстановитьЗначение(Запись.Место,				Гель.Место);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоОчков,		Выборка.КоличествоОчков);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоЗабито,	Выборка.КоличествоЗабито);
				ОбщегоНазначения.УстановитьЗначение(Запись.КоличествоПропущено,	Выборка.КоличествоПропущено);
			КонецЦикла;
		КонецЕсли;
		Если НЗ.Модифицированность() Тогда
			НЗ.ОбменДанными.Загрузка	= Истина;
			НЗ.Записать();
		КонецЕсли;
		
		Если Олимпийская Тогда
			Прервать;
		КонецЕсли;
		Гель.Место	= Гель.Место + 1;
	КонецЦикла;
КонецПроцедуры

Функция ЧемпионатЗакончен(Чемпионат)
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда Возврат Ложь;
	ИначеЕсли Чемпионат.ДатаОкончания > ТекущаяДатаСеанса() Тогда Возврат Ложь;
	КонецЕсли;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ВТ.Факт КАК Количество
	      	               |ИЗ
	      	               |	(ВЫБРАТЬ
	      	               |		ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор), 0) КАК Факт,
	      	               |		(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) - 1) * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) КАК План
	      	               |	ИЗ
	      	               |		РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |	ГДЕ
	      	               |		ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |		И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч) КАК ВТ
	      	               |ГДЕ
	      	               |	ВТ.Факт > 0
	      	               |	И ВЫБОР
	      	               |			КОГДА ВТ.Факт = ВТ.План
	      	               |				ТОГДА ИСТИНА
	      	               |			КОГДА ВТ.Факт > ВТ.План
	      	               |				ТОГДА ЦЕЛ(ВТ.Факт / 2) * 2 = ВТ.Факт
	      	               |			ИНАЧЕ ЛОЖЬ
	      	               |		КОНЕЦ");
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Если Чемпионат.Владелец.ОлимпийскаяСистема Тогда
		Запрос.Текст	= "ВЫБРАТЬ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) КАК Команда,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК Регистратор,
		            	  |	МИНИМУМ(Финал.Регистратор) КАК ФиналРегистратор,
		            	  |	МИНИМУМ(Финал.Команда) КАК ФиналКоманда
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		            	  |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК Регистратор,
		            	  |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) КАК Команда
		            	  |		ИЗ
		            	  |			РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ГДЕ
		            	  |			ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |		
		            	  |		СГРУППИРОВАТЬ ПО
		            	  |			ТурнирнаяТаблица.Тур) КАК Финал
		            	  |		ПО (ИСТИНА)
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |	И ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ИСТИНА";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И Выборка.ФиналРегистратор = 1 И Выборка.ФиналКоманда = 2 Тогда
			Если Выборка.Команда = 32 И Выборка.Регистратор >= 31 Тогда
				Возврат Истина;
			ИначеЕсли Выборка.Команда = 64 И Выборка.Регистратор >= 63 Тогда
				Возврат Истина;
			ИначеЕсли Выборка.Команда = 2 И Выборка.Регистратор = 1 Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции
