
&НаКлиенте
Процедура ТикТак() Экспорт
	Таймер = Финиш - ТекущаяДата();
	Если Финиш <= ТекущаяДата() Тогда
		Стоп(3);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Дальше() Экспорт
	Было.Добавить(ТекущийВопрос);
	
	ОсталосьВопросов	= ОсталосьВопросов - 1;
	
	Если ОсталосьВопросов = 0 Тогда
		Стоп();
	ИначеЕсли ВопросУстановить() Тогда
		Таймер 	= Финиш - ТекущаяДата();
		ОтветыОбновить();
	Иначе
		Стоп();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВопросУстановить()
	ЭтоФиниш = Истина;
	Для Каждого Элемент Из Вопросы Цикл
		Если ПустаяСтрока(Элемент.Представление) Тогда
			ТекущийВопрос	= Элемент.Значение;
			ВопросТекст		= ПолноеНаименование(ТекущийВопрос);
			ЭтоФиниш = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЭтоФиниш Тогда
		ТекущийВопрос	= Неопределено;
		ВопросТекст		= "";
	КонецЕсли;
	Возврат ЗначениеЗаполнено(ТекущийВопрос);
КонецФункции

&НаСервере
Функция ИзображениеПолучить(Ссылка)
	Возврат СерверныйФункции.ИзображениеПолучить(Ссылка, Ложь);
КонецФункции

&НаСервереБезКонтекста
Функция ПолноеНаименование(Ссылка)
	Возврат Ссылка.ПолноеНаименование();
КонецФункции

&НаСервере
Процедура ОтветыОбновить()
	ГС		= Новый ГенераторСлучайныхЧисел;

	Реквизиты = Новый Массив;
	Реквизиты.Добавить("Ответ0");
	Реквизиты.Добавить("Ответ1");
	Реквизиты.Добавить("Ответ2");
	Реквизиты.Добавить("Ответ3");
	Реквизиты.Добавить("Ответ4");
	Реквизиты.Добавить("Ответ5");
	
	Вопросник	= ГС.СлучайноеЧисло(0, Реквизиты.ВГраница());
	Пронто		= Новый Массив;
	Для Индекс = 0 По Реквизиты.ВГраница() Цикл
		Реквизит	= Реквизиты.Получить(Индекс);
		Если Вопросник = Индекс Тогда
			Изо	= ИзображениеПолучить(ТекущийВопрос);
			ЗаполнитьЗначенияСвойств(ЭтаФорма, Новый Структура(Реквизит, Изо));
			Элемент = Вопросы.НайтиПоЗначению(ТекущийВопрос);
			Элемент.Представление	= Изо;
		Иначе
			Виктим		= Неопределено;
			Ответы.СортироватьПоПредставлению();
			Итератор	= Вопросы.Количество();
			Пока Виктим = Неопределено Цикл
				Для Каждого Элемент Из Ответы Цикл
					Если Пронто.Найти(Элемент.Значение) <> Неопределено Тогда
						//
					ИначеЕсли Элемент.Значение = ТекущийВопрос Тогда
						//
					ИначеЕсли Итератор <= Вопросник Тогда
						Виктим	= Элемент;
						Прервать;
					КонецЕсли;
					Итератор = Итератор - 1.7 * Макс(1, Секунда(ТекущаяДата()));
				КонецЦикла;
			КонецЦикла;
			Изо	= ИзображениеПолучить(Виктим.Значение);
			ЗаполнитьЗначенияСвойств(ЭтаФорма, Новый Структура(Реквизит, Изо));
			Виктим.Представление = Изо;
			
			Пронто.Добавить(Виктим.Значение);
		КонецЕсли;
		Элементы.Найти(Реквизит).ПропускатьПриВводе	= Истина;
		Элементы.Найти(Реквизит).Видимость			= Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВидимостьУстановить(ЭтоСтарт=Ложь)
	Элементы.СтраницаНачало.Видимость	= (Шаг < 1);
	Элементы.СтраницаНачало.Доступность	= (Шаг < 1);
	Элементы.СтраницаТест.Видимость		= (Шаг = 1);
	Элементы.СтраницаТест.Доступность	= (Шаг = 1);
	Элементы.СтраницаФиниш.Видимость	= (Шаг > 1);
	Элементы.СтраницаФиниш.Доступность	= (Шаг > 1);
	Если ЭтоСтарт Тогда
		Элементы.ГруппаПараметры.ТолькоПросмотр = Истина;
		ДатаНачало	= ТекущаяДата();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Стоп(ШагСледующий=2)
	ОтключитьОбработчикОжидания("ТикТак");
	
	Если ШагСледующий = 2 Тогда
		Затрачено			= ТекущаяДата() - ДатаНачало;
		ЗатраченоВремени	= РазницаВоВремени(Затрачено);
	Иначе
		Затрачено			= -1;
		ЗатраченоВремени	= "Прервано";
	КонецЕсли;
	Шаг		= ШагСледующий;
	СтопНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтветНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка	= Ложь;
	Если Элемент.ПропускатьПриВводе Тогда
		Элемент.ПропускатьПриВводе	= Ложь;
		
		Вопрос		= Вопросы.НайтиПоЗначению(ТекущийВопрос).Представление;
		Если СтрСравнить(Вычислить(Элемент.Имя), Вопрос) = 0 Тогда
			ПравильныхОтветов	= ПравильныхОтветов + 1;
			Исключения.Добавить(ТекущийВопрос);
		КонецЕсли;
		Элементы.Ответ0.Видимость	= (Ответ0 = Вопрос);
		Элементы.Ответ1.Видимость	= (Ответ1 = Вопрос);
		Элементы.Ответ2.Видимость	= (Ответ2 = Вопрос);
		Элементы.Ответ3.Видимость	= (Ответ3 = Вопрос);
		Элементы.Ответ4.Видимость	= (Ответ4 = Вопрос);
		Элементы.Ответ5.Видимость	= (Ответ5 = Вопрос);
		
		ПодключитьОбработчикОжидания("Дальше", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция РазницаВоВремени(Знач Количество, Знач Степ=2)
	Степ = Степ - 1;
	Ответ = "";
	Если Количество = 0 Или Степ < 0 Тогда
	ИначеЕсли Количество < 60 Тогда
		Ответ = Строка(Количество) + " сек";
	ИначеЕсли Количество < 3600 Тогда
		Ответ = Строка(Цел(Количество/60)) + " мин " + РазницаВоВремени(Количество-Цел(Количество/60)*60, Степ);
	Иначе
		Ответ = Строка(Цел(Количество/3600)) + " час " + РазницаВоВремени(Количество-Цел(Количество/3600)*3600, Степ);
	КонецЕсли;
	Возврат СокрП(Ответ);
КонецФункции

&НаСервереБезКонтекста
Функция Цвет(Аларм=Ложь)
	Возврат ?(Аларм, ЦветаСтиля.ЦветОтрицательногоЧисла, ЦветаСтиля.ЦветТекстаФормы);
КонецФункции

&НаСервере
Процедура СтопНаСервере()
	Элементы.ПравильныхОтветов.ЦветТекста	= Цвет();
	Элементы.ЗатраченоВремени.ЦветТекста	= Цвет();
	Если ПравильныхОтветов = КоличествоВопросов Тогда
		Элементы.ПравильныхОтветов.ЦветТекста = Цвет(Истина);
		Если Затрачено <= Макс(30, 2.8 * КоличествоВопросов - Уровень) Тогда
			Элементы.ЗатраченоВремени.ЦветТекста = Цвет(Истина);
			Для Каждого Элемент Из Ответы Цикл
				Если Исключения.НайтиПоЗначению(Элемент.Значение) = Неопределено Тогда
					Исключения.Добавить(Элемент.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ПравильныхОтветов = 0 Тогда
		Элементы.ПравильныхОтветов.ЦветТекста = Цвет(Истина);
	КонецЕсли;
	ВидимостьУстановить();
КонецПроцедуры

&НаСервере
Функция ЗапросИнит()
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 66
	                      |	ТурнирнаяТаблица.Команда КАК Ссылка,
	                      |	ЛОЖЬ КАК МояКоманда
	                      |ИЗ
	                      |	РегистрСведений.Итоги КАК ТурнирнаяТаблица
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Период >= &Дата
	                      |	И РАЗМЕРХРАНИМЫХДАННЫХ(ТурнирнаяТаблица.Команда.Изображение) >= 64
	                      |	И ТурнирнаяТаблица.Место <= 6
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ТурнирнаяТаблица.Команда В (&Было),
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Место) <= &ЧислоСлевина");
	Если ЗначениеЗаполнено(МояКоманда) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТурнирнаяТаблица.Команда КАК Ссылка,
		               |	ЛОЖЬ КАК МояКоманда
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Моя
		               |		ПО ТурнирнаяТаблица.Регистратор = Моя.Регистратор
		               |ГДЕ
		               |	ТурнирнаяТаблица.Период >= &Дата
		               |	И ВЫБОР
		               |			КОГДА ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ИСТИНА
		               |				ТОГДА ТурнирнаяТаблица.Тур.Владелец.Владелец.Страна ЕСТЬ NULL
		               |			ИНАЧЕ ИСТИНА
		               |		КОНЕЦ
		               |	И РАЗМЕРХРАНИМЫХДАННЫХ(ТурнирнаяТаблица.Команда.Изображение) >= 64
		               |	И Моя.Команда = &Команда
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Команда
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТурнирнаяТаблица.Команда В (&Было),
		               |	МАКСИМУМ(ТурнирнаяТаблица.КоличествоОчков = &ЧислоСлевина) УБЫВ";
	КонецЕсли;
	Запрос.УстановитьПараметр("Дата",				ДобавитьМесяц(ТекущаяДатаСеанса(), -42));
	Запрос.УстановитьПараметр("Команда",			МояКоманда);
	Запрос.УстановитьПараметр("Было",				Было);
	Запрос.УстановитьПараметр("Исключения",			Исключения);
	Запрос.УстановитьПараметр("ЧислоСлевина",		0);
	Возврат Запрос;
КонецФункции

&НаСервере
Функция ВопросыОтветыПолучить()
	ГС	= Новый ГенераторСлучайныхЧисел;
	
	Запрос = ЗапросИнит();
	Запрос.УстановитьПараметр("ЧислоСлевина",		ГС.СлучайноеЧисло(0, 3));
	Если Уровень = 1 И ГС.СлучайноеЧисло(0, 2) = 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 66
		               |	Команды.Ссылка КАК Ссылка,
		               |	Команды.Ссылка = &Команда КАК МояКоманда
		               |ИЗ
		               |	Справочник.Команды КАК Команды
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ПО Команды.Ссылка = ТурнирнаяТаблица.Команда
		               |ГДЕ
		               |	Команды.ПометкаУдаления = ЛОЖЬ
		               |	И РАЗМЕРХРАНИМЫХДАННЫХ(Команды.Изображение) >= 64
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Команды.Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Команды.Ссылка В (&Было),
		               |	МАКСИМУМ(НАЧАЛОПЕРИОДА(ТурнирнаяТаблица.Период, ГОД))";
	ИначеЕсли Уровень = 1 Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 66
		               |	Команды.Ссылка КАК Ссылка,
		               |	Команды.Ссылка = &Команда КАК МояКоманда
		               |ИЗ
		               |	Справочник.Команды КАК Команды
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ПО Команды.Ссылка = ТурнирнаяТаблица.Команда
		               |ГДЕ
		               |	Команды.ПометкаУдаления = ЛОЖЬ
		               |	И РАЗМЕРХРАНИМЫХДАННЫХ(Команды.Изображение) >= 64
		               |	И (Команды.Ссылка В (&Исключения)) = ЛОЖЬ
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Команды.Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Команды.Ссылка В (&Было),
		               |	ЕСТЬNULL(МАКСИМУМ(НАЧАЛОПЕРИОДА(ТурнирнаяТаблица.Период, КВАРТАЛ)), &Дата) УБЫВ";
	ИначеЕсли Уровень = 2 Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 66
		               |	Команды.Ссылка КАК Ссылка,
		               |	Команды.Ссылка = &Команда КАК МояКоманда
		               |ИЗ
		               |	Справочник.Команды КАК Команды
		               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			ТурнирнаяТаблица.Команда КАК Команда,
		               |			ЦЕЛ(СУММА(ТурнирнаяТаблица.КоличествоОчков) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГОД(ТурнирнаяТаблица.Период))) КАК Количество
		               |		ИЗ
		               |			РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		
		               |		СГРУППИРОВАТЬ ПО
		               |			ТурнирнаяТаблица.Команда
		               |		
		               |		ИМЕЮЩИЕ
		               |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Период) >= 2) КАК ТурнирнаяТаблица
		               |		ПО Команды.Ссылка = ТурнирнаяТаблица.Команда
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПеремещенияИгроков КАК ПеремещенияИгроков
		               |		ПО Команды.Ссылка = ПеремещенияИгроков.Команда
		               |ГДЕ
		               |	Команды.ПометкаУдаления = ЛОЖЬ
		               |	И РАЗМЕРХРАНИМЫХДАННЫХ(Команды.Изображение) >= 64
		               |	И (Команды.Ссылка В (&Исключения)) = ЛОЖЬ
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Команды.Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Команды.Ссылка В (&Было),
		               |	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПеремещенияИгроков.Игрок), &ЧислоСлевина) + ЕСТЬNULL(СУММА(ТурнирнаяТаблица.Количество), &ЧислоСлевина)";
	КонецЕсли;
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() <= КоличествоВопросов * 2 Тогда
		ОчиститьКеш();
		Выборка	= Запрос.Выполнить().Выбрать();
	КонецЕсли;
	Пока Выборка.Следующий() Цикл
		Если Выборка.Количество() > 3 * КоличествоВопросов И ГС.СлучайноеЧисло(0, 3) = 0 Тогда
			Продолжить;
		ИначеЕсли Вопросы.Количество() < КоличествоВопросов И НЕ Выборка.МояКоманда Тогда
			Вопросы.Добавить(Выборка.Ссылка);
			Если Уровень > 0 И ГС.СлучайноеЧисло(0, 3) = 0 Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Ответы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Возврат (Ответы.Количество() > КоличествоВопросов);
КонецФункции

&НаСервере
Процедура ОчиститьКеш()
	Было.Очистить();
	Исключения.Очистить();
	Если Уровень = 2 Тогда
		Запрос = ЗапросИнит();
		Исключения.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Старт()
	Вопросы.Очистить();
	Ответы.Очистить();
	Если ВопросыОтветыПолучить() Тогда
		Шаг	= 1;
		Если ВопросУстановить() Тогда
			ПравильныхОтветов	= 0;
			КоличествоВопросов	= Вопросы.Количество();
			ОсталосьВопросов	= КоличествоВопросов;
			Элементы.ОсталосьВопросов.МаксимальноеЗначение	= КоличествоВопросов;
			Таймер				= Макс(60, КоличествоВопросов * (10.1 - 3 * Уровень));
			Элементы.Таймер.МаксимальноеЗначение			= Таймер;
			Финиш				= ТекущаяДата() + Таймер;
			
			ОтветыОбновить();
			ВидимостьУстановить(Истина);
			
			ПодключитьОбработчикОжидания("ТикТак", 3);
		Иначе
			Шаг = 9;
			ВидимостьУстановить();
		КонецЕсли;
		
	ИначеЕсли Исключения.Количество() > 0 Тогда
		ОчиститьКеш();
		
		Старт();
	Иначе
		ОбщегоНазначения.СообщитьОбОшибке("Ошибка при формировании вопросов");
		Элементы.СтраницаНачало.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаСтарт(Команда)
	Старт();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы Тогда
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Шаг = 0 Тогда
		//
	ИначеЕсли Шаг = 1 И ОсталосьВопросов = КоличествоВопросов Тогда
		//
	ИначеЕсли Шаг = 1 Тогда
		Отказ	= Истина;
		Стоп(4);
	ИначеЕсли Шаг <= 3 Тогда
		Отказ	= Истина;
		Шаг		= 0;
		Элементы.КомандаСтарт.Заголовок = "Повторить";
		ВидимостьУстановить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	МояКоманда			= СерверныйФункции.МояКомандаПолучить(Ложь);
	КоличествоВопросов	= Макс(10, КоличествоВопросов);
	Если Уровень = 2 И ЗначениеЗаполнено(МояКоманда) И Исключения.НайтиПоЗначению(МояКоманда) = Неопределено Тогда
		Исключения.Добавить(МояКоманда);
	КонецЕсли;
КонецПроцедуры
