
&НаСервереБезКонтекста
Функция Цвет(Фаза, Дефолт=Ложь)
	Возврат Биоритмы.Цвет(Фаза, Дефолт);
КонецФункции

&НаСервере
Процедура ВидимостьДоступностьУстановить()
	Элементы.АвтоудалениеНапоминаний.Доступность		= НаборКонстант.ВключитьНапоминания;
	Элементы.ПоказыватьРазницуМячей.Доступность			= (НЕ НаборКонстант.ТурнирнаяТаблицаКакНаBBC);
	Элементы.КоличествоМатчейВАнонсе.Доступность		= НаборКонстант.СтарыйВидАнонсов;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Физический			= Цвет(1);
	Эмоциональный		= Цвет(2);
	Интеллектуальный	= Цвет(3);
	
	Элементы.Физический.ЦветФона		= Физический;
	Элементы.Эмоциональный.ЦветФона		= Эмоциональный;
	Элементы.Интеллектуальный.ЦветФона	= Интеллектуальный;
	
	РеквизитD	= "D";
	РеквизитL	= "L";
	РеквизитW	= "W";
	
	ТурнирнаяТаблицаКакНаBBC	= НаборКонстант.ТурнирнаяТаблицаКакНаBBC;
	ВыводитьТурВместоЧемпионата	= НаборКонстант.ВыводитьТурВместоЧемпионата;
	ИспользоватьСтадионы		= НаборКонстант.ИспользоватьСтадионы;
	
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ФизическийПриИзменении(Элемент)
	Элементы.Физический.ЦветФона		= Физический;
КонецПроцедуры

&НаКлиенте
Процедура ФизическийОчистка(Элемент, СтандартнаяОбработка)
	ЦветОчистка(Элемент, СтандартнаяОбработка, Физический, 1);
КонецПроцедуры

&НаКлиенте
Процедура ЦветОчистка(Элемент, СтандартнаяОбработка, Цвет, Нумер=1)
	СтандартнаяОбработка	= Ложь;
	Цвет					= Цвет(Нумер, Истина);
	Элемент.ЦветФона		= Цвет;
КонецПроцедуры

&НаКлиенте
Процедура ЭмоциональныйПриИзменении(Элемент)
	Элементы.Эмоциональный.ЦветФона		= Эмоциональный;
КонецПроцедуры

&НаКлиенте
Процедура ЭмоциональныйОчистка(Элемент, СтандартнаяОбработка)
	ЦветОчистка(Элемент, СтандартнаяОбработка, Эмоциональный, 2);
КонецПроцедуры

&НаКлиенте
Процедура ИнтеллектуальныйПриИзменении(Элемент)
	Элементы.Интеллектуальный.ЦветФона	= Интеллектуальный;
КонецПроцедуры

&НаКлиенте
Процедура ИнтеллектуальныйОчистка(Элемент, СтандартнаяОбработка)
	ЦветОчистка(Элемент, СтандартнаяОбработка, Интеллектуальный, 3);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Цвета	= Новый Соответствие;
	Цвета.Вставить(1, Физический);
	Цвета.Вставить(2, Эмоциональный);
	Цвета.Вставить(3, Интеллектуальный);
	Нота = Биоритмы.ЦветаСохранить(Цвета);
	Если НЕ ПустаяСтрока(Нота) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(Нота);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбновитьИнтерфейс();
	Если ТурнирнаяТаблицаКакНаBBC <> НаборКонстант.ТурнирнаяТаблицаКакНаBBC
	Или ВыводитьТурВместоЧемпионата <> НаборКонстант.ВыводитьТурВместоЧемпионата
	Тогда
		ТурнирнаяТаблицаКакНаBBC	= НаборКонстант.ТурнирнаяТаблицаКакНаBBC;
		ВыводитьТурВместоЧемпионата	= НаборКонстант.ВыводитьТурВместоЧемпионата;
		Оповестить("ТурнирнаяТаблица");
	КонецЕсли;
	Если ИспользоватьСтадионы <> НаборКонстант.ИспользоватьСтадионы Тогда
		ИспользоватьСтадионы	= НаборКонстант.ИспользоватьСтадионы;
		Оповестить("ИспользоватьСтадионы", ИспользоватьСтадионы);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВключитьНапоминанияПриИзменении(Элемент)
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ПервоначальноеЗаполнение(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("Заполнение", ЭтотОбъект), "Точно?", РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.Отмена, "Первоначальное заполнение");
КонецПроцедуры

&НаКлиенте
Процедура Заполнение(Данные, ДопПараметры) Экспорт
	Если Данные = КодВозвратаДиалога.ОК Тогда
		СерверныйФункции.ПервоначальноеЗаполнение();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТурнирнаяТаблицаКакНаBBCПриИзменении(Элемент)
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НаборКонстант.ПоказыватьРазницуМячей
	И НаборКонстант.ТурнирнаяТаблицаКакНаBBC
	Тогда
		НаборКонстант.ПоказыватьРазницуМячей = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаОбщаяПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если Элементы.СтраницаСтатистика = ТекущаяСтраница И КоличествоКоманд = 0 Тогда
		КомандаОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандаОбновитьНаСервере()
	КоличествоКоманд	= 0;
	КоличествоИгроков	= 0;
	КоличествоМатчей	= 0;
	КоличествоТрансферов= 0;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	10 КАК Параметр,
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Команды.Ссылка) КАК Количество
	                      |ИЗ
	                      |	Справочник.Команды КАК Команды
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	20,
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Игроки.Ссылка)
	                      |ИЗ
	                      |	Справочник.Игроки КАК Игроки
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	30,
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Матч.Ссылка)
	                      |ИЗ
	                      |	Документ.Матч КАК Матч
	                      |ГДЕ
	                      |	Матч.Проведен = ИСТИНА
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	40,
	                      |	СУММА(1)
	                      |ИЗ
	                      |	РегистрСведений.ПеремещенияИгроков КАК ПеремещенияИгроков");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Параметр = 10 Тогда
			КоличествоКоманд = Выборка.Количество;
		ИначеЕсли Выборка.Параметр = 20 Тогда
			КоличествоИгроков = Выборка.Количество;
		ИначеЕсли Выборка.Параметр = 30 Тогда
			КоличествоМатчей = Выборка.Количество;
		ИначеЕсли Выборка.Параметр = 40 Тогда
			КоличествоТрансферов = Выборка.Количество;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновить(Команда)
	КомандаОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СтарыйВидАнонсовПриИзменении(Элемент)
	ВидимостьДоступностьУстановить();
КонецПроцедуры
