
&НаКлиенте
Процедура ТолькоДниРожденияПриИзменении(Элемент)
	СписокУстановитьЗначениеПараметра("ТолькоДниРождения",	ТолькоДниРождения);
КонецПроцедуры

&НаКлиенте
Процедура ДатыУстановить()
	Период.Вариант	= ВариантСтандартногоПериода.ДоКонцаЭтогоМесяца;
КонецПроцедуры

&НаКлиенте
Процедура КомандаСегодня(Команда)
	ДатыУстановить();
	КомандаОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Игроки")
	И Параметр = Игрок
	Тогда
		Игрок = Неопределено;
		ИзображениеПолучить(Элементы.Список.ТекущиеДанные.ИгрокИлиМатч, Игрок, Изображение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПриИзменении(Элемент)
	СписокУстановитьЗначениеПараметра("Команда",			МояКоманда);
	СписокПриАктивизацииСтроки();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ИзображениеДефолт	= ИзображениеДефолтПолучить();
	
	Список.Параметры.УстановитьЗначениеПараметра("Дата",				Вчера());
	Список.Параметры.УстановитьЗначениеПараметра("ТолькоДниРождения",	ТолькоДниРождения);
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		ДатыУстановить();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(МояКоманда) Тогда
		МояКоманда		= МояКоманда();
	КонецЕсли;
	КомандаОбновить();
КонецПроцедуры

&НаСервере
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаСервереБезКонтекста
Функция ИзображениеДефолтПолучить()
	Возврат СерверныйФункции.ИзображениеДефолтПолучить();
КонецФункции

&НаСервереБезКонтекста
Функция Вчера()
	Возврат (НачалоДня(ТекущаяДатаСеанса()) - 1);
КонецФункции

&НаКлиенте
Процедура КомандаОбновить(Команда=Неопределено)
	Элементы.Период.Подсказка	= Строка(Период);
	
	СписокУстановитьЗначениеПараметра("Команда",			МояКоманда);
	СписокУстановитьЗначениеПараметра("ДатаН",				Период.ДатаНачала);
	СписокУстановитьЗначениеПараметра("ДатаК",				Период.ДатаОкончания);
	СписокУстановитьЗначениеПараметра("ТолькоДниРождения",	ТолькоДниРождения);
	СписокПриАктивизацииСтроки();
КонецПроцедуры

&НаКлиенте
Процедура СписокУстановитьЗначениеПараметра(ПараметрИмя, Значение)
	ШалостьУдалась = Ложь;
	Для Каждого Параметр Из Список.Параметры.Элементы Цикл
		Если СтрСравнить(Параметр.Параметр, ПараметрИмя) = 0 Тогда
			Если Параметр.Значение <> Значение Тогда
				Список.Параметры.УстановитьЗначениеПараметра(ПараметрИмя, Значение);
			КонецЕсли;
			ШалостьУдалась		= Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НЕ ШалостьУдалась Тогда
		Список.Параметры.УстановитьЗначениеПараметра(ПараметрИмя, Значение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элемент.ТекущиеДанные.ИгрокИлиМатч);
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент=Неопределено)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Изображение	= СерверныйФункции.ИзображениеПолучить(МояКоманда);
	ИначеЕсли Элементы.Список.ТекущиеДанные.ЭтоИгрок Тогда
		ИзображениеПолучить(Элементы.Список.ТекущиеДанные.ИгрокИлиМатч, Игрок, Изображение);
	Иначе
		Изображение	= СерверныйФункции.ИзображениеПолучить(МояКоманда);
		Игрок		= Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзображениеПолучить(ИгрокИлиМатч, Игрок, Изображение)
	Если ИгрокИлиМатч = Игрок Тогда
		//
	Иначе
		Изображение	= СерверныйФункции.ИзображениеПолучить(ИгрокИлиМатч);
		Игрок	= ИгрокИлиМатч;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	КомандаОбновить();
КонецПроцедуры

&НаСервере
Процедура Зачистить(Виктим, ЭтоСобытие=Ложь)
	ЗаполнитьЗначенияСвойств(Виктим, Новый Структура("ЭтоСобытие,Дата,Команды,Стадион,Комментарий", ЭтоСобытие, Дата(1,1,1), Новый Массив));
КонецПроцедуры

&НаСервере
Функция ПервоеДороже(Слова, Первое=Ложь)
	Ответ	= "";
	мСлова = СтрРазделить(Слова, ":", Ложь);
	Если Первое Тогда
		Если мСлова.Количество() > 0
		И НЕ ПустаяСтрока(Лев(мСлова.Получить(0), 1))
		Тогда
			Ответ = СокрЛП(мСлова.Получить(0));
		КонецЕсли;
	Иначе
		Если ПустаяСтрока(Лев(Слова, 1)) Тогда
			Ответ	= Слова;
		Иначе
			Если мСлова.Количество() >= 2 Тогда
				Ответ	= мСлова.Получить(1);
				Для Индекс = 2 По мСлова.ВГраница() Цикл
					Ответ	= Ответ + ":" + мСлова.Получить(Индекс);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция СтрокаКакКоманды(Знач Слова)
	Ответ	= Новый Массив;
	Слова	= СокрЛП(СтрЗаменить(СтрЗаменить(Слова, "(A)",""), "(H)",""));
	Солянка	= "";
	мСлова	= Новый Массив;
	Для Каждого Слово Из СтрРазделить(Слова, " ", Ложь) Цикл
		Если СтрСравнить(Слово, "vs") = 0 Тогда
			мСлова.Добавить(Солянка);
			Солянка	= "";
		ИначеЕсли СтрСравнить(Слово, "v") = 0 Тогда
			мСлова.Добавить(Солянка);
			Солянка	= "";
		ИначеЕсли ПустаяСтрока(Солянка) Тогда
			Солянка	= Слово;
		Иначе
			Солянка	= Солянка + " " + Слово;
		КонецЕсли;
	КонецЦикла;
	мСлова.Добавить(Солянка);
	Для Каждого Слово Из мСлова Цикл
		Команда = СерверныйФункции.КомандаНайти(Слово);
		Если ЗначениеЗаполнено(Команда) Тогда
			Ответ.Добавить(Команда);
		КонецЕсли;
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция Внятно(Слово, Целиком=Ложь)
	Если Целиком Тогда
		Возврат СтрЗаменить(Слово, "\", "");
	КонецЕсли;
	
	Ответ	= "";
	Для Индекс = 1 По СтрДлина(Слово) Цикл
		Если СтрСравнить("\", Сред(Слово, Индекс, 1)) = 0 Тогда
			Прервать;
		Иначе
			Ответ = Ответ + Сред(Слово, Индекс, 1);
		КонецЕсли;
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция КомандаИмпортНаСервере(Текст)
	Ответ	= 0;
	Сдвиг	= 6 * 3600;
	Элемент = Новый Структура("ЭтоСобытие,Дата,Команды,Стадион,Комментарий");
	Зачистить(Элемент);
	Для НомерСтроки = 1 По СтрЧислоСтрок(Текст) Цикл
		Слово	= СтрПолучитьСтроку(Текст, НомерСтроки);
		Первое	= ПервоеДороже(Слово, Истина);
		Если СтрСравнить(Первое, "BEGIN") = 0 Тогда
			Зачистить(Элемент, СтрСравнить(ПервоеДороже(Слово), "VEVENT") = 0);
			
		ИначеЕсли НЕ Элемент.ЭтоСобытие Тогда
			//
		ИначеЕсли ПустаяСтрока(Первое)
		И НЕ ПустаяСтрока(Элемент.Комментарий)
		Тогда
			Элемент.Комментарий = Элемент.Комментарий + ПервоеДороже(Слово);
		ИначеЕсли СтрСравнить(Первое, "DESCRIPTION") = 0 Тогда
			Элемент.Комментарий = ПервоеДороже(Слово);
		ИначеЕсли СтрСравнить(Первое, "DTSTART") = 0 Тогда
			Элемент.Дата	= СтрокаКакДата(ПервоеДороже(Слово));
			Если Элемент.Дата > ТекущаяУниверсальнаяДата() Тогда
				Элемент.Дата = Элемент.Дата + Сдвиг;
			Иначе
				Зачистить(Элемент);
			КонецЕсли;
		ИначеЕсли СтрСравнить(Первое, "SUMMARY") = 0 Тогда
			Элемент.Команды = СтрокаКакКоманды(ПервоеДороже(Слово));
		ИначеЕсли СтрСравнить(Первое, "LOCATION") = 0 Тогда
			Слово	= Внятно(ПервоеДороже(Слово), Истина);
			Элемент.Стадион = СерверныйФункции.СтадионНайти(Слово);
		ИначеЕсли СтрСравнить(Первое, "END") = 0 Тогда
			Если Элемент.Дата > ТекущаяДатаСеанса() Тогда
				Если Элемент.Команды.Количество() = 2 Тогда
					Матч	= СерверныйФункции.МатчНайти(Элемент.Команды, Элемент.Дата);
					Если НЕ ЗначениеЗаполнено(Матч) Тогда
						МатчОбъект	= Документы.Матч.СоздатьДокумент();
						МатчОбъект.Заполнить(Элемент);
						МатчОбъект.Записать(РежимЗаписиДокумента.Запись);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			Зачистить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура КомандаИмпорт(Команда)
	Гиперссылка	= УРЛ;
	ПоказатьВводСтроки(Новый ОписаниеОповещения("Импорт", ЭтотОбъект), Гиперссылка, "Импорт календаря (iCal)");
КонецПроцедуры

&НаКлиенте
Процедура Импорт(Гиперссылка, ДополнительныеПараметры=Неопределено) Экспорт
	Ответ	= 0;
	Если ПустаяСтрока(Гиперссылка) Тогда
		//
	ИначеЕсли ЭтоУРЛ(Гиперссылка) Тогда
		УРЛ		= Гиперссылка;
		Ответ	= КомандаИмпортНаСервере(ОбщегоНазначения.СкачатьПоСсылке(Гиперссылка));
	Иначе
		Ответ	= КомандаИмпортНаСервере(ОбщегоНазначения.ТекстовыйДокументПрочитать(Гиперссылка));
	КонецЕсли;
	Если Ответ > 0 Тогда
		Сообщить(СтрШаблон("Добавлено событий  %1", Ответ));
	КонецЕсли;
КонецПроцедуры
