
Процедура ОбъектПередЗаписью(Источник, Отказ) Экспорт
	Если Отказ Тогда
		//
	ИначеЕсли Источник.ОбменДанными.Загрузка Тогда
		//
	ИначеЕсли Источник.Модифицированность() Тогда
		Исправитель	= Новый Структура("Наименование,Комментарий", "", "");
		ЗаполнитьЗначенияСвойств(Исправитель, Источник);
		Исправитель.Наименование	= СокрЛП(Исправитель.Наименование);
		Исправитель.Комментарий		= СокрЛП(Лев(Исправитель.Комментарий, 10000));
		ЗаполнитьЗначенияСвойств(Источник, Исправитель);
	КонецЕсли;
КонецПроцедуры

Процедура ОбъектПриКопировании(Источник, ОбъектКопирования) Экспорт
	ЗаполнитьЗначенияСвойств(Источник, Новый Структура("Комментарий,Изображение", ""));
КонецПроцедуры

Процедура СправочникПередУдалением(Источник, Отказ) Экспорт
	Если НЕ Источник.ПометкаУдаления Тогда
		Отказ = Истина;
	ИначеЕсли СерверныйФункции.ЕстьИстория(Источник.Ссылка) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		Если Источник.ДополнительныеСвойства.Свойство("Импорт") = Ложь Тогда
			РегистрыСведений.Итоги.Обновить(Источник.Тур.Владелец);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		РегистрыСведений.Итоги.Очистить(Источник.Тур.Владелец);
	КонецЕсли;
КонецПроцедуры

Функция ИмпортТуры(Чемпионат, ТурКод)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 2
	                      |	Туры.Ссылка КАК Тур,
	                      |	Туры.Код КАК Код,
	                      |	Туры.ДатаНачала КАК ДатаНачала
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |ГДЕ
	                      |	Туры.Владелец = &Чемпионат
	                      |	И Туры.Код >= &Код
	                      |	И Туры.ПометкаУдаления = ЛОЖЬ
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаНачала,
	                      |	Код");
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Запрос.УстановитьПараметр("Код",			ТурКод);
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции

Процедура ИмпортИгр(ОдинРаз=Ложь) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур) КАК Тур,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод,
	                      |	ЦЕЛ(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) / ЦЕЛ(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда) / 2)) КАК Количество
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Итоги КАК Итоги
	                      |		ПО ТурнирнаяТаблица.Тур.Владелец = Итоги.Чемпионат
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |		ПО ТурнирнаяТаблица.Команда = ЧемпионатыКоманды.Команда
	                      |			И ТурнирнаяТаблица.Тур.Владелец = ЧемпионатыКоманды.Ссылка
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания >= &Дата
	                      |	И ТурнирнаяТаблица.Тур.Владелец.Владелец.ОлимпийскаяСистема = ЛОЖЬ
	                      |	И Итоги.Чемпионат ЕСТЬ NULL
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Тур.Владелец
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	МИНИМУМ(ТурнирнаяТаблица.Период)");
	Запрос.УстановитьПараметр("Дата",			ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Туры	= ИмпортТуры(Выборка.Чемпионат, ?(Выборка.ТурКод = Выборка.Количество, 1 + Выборка.ТурКод, Выборка.ТурКод));
		Если ОдинРаз Тогда
			Если Туры.Следующий() Тогда
				Гиперссылка	= СерверныйФункции.ИмпортУРЛ(Туры.Тур);
				СерверныйФункции.ИмпортИгрПоГиперссылке(Гиперссылка, Туры.Тур);
			КонецЕсли;
			Прервать;
		Иначе
			Пока Туры.Следующий() Цикл
				Гиперссылка	= СерверныйФункции.ИмпортУРЛ(Туры.Тур);
				Если НЕ СерверныйФункции.ИмпортИгрПоГиперссылке(Гиперссылка, Туры.Тур) Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
