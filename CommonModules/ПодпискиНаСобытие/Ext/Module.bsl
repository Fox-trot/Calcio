
Процедура ОбъектПередЗаписью(Источник, Отказ) Экспорт
	Если Отказ Тогда
		//
	ИначеЕсли Источник.ОбменДанными.Загрузка Тогда
		//
	ИначеЕсли Источник.Модифицированность() Тогда
		Исправитель	= Новый Структура("Наименование,Комментарий", "", "");
		ЗаполнитьЗначенияСвойств(Исправитель, Источник);
		Исправитель.Наименование	= СокрЛП(Исправитель.Наименование);
		Исправитель.Комментарий		= СокрЛП(Лев(Исправитель.Комментарий, 10000));
		ЗаполнитьЗначенияСвойств(Источник, Исправитель);
	КонецЕсли;
КонецПроцедуры

Процедура ОбъектПриКопировании(Источник, ОбъектКопирования) Экспорт
	ЗаполнитьЗначенияСвойств(Источник, Новый Структура("Комментарий,Изображение", ""));
КонецПроцедуры

Процедура СправочникПередУдалением(Источник, Отказ) Экспорт
	Если НЕ Источник.ПометкаУдаления Тогда
		Отказ = Истина;
	ИначеЕсли СерверныйФункции.ЕстьИстория(Источник.Ссылка) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		Если Источник.ДополнительныеСвойства.Свойство("Импорт") = Ложь Тогда
			РегистрыСведений.Итоги.Обновить(Источник.Тур.Владелец);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		РегистрыСведений.Итоги.Очистить(Источник.Тур.Владелец);
	КонецЕсли;
КонецПроцедуры

Процедура ИмпортИгр() Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Туры.Ссылка КАК Тур
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
	                      |			МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод,
	                      |			ЦЕЛ(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) / ЦЕЛ(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда) / 2)) КАК Количество
	                      |		ИЗ
	                      |			РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Итоги КАК Итоги
	                      |				ПО ТурнирнаяТаблица.Тур.Владелец = Итоги.Чемпионат
	                      |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |				ПО ТурнирнаяТаблица.Команда = ЧемпионатыКоманды.Команда
	                      |					И ТурнирнаяТаблица.Тур.Владелец = ЧемпионатыКоманды.Ссылка
	                      |		ГДЕ
	                      |			ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания >= &Дата
	                      |			И ТурнирнаяТаблица.Тур.Владелец.Владелец.ОлимпийскаяСистема = ЛОЖЬ
	                      |			И Итоги.Чемпионат ЕСТЬ NULL
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			ТурнирнаяТаблица.Тур.Владелец) КАК ВТ
	                      |		ПО Туры.Владелец = ВТ.Чемпионат
	                      |			И (ВЫБОР
	                      |				КОГДА ВТ.ТурКод = ВТ.Количество
	                      |					ТОГДА Туры.Код МЕЖДУ 1 + ВТ.ТурКод И 2 + ВТ.ТурКод
	                      |				ИНАЧЕ Туры.Код МЕЖДУ ВТ.ТурКод И 1 + ВТ.ТурКод
	                      |			КОНЕЦ)
	                      |ГДЕ
	                      |	Туры.ПометкаУдаления = ЛОЖЬ
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Туры.Владелец");
	Запрос.УстановитьПараметр("Дата",			ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоОшибок = 0;
	Пока Выборка.Следующий() И КоличествоОшибок < 2 Цикл
		Гиперссылка	= СерверныйФункции.ИмпортУРЛ(Выборка.Тур);
		Если НЕ СерверныйФункции.ИмпортИгрПоГиперссылке(Гиперссылка, Выборка.Тур) Тогда
			КоличествоОшибок = КоличествоОшибок + 1;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
