
Процедура ОбъектПередЗаписью(Источник, Отказ) Экспорт
	Если Отказ Тогда
		//
	ИначеЕсли Источник.ОбменДанными.Загрузка Тогда
		//
	ИначеЕсли Источник.Модифицированность() Тогда
		Исправитель	= Новый Структура("Наименование,Комментарий", "", "");
		ЗаполнитьЗначенияСвойств(Исправитель, Источник);
		Исправитель.Наименование	= СокрЛП(Исправитель.Наименование);
		Исправитель.Комментарий		= СокрЛП(Лев(Исправитель.Комментарий, 10000));
		ЗаполнитьЗначенияСвойств(Источник, Исправитель);
	КонецЕсли;
КонецПроцедуры

Процедура ОбъектПриКопировании(Источник, ОбъектКопирования) Экспорт
	ЗаполнитьЗначенияСвойств(Источник, Новый Структура("Комментарий,Изображение", ""));
КонецПроцедуры

Процедура СправочникПередУдалением(Источник, Отказ) Экспорт
	Если НЕ Источник.ПометкаУдаления Тогда
		Отказ = Истина;
	ИначеЕсли СерверныйФункции.ЕстьИстория(Источник.Ссылка) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Источник, Отказ, РежимПроведения) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		Если Источник.ДополнительныеСвойства.Свойство("Импорт") = Ложь Тогда
			РегистрыСведений.Итоги.Обновить(Источник.Тур.Владелец);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	Если ЗначениеЗаполнено(Источник.Тур) Тогда
		РегистрыСведений.Итоги.Очистить(Источник.Тур.Владелец);
	КонецЕсли;
КонецПроцедуры

Процедура ИмпортИгр() Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Туры.Владелец КАК Чемпионат,
	                      |	Туры.Ссылка КАК Тур,
	                      |	МАКСИМУМ(ЕСТЬNULL(ТурнирнаяТаблица.Период, Туры.ДатаОкончания)) КАК Период
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ПО (ТурнирнаяТаблица.Тур = Туры.Ссылка)
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |		ПО Туры.Владелец = ЧемпионатыКоманды.Ссылка
	                      |ГДЕ
	                      |	Туры.ПометкаУдаления = ЛОЖЬ
	                      |	И Туры.Владелец.ПометкаУдаления = ЛОЖЬ
	                      |	И Туры.Владелец.ДатаОкончания >= &Дата
	                      |	И Туры.Владелец.ДатаНачала < &ДатаН
	                      |	И Туры.Владелец.Владелец.ОлимпийскаяСистема = ЛОЖЬ
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Туры.Ссылка,
	                      |	Туры.Владелец
	                      |
	                      |ИМЕЮЩИЕ
	                      |	1 + 2 * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) < КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Чемпионат,
	                      |	Тур
	                      |ИТОГИ ПО
	                      |	Чемпионат");
	Запрос.УстановитьПараметр("ДатаН",			ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Дата",			ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	Чемпионаты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Чемпионаты.Следующий() Цикл
		Выборка = Чемпионаты.Выбрать();
		КоличествоОшибок = 0;
		Пока Выборка.Следующий() И КоличествоОшибок < 3 Цикл
			Гиперссылка	= СерверныйФункции.ИмпортУРЛ(Выборка.Тур);
			Если НЕ СерверныйФункции.ИмпортИгрПоГиперссылке(Гиперссылка, Выборка.Тур) Тогда
				КоличествоОшибок = КоличествоОшибок + ?(Выборка.Период > ТекущаяДатаСеанса(), 3, 1);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
