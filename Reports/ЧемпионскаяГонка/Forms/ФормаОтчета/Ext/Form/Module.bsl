
&НаСервере
Процедура ГрафикОбновитьНаСервере(ИспользоватьКеш=Ложь)
	Если НЕ ИспользоватьКеш Тогда
		ДанныеПолучить();
	КонецЕсли;
	
	График.Очистить();
	//График.ТипДиаграммы = ?(ЗначениеЗаполнено(Отчет.Команда), ТипДиаграммы.ГрафикСНакоплением, ТипДиаграммы.График);
	Для Каждого Детали Из Отчет.Данные Цикл
		Если ЗначениеЗаполнено(Отчет.Команда) Тогда
			График.УстановитьЗначение(График.УстановитьТочку(Детали.Тур), График.УстановитьСерию(ФорматЧГ(Детали.Период)), Значение(Детали), Детали.Матч);
		Иначе
			График.УстановитьЗначение(График.УстановитьТочку(СтрШаблон("%1/%2", ФорматЧГ(Детали.Период), Детали.Тур)), УстановитьСерию(Детали.Команда), Значение(Детали));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция Значение(Детали)
	Возврат ?(Показатель=0, Детали.КоличествоОчков, ?(Показатель=1, Детали.КоличествоЗабито, Детали.КоличествоПропущено));
КонецФункции

&НаСервере
Процедура ДанныеПолучить()
	Отчет.Данные.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 3
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
	                      |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод
	                      |ПОМЕСТИТЬ Чемпионаты
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Тур.Владелец,
	                      |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаОкончания УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Чемпионаты.ДатаОкончания КАК Период,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК Тур,
	                      |	ТурнирнаяТаблица.Команда КАК Команда,
	                      |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК ТурВладелец
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	                      |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	                      |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
	                      |		ПО ТурнирнаяТаблица.Тур.Владелец = Чемпионаты.Чемпионат
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			МИНИМУМ(ВременнаяТаблица.ТурКод) КАК ТурКод
	                      |		ИЗ
	                      |			Чемпионаты КАК ВременнаяТаблица) КАК ВложенныйЗапрос
	                      |		ПО ТурнирнаяТаблица.Тур.Код <= ВложенныйЗапрос.ТурКод
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Команда В
	                      |			(ВЫБРАТЬ
	                      |				Клубы.Команда
	                      |			ИЗ
	                      |				Справочник.Лига.Команды КАК Клубы
	                      |			ГДЕ
	                      |				Клубы.Ссылка = &Лига)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда,
	                      |	ТурнирнаяТаблица.Тур.Владелец,
	                      |	Чемпионаты.ДатаОкончания
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период,
	                      |	Команда
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Лига",			Отчет.Лига);
	Запрос.УстановитьПараметр("Команда",		Отчет.Команда);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 3
		               |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод
		               |ПОМЕСТИТЬ Чемпионаты
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ДатаОкончания УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Чемпионаты.ДатаОкончания КАК Период,
		               |	ТурнирнаяТаблица.Тур.Код КАК Тур,
		               |	ТурнирнаяТаблица.Команда КАК Команда,
		               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
		               |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
		               |	ТурнирнаяТаблица.Регистратор КАК Матч
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
		               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
		               |		ПО ТурнирнаяТаблица.Тур.Владелец = Чемпионаты.Чемпионат
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			МИНИМУМ(ВременнаяТаблица.ТурКод) КАК ТурКод
		               |		ИЗ
		               |			Чемпионаты КАК ВременнаяТаблица) КАК ВложенныйЗапрос
		               |		ПО ТурнирнаяТаблица.Тур.Код <= ВложенныйЗапрос.ТурКод
		               |ГДЕ
		               |	ТурнирнаяТаблица.Команда = &Команда
		               |	И Противник.Тур.Владелец = Чемпионаты.Чемпионат
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Чемпионаты.ДатаОкончания,
		               |	ТурнирнаяТаблица.Тур.Код,
		               |	ТурнирнаяТаблица.Команда,
		               |	ТурнирнаяТаблица.Регистратор
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период УБЫВ,
		               |	Тур";
	КонецЕсли;
	Выборка	= Запрос.Выполнить().Выбрать();
	Итоги	= Новый Структура("КоличествоПропущено,КоличествоЗабито,КоличествоОчков", 0, 0, 0);
	Период	= Дата(1,1,1);
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Отчет.Команда) Тогда
			Если Период = Выборка.Период Тогда
				Итоги.КоличествоПропущено	= Итоги.КоличествоПропущено + Выборка.КоличествоПропущено;
				Итоги.КоличествоЗабито		= Итоги.КоличествоЗабито + Выборка.КоличествоЗабито;
				Итоги.КоличествоОчков		= Итоги.КоличествоОчков + Выборка.КоличествоОчков;
				
				НоваяСтрока = Отчет.Данные.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Итоги);
			Иначе
				Период	= Выборка.Период;
				
				Итоги.КоличествоПропущено	= Выборка.КоличествоПропущено;
				Итоги.КоличествоЗабито		= Выборка.КоличествоЗабито;
				Итоги.КоличествоОчков		= Выборка.КоличествоОчков;
				
				ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
			КонецЕсли;
		Иначе
			ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НастройкиУстановить()
	Если ЗначениеЗаполнено(Отчет.Лига) Тогда
		ЛигаПриИзмененииНаСервере();
	Иначе
		Отчет.Лига = СерверныйФункции.ЛигаКоманды(МояКоманда());
		Если ЗначениеЗаполнено(Отчет.Лига) Тогда
			ЛигаПриИзмененииНаСервере();
		КонецЕсли;
		Отчет.Команда = МояКоманда();
	КонецЕсли;
	
	Элементы.Показатель.СписокВыбора.Добавить(0, "Количество очков");
	Элементы.Показатель.СписокВыбора.Добавить(1, "Забито голов");
	Элементы.Показатель.СписокВыбора.Добавить(2, "Пропущено голов");
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЦветКоманды(Клуб)
	Возврат СерверныйФункции.ЦветКоманды(Клуб);
КонецФункции

&НаСервере
Функция УстановитьСерию(Команда)
	Серия	= График.УстановитьСерию(Команда);
	Цвет	= ЦветКоманды(Команда);
	Если ЗначениеЗаполнено(Цвет) Тогда
		Серия.Цвет			= Цвет;
	КонецЕсли;
	Возврат Серия;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкиУстановить();
	КартинкиЗагрузить();
	Сформировать();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаКлиенте
Процедура КомандаОчистка(Элемент, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Команда) Тогда
		СтандартнаяОбработка	= Ложь;
		Отчет.Команда			= МояКоманда();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПриИзменении(Элемент)
	ГрафикОбновитьНаСервере(Истина);
КонецПроцедуры

&НаСервере
Процедура ЛигаПриИзмененииНаСервере()
	КомандыЛиги.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Отчет.Лига));
	Элементы.Команда.СписокВыбора.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Отчет.Лига, Истина));
КонецПроцедуры

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент)
	Отчет.Команда	= Неопределено;
	ЛигаПриИзмененииНаСервере();
	ПодключитьОбработчикОжидания("КартинкиЗагрузить", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КартинкиЗагрузить() Экспорт
	ОбщегоНазначения.КартинкиЗагрузить(Элементы.Команда.СписокВыбора, Кеш);
КонецПроцедуры
