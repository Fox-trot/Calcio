
&НаСервере
Процедура ГрафикОбновитьНаСервере(ИспользоватьКеш=Ложь)
	Если НЕ ИспользоватьКеш Тогда
		ДанныеПолучить();
	КонецЕсли;
	График.Очистить();

	Для Каждого Детали Из Отчет.Данные Цикл
		Если ЗначениеЗаполнено(Отчет.Команда) Тогда
			График.УстановитьЗначение(График.УстановитьТочку(Детали.Тур), График.УстановитьСерию(ФорматЧГ(Детали.Период)), Значение(Детали));
		Иначе
			График.УстановитьЗначение(График.УстановитьТочку(СтрШаблон("%1/%2", ФорматЧГ(Детали.Период), Детали.Тур)), УстановитьСерию(Детали.Команда), Значение(Детали));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция Значение(Детали)
	Возврат ?(Показатель=0, Детали.КоличествоОчков, ?(Показатель=3, Детали.Место, ?(Показатель=1, Детали.КоличествоЗабито, Детали.КоличествоПропущено)));
КонецФункции

&НаСервере
Процедура ДанныеПолучить()
	Отчет.Данные.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 3
	                      |	ИтогиПромежуточные.Тур.Владелец КАК Ссылка,
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания КАК Период,
	                      |	МАКСИМУМ(ИтогиПромежуточные.Тур.Код) КАК Код
	                      |ПОМЕСТИТЬ Чемпионаты
	                      |ИЗ
	                      |	РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |ГДЕ
	                      |	ИтогиПромежуточные.Тур.Владелец.Владелец = &Лига
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ИтогиПромежуточные.Тур.Владелец,
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период УБЫВ,
	                      |	Код УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания КАК Период,
	                      |	ИтогиПромежуточные.Тур КАК Тур,
	                      |	ИтогиПромежуточные.Команда КАК Команда,
	                      |	ИтогиПромежуточные.КоличествоОчков КАК КоличествоОчков,
	                      |	ИтогиПромежуточные.КоличествоЗабито КАК КоличествоЗабито,
	                      |	ИтогиПромежуточные.КоличествоПропущено КАК КоличествоПропущено,
	                      |	ИтогиПромежуточные.Место КАК Место
	                      |ИЗ
	                      |	РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			МИНИМУМ(Чемпионаты.Код) КАК Код
	                      |		ИЗ
	                      |			Чемпионаты КАК Чемпионаты) КАК Туры
	                      |		ПО ИтогиПромежуточные.Тур.Код = Туры.Код
	                      |ГДЕ
	                      |	ИтогиПромежуточные.Тур.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				Чемпионаты.Ссылка
	                      |			ИЗ
	                      |				Чемпионаты)
	                      |	И ИтогиПромежуточные.Команда В
	                      |			(ВЫБРАТЬ
	                      |				Клубы.Команда
	                      |			ИЗ
	                      |				Справочник.Лига.Команды КАК Клубы
	                      |			ГДЕ
	                      |				Клубы.Ссылка = &Лига)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период,
	                      |	Место");
	Запрос.УстановитьПараметр("Лига",			Отчет.Лига);
	Запрос.УстановитьПараметр("Команда",		Отчет.Команда);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 3
	                      |	ИтогиПромежуточные.Тур.Владелец КАК Ссылка,
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания КАК Период,
	                      |	МАКСИМУМ(ИтогиПромежуточные.Тур.Код) КАК Код
	                      |ПОМЕСТИТЬ Чемпионаты
	                      |ИЗ
	                      |	РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |ГДЕ
	                      |	ИтогиПромежуточные.Команда = &Команда
	                      |	И ИтогиПромежуточные.Тур.Владелец.Владелец = &Лига
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ИтогиПромежуточные.Тур.Владелец,
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период УБЫВ,
	                      |	Код УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ИтогиПромежуточные.Тур.Владелец.ДатаОкончания КАК Период,
	                      |	ИтогиПромежуточные.Тур.Код КАК Тур,
	                      |	ИтогиПромежуточные.КоличествоОчков КАК КоличествоОчков,
	                      |	ИтогиПромежуточные.КоличествоЗабито КАК КоличествоЗабито,
	                      |	ИтогиПромежуточные.КоличествоПропущено КАК КоличествоПропущено,
	                      |	ИтогиПромежуточные.Место КАК Место
	                      |ИЗ
	                      |	РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			МИНИМУМ(Чемпионаты.Код) КАК Код
	                      |		ИЗ
	                      |			Чемпионаты КАК Чемпионаты) КАК Туры
	                      |		ПО ИтогиПромежуточные.Тур.Код <= Туры.Код
	                      |ГДЕ
	                      |	ИтогиПромежуточные.Команда = &Команда
	                      |	И ИтогиПромежуточные.Тур.Владелец В
	                      |			(ВЫБРАТЬ
	                      |				Чемпионаты.Ссылка
	                      |			ИЗ
	                      |				Чемпионаты)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период УБЫВ,
	                      |	Тур";
	КонецЕсли;
	Детали	= Запрос.Выполнить().Выбрать();
	Пока Детали.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Детали);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НастройкиУстановить()
	Если ЗначениеЗаполнено(Отчет.Лига) Тогда
		ЛигаПриИзмененииНаСервере();
	Иначе
		Отчет.Лига = СерверныйФункции.ЛигаКоманды(МояКоманда());
		ЛигаПриИзмененииНаСервере();
		
		Отчет.Команда = МояКоманда();
	КонецЕсли;
	
	Элементы.Показатель.СписокВыбора.Добавить(0, "Количество очков");
	Элементы.Показатель.СписокВыбора.Добавить(1, "Забито голов");
	Элементы.Показатель.СписокВыбора.Добавить(2, "Пропущено голов");
	Элементы.Показатель.СписокВыбора.Добавить(3, "Место");
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЦветКоманды(Команда)
	Возврат СерверныйФункции.ЦветКоманды(Команда);
КонецФункции

&НаСервере
Функция УстановитьСерию(Команда)
	Серия	= График.УстановитьСерию(Команда);
	Цвет	= ЦветКоманды(Команда);
	Если ЗначениеЗаполнено(Цвет) Тогда
		Серия.Цвет			= Цвет;
	КонецЕсли;
	Возврат Серия;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкиУстановить();
	КартинкиЗагрузить();
	Сформировать();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаКлиенте
Процедура КомандаОчистка(Элемент, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Команда) Тогда
		СтандартнаяОбработка	= Ложь;
		Отчет.Команда			= МояКоманда();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказательПриИзменении(Элемент)
	ГрафикОбновитьНаСервере(Истина);
КонецПроцедуры

&НаСервере
Процедура ЛигаПриИзмененииНаСервере()
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ИтогиПромежуточные.Команда КАК Команда
	                      |ИЗ
	                      |	РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |ГДЕ
	                      |	ИтогиПромежуточные.Тур.Владелец.Владелец = &Лига
	                      |	И ГОД(ИтогиПромежуточные.Период) В
	                      |			(ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 3
	                      |				ГОД(ИтогиПромежуточные.Период) КАК Период
	                      |			ИЗ
	                      |				РегистрСведений.ИтогиПромежуточные КАК ИтогиПромежуточные
	                      |			ГДЕ
	                      |				ИтогиПромежуточные.Тур.Владелец.Владелец = &Лига
	                      |			УПОРЯДОЧИТЬ ПО
	                      |				Период УБЫВ)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ИтогиПромежуточные.Команда
	                      |
	                      |ИМЕЮЩИЕ
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИтогиПромежуточные.Тур.Владелец) >= 3
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Команда
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Лига",			Отчет.Лига);
	КомандыЛиги.ЗагрузитьЗначения(?(ЗначениеЗаполнено(Отчет.Лига), Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0), Новый Массив));
	Элементы.Команда.СписокВыбора.ЗагрузитьЗначения(КомандыЛиги.ВыгрузитьЗначения());
КонецПроцедуры

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент)
	ЛигаПриИзмененииНаСервере();
	ПодключитьОбработчикОжидания("КартинкиЗагрузить", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КартинкиЗагрузить() Экспорт
	ОбщегоНазначения.КартинкиЗагрузить(Элементы.Команда.СписокВыбора, Кеш);
КонецПроцедуры
