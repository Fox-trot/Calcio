
&НаСервере
Процедура ГрафикОбновитьНаСервере()
	ДанныеПолучить();
	
	График.Очистить();
	График.ОбластьПостроения.ШкалаЗначений.ШагШкалы = ?(Показатель = 3, 0, 6);
	Для Каждого Детали Из Отчет.Данные Цикл
		Если ЗначениеЗаполнено(Отчет.Команда) И Показатель = 3 Тогда
			График.УстановитьЗначение(УстановитьТочку(Детали), УстановитьСерию(Детали.Период), Детали.КоличествоПропущено, Детали.Матч, Детали.Матч);
		ИначеЕсли ЗначениеЗаполнено(Отчет.Команда) Тогда
			График.УстановитьЗначение(УстановитьТочку(Детали), УстановитьСерию(Детали.Период), Значение(Детали), Детали.Матч, Детали.Матч);
		ИначеЕсли Показатель = 3 Тогда
			График.УстановитьЗначение(УстановитьТочку(Детали), УстановитьСерию(Детали.Команда), Детали.КоличествоПропущено, Детали.Матч, Детали.Матч);
		Иначе
			График.УстановитьЗначение(УстановитьТочку(Детали), УстановитьСерию(Детали.Команда), Значение(Детали));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция УстановитьТочку(Детали)
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		Точка = График.УстановитьТочку(Детали.Тур.Код);
	Иначе
		Точка = График.УстановитьТочку(СтрШаблон("%1/%2", ФорматЧГ(Детали.Период), Детали.Тур.Код));
		Если График.Точки.Количество() > 1 Тогда
			Точка.Текст = ФорматЧГ(Детали.Период);
		КонецЕсли;
	КонецЕсли;
	Точка.Расшифровка	= Детали.Тур;
	Возврат Точка;
КонецФункции

&НаСервереБезКонтекста
Функция Цвет(Клуб)
	Возврат СерверныйФункции.ЦветКоманды(Клуб);
КонецФункции

&НаСервере
Функция УстановитьСерию(Параметр)
	Если ТипЗнч(Параметр) = Тип("Дата") Тогда
		Серия = График.УстановитьСерию(ФорматЧГ(Параметр));
	Иначе
		Серия	= График.УстановитьСерию(Параметр);
		Серия.Расшифровка	= Параметр;
		Цвет	= Цвет(Параметр);
		Если ЗначениеЗаполнено(Цвет) Тогда
			Серия.Цвет			= Цвет;
			Серия.ПриоритетЦвета= Истина;
		КонецЕсли;
	КонецЕсли;
	Возврат Серия;
КонецФункции

&НаСервере
Функция Значение(Детали)
	Возврат ?(Показатель=0, Детали.КоличествоОчков, ?(Показатель=1, Детали.КоличествоЗабито, Детали.КоличествоПропущено));
КонецФункции

&НаСервере
Процедура ДанныеПолучить()
	Отчет.Данные.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 5
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
	                      |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания
	                      |ПОМЕСТИТЬ Последнее
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаОкончания УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ РАЗЛИЧНЫЕ
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
	                      |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания,
	                      |	ТурнирнаяТаблица.Тур.Код КАК ТурКод,
	                      |	ТурнирнаяТаблица.Тур КАК Тур
	                      |ПОМЕСТИТЬ Чемпионаты
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последнее КАК Последнее
	                      |		ПО ТурнирнаяТаблица.Тур.Владелец = Последнее.Чемпионат
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Тур.Код В
	                      |			(ВЫБРАТЬ ПЕРВЫЕ 1
	                      |				ТурнирнаяТаблица.Тур.Код
	                      |			ИЗ
	                      |				РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |					ПО
	                      |						ТурнирнаяТаблица.Тур.Владелец = ЧемпионатыКоманды.Ссылка
	                      |			ГДЕ
	                      |				ТурнирнаяТаблица.Тур.Владелец В
	                      |					(ВЫБРАТЬ ПЕРВЫЕ 1
	                      |						Последнее.Чемпионат
	                      |					ИЗ
	                      |						Последнее
	                      |					УПОРЯДОЧИТЬ ПО
	                      |						Последнее.ДатаОкончания УБЫВ)
	                      |				И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	                      |			СГРУППИРОВАТЬ ПО
	                      |				ТурнирнаяТаблица.Тур.Код
	                      |			ИМЕЮЩИЕ
	                      |				1 + 2 * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда)
	                      |			УПОРЯДОЧИТЬ ПО
	                      |				ТурнирнаяТаблица.Тур.Код УБЫВ)
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Чемпионаты.ДатаОкончания КАК Период,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод,
	                      |	МАКСИМУМ(ТурнирнаяТаблица.Тур) КАК Тур,
	                      |	ТурнирнаяТаблица.Команда КАК Команда,
	                      |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	                      |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	                      |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	                      |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
	                      |		ПО ТурнирнаяТаблица.Тур.Владелец = Чемпионаты.Чемпионат
	                      |			И ТурнирнаяТаблица.Тур.Код <= Чемпионаты.ТурКод
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Лига.Команды КАК Клубы
	                      |		ПО ТурнирнаяТаблица.Команда = Клубы.Команда
	                      |ГДЕ
	                      |	Клубы.Ссылка = &Лига
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда,
	                      |	Чемпионаты.ДатаОкончания,
	                      |	ТурнирнаяТаблица.Тур.Владелец
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Период,
	                      |	ТурКод,
	                      |	Команда
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Лига",			Отчет.Лига);
	Запрос.УстановитьПараметр("Команда",		Отчет.Команда);
		
	Если ЗначениеЗаполнено(Отчет.Команда) И Показатель = 3 Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 5
		               |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК Код
		               |ПОМЕСТИТЬ Чемпионаты
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |	И ТурнирнаяТаблица.Команда = &Команда
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Чемпионаты.Чемпионат КАК Чемпионат,
		               |	Туры.Код КАК Код
		               |ИЗ
		               |	Чемпионаты КАК Чемпионаты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			МИНИМУМ(Чемпионаты.Код) КАК Код
		               |		ИЗ
		               |			Чемпионаты КАК Чемпионаты) КАК Туры
		               |		ПО (ИСТИНА)";
		КодМакс	= 1;
		Чемпионаты	= Новый Массив;
		Выборка	= Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			КодМакс	= Выборка.Код;
			Чемпионаты.Добавить(Выборка.Чемпионат);
		КонецЦикла;
		Запрос.УстановитьПараметр("Чемпионаты",		Чемпионаты);
		Клубы	= Новый Массив;
		Клубы.Добавить(Отчет.Команда);
		
	ИначеЕсли ЗначениеЗаполнено(Отчет.Команда) Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 5
		               |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод
		               |ПОМЕСТИТЬ Чемпионаты
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Команда = &Команда
		               |	И ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ДатаОкончания УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Чемпионаты.Чемпионат КАК Чемпионат,
		               |	Чемпионаты.ДатаОкончания КАК Период,
		               |	ТурнирнаяТаблица.Тур.Код КАК ТурКод,
		               |	ТурнирнаяТаблица.Тур КАК Тур,
		               |	ТурнирнаяТаблица.Команда КАК Команда,
		               |	Противник.КоличествоГолов КАК КоличествоПропущено,
		               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоЗабито,
		               |	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков,
		               |	ТурнирнаяТаблица.Регистратор КАК Матч
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
		               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
		               |		ПО ТурнирнаяТаблица.Тур.Владелец = Чемпионаты.Чемпионат
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			МИНИМУМ(ВременнаяТаблица.ТурКод) КАК ТурКод
		               |		ИЗ
		               |			Чемпионаты КАК ВременнаяТаблица) КАК ВложенныйЗапрос
		               |		ПО ТурнирнаяТаблица.Тур.Код <= ВложенныйЗапрос.ТурКод
		               |ГДЕ
		               |	ТурнирнаяТаблица.Команда = &Команда
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период,
		               |	ТурКод";
		
	ИначеЕсли Показатель = 3 Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 5
		               |	ТурнирнаяТаблица.Тур.Владелец КАК Чемпионат,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК Код
		               |ПОМЕСТИТЬ Чемпионаты
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |	И ТурнирнаяТаблица.Тур.Код В
		               |			(ВЫБРАТЬ ПЕРВЫЕ 1
		               |				ТурнирнаяТаблица.Тур.Код
		               |			ИЗ
		               |				РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
		               |					ПО
		               |						ТурнирнаяТаблица.Тур.Владелец = ЧемпионатыКоманды.Ссылка
		               |			ГДЕ
		               |				ТурнирнаяТаблица.Тур.Владелец В
		               |					(ВЫБРАТЬ ПЕРВЫЕ 1
		               |						ТурнирнаяТаблица.Тур.Владелец
		               |					ИЗ
		               |						РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |					ГДЕ
		               |						ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |					УПОРЯДОЧИТЬ ПО
		               |						ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания УБЫВ)
		               |				И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
		               |			СГРУППИРОВАТЬ ПО
		               |				ТурнирнаяТаблица.Тур.Код
		               |			ИМЕЮЩИЕ
		               |				1 + 2 * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда)
		               |			УПОРЯДОЧИТЬ ПО
		               |				ТурнирнаяТаблица.Тур.Код УБЫВ)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец,
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ДатаОкончания УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Чемпионаты.Чемпионат КАК Чемпионат,
		               |	Туры.Код КАК Код,
		               |	Чемпионаты.ДатаОкончания КАК ДатаОкончания
		               |ИЗ
		               |	Чемпионаты КАК Чемпионаты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		               |			МИНИМУМ(Чемпионаты.Код) КАК Код
		               |		ИЗ
		               |			Чемпионаты КАК Чемпионаты) КАК Туры
		               |		ПО (ИСТИНА)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ДатаОкончания УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ ПЕРВЫЕ 6
		               |	Команды.Команда КАК Команда
		               |ИЗ
		               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |		Итоги.Команда КАК Команда,
		               |		ЛОЖЬ КАК Порядок
		               |	ИЗ
		               |		РегистрСведений.Итоги КАК Итоги
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
		               |			ПО Итоги.Чемпионат = Чемпионаты.Чемпионат
		               |	ГДЕ
		               |		Итоги.Место <= 3
		               |	
		               |	ОБЪЕДИНИТЬ
		               |	
		               |	ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 6
		               |		ЛигаКоманды.Команда,
		               |		ИСТИНА
		               |	ИЗ
		               |		Справочник.Лига.Команды КАК ЛигаКоманды
		               |	ГДЕ
		               |		ЛигаКоманды.Ссылка = &Лига) КАК Команды
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Команды.Команда
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	МАКСИМУМ(Команды.Порядок) УБЫВ";
		Таблицы	= Запрос.ВыполнитьПакет();
		Выборка	= Таблицы.Получить(1).Выбрать();
		КодМакс	= 1;
		Чемпионаты	= Новый Массив;
		Пока Выборка.Следующий() Цикл
			КодМакс	= Выборка.Код;
			Чемпионаты.Добавить(Выборка.Чемпионат);
		КонецЦикла;
		Запрос.УстановитьПараметр("Чемпионаты",		Чемпионаты);
		Клубы	= Таблицы.Получить(2).Выгрузить().ВыгрузитьКолонку(0);
	КонецЕсли;
	
	Если Показатель = 3 Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК Период,
		               |	ТурнирнаяТаблица.Команда КАК Команда,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Команда В (&Команды)) КАК Включить,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур.Код) КАК ТурКод,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Тур) КАК Тур,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Регистратор) КАК Матч
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
		               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур.Владелец В(&Чемпионаты)
		               |	И ТурнирнаяТаблица.Тур.Код <= &Код
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания,
		               |	ТурнирнаяТаблица.Команда
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период,
		               |	СУММА(ТурнирнаяТаблица.КоличествоОчков) УБЫВ,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) - ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) УБЫВ,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) УБЫВ,
		               |	СУММА(ВЫБОР
		               |			КОГДА ЕСТЬNULL(Противник.КоличествоОчков, 1) = 0
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) УБЫВ";
		Запрос.УстановитьПараметр("Команды",		Клубы);
		Для Итератор = ?(ЗначениеЗаполнено(Отчет.Команда), Макс(1, КодМакс - 4), КодМакс) По КодМакс Цикл
			Запрос.УстановитьПараметр("Код",		Итератор);
			Выборка	= Запрос.Выполнить().Выбрать();
			
			Всего	= 0;
			Итоги	= Новый Структура("Место,Период", 0, Дата(1,1,1));
			Пока Выборка.Следующий() Цикл
				Если Итоги.Период <> Выборка.Период Тогда
					Итоги.Место		= 0;
					Итоги.Период	= Выборка.Период;
				ИначеЕсли Всего = Чемпионаты.Количество() * Клубы.Количество() Тогда
					Прервать;
				КонецЕсли;
				Итоги.Место	= Итоги.Место + 1;
				Если Выборка.Включить Тогда
					Всего = Всего + 1;
					НоваяСтрока = Отчет.Данные.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					НоваяСтрока.КоличествоПропущено = Итоги.Место;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Если ЗначениеЗаполнено(Отчет.Команда) Тогда
			Отчет.Данные.Сортировать("Период УБЫВ,Тур");
		Иначе
			Отчет.Данные.Сортировать("Период,Тур,Команда");
		КонецЕсли;
		
	Иначе
		Выборка	= Запрос.Выполнить().Выбрать();
		Итоги	= Новый Структура("КоличествоПропущено,КоличествоЗабито,КоличествоОчков", 0, 0, 0);
		Период	= Дата(1,1,1);
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Отчет.Команда) Тогда
				Если Период = Выборка.Период Тогда
					Итоги.КоличествоПропущено	= Итоги.КоличествоПропущено + Выборка.КоличествоПропущено;
					Итоги.КоличествоЗабито		= Итоги.КоличествоЗабито + Выборка.КоличествоЗабито;
					Итоги.КоличествоОчков		= Итоги.КоличествоОчков + Выборка.КоличествоОчков;
					
					НоваяСтрока = Отчет.Данные.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Итоги);
				Иначе
					Период	= Выборка.Период;
					
					Итоги.КоличествоПропущено	= Выборка.КоличествоПропущено;
					Итоги.КоличествоЗабито		= Выборка.КоличествоЗабито;
					Итоги.КоличествоОчков		= Выборка.КоличествоОчков;
					
					ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
				КонецЕсли;
			Иначе
				ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НастройкиУстановить()
	Если ЗначениеЗаполнено(Отчет.Лига) Тогда
		ЛигаПриИзмененииНаСервере();
	Иначе
		Отчет.Лига = СерверныйФункции.ЛигаКоманды(МояКоманда());
		Если ЗначениеЗаполнено(Отчет.Лига) Тогда
			ЛигаПриИзмененииНаСервере();
		КонецЕсли;
		Отчет.Команда = МояКоманда();
	КонецЕсли;
	
	Элементы.Показатель.СписокВыбора.Добавить(0, "Количество очков");
	Элементы.Показатель.СписокВыбора.Добавить(1, "Забито голов");
	Элементы.Показатель.СписокВыбора.Добавить(2, "Пропущено голов");
	Элементы.Показатель.СписокВыбора.Добавить(3, "Занятое место");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастройкиУстановить();
	КартинкиЗагрузить();
	Сформировать();
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаКлиенте
Процедура КомандаОчистка(Элемент, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Команда) И Элементы.Команда.СписокВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка	= Ложь;
		Клуб	= МояКоманда();
		Если Элементы.Команда.СписокВыбора.НайтиПоЗначению(Клуб) <> Неопределено Тогда
			Отчет.Команда			= Клуб;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЛигаПриИзмененииНаСервере()
	КомандыЛиги.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Отчет.Лига));
	Элементы.Команда.СписокВыбора.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Отчет.Лига, Истина));
КонецПроцедуры

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент)
	Отчет.Команда	= Неопределено;
	ЛигаПриИзмененииНаСервере();
	ПодключитьОбработчикОжидания("КартинкиЗагрузить", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КартинкиЗагрузить() Экспорт
	ОбщегоНазначения.КартинкиЗагрузить(Элементы.Команда.СписокВыбора, Кеш);
КонецПроцедуры
