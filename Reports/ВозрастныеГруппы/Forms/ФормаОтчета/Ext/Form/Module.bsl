
&НаКлиенте
Процедура Сформировать(Команда)
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДанныеЗагрузить(ТЧ=Неопределено)
	Если ТЧ = Неопределено Тогда
		Отчет.Данные.Очистить();
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	""младше 21"" КАК Группа,
		                      |	16 КАК Мин,
		                      |	20 КАК Макс
		                      |ПОМЕСТИТЬ ВТ
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""21-25"",
		                      |	21,
		                      |	25
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""26-30"",
		                      |	26,
		                      |	30
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""старше 30"",
		                      |	31,
		                      |	99
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	ПеремещенияИгроков.Игрок КАК Игрок,
		                      |	ПеремещенияИгроков.Команда КАК Команда,
		                      |	ВТ.Группа КАК Группа,
		                      |	РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) КАК Количество
		                      |ИЗ
		                      |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ГОД(Игрок.ДатаРождения) >= 1900) КАК ПеремещенияИгроков
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		                      |		ПО ПеремещенияИгроков.Игрок = Игроки.Ссылка
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ КАК ВТ
		                      |		ПО (РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) МЕЖДУ ВТ.Мин И ВТ.Макс)
		                      |ГДЕ
		                      |	ПеремещенияИгроков.Команда В(&Команды)
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	Игрок
		                      |АВТОУПОРЯДОЧИВАНИЕ
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	ПеремещенияИгроков.Команда КАК Команда,
		                      |	ВТ.Группа КАК Группа,
		                      |	СУММА(1) КАК Количество
		                      |ИЗ
		                      |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ГОД(Игрок.ДатаРождения) >= 1900) КАК ПеремещенияИгроков
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		                      |		ПО ПеремещенияИгроков.Игрок = Игроки.Ссылка
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ КАК ВТ
		                      |		ПО (РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) МЕЖДУ ВТ.Мин И ВТ.Макс)
		                      |ГДЕ
		                      |	ПеремещенияИгроков.Команда В(&Команды)
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	ПеремещенияИгроков.Команда,
		                      |	ВТ.Группа,
		                      |	ВТ.Мин
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	Команда,
		                      |	ВТ.Мин
		                      |АВТОУПОРЯДОЧИВАНИЕ");
		Запрос.УстановитьПараметр("Команды",		_Команды);
		Запрос.УстановитьПараметр("Дата",			ТекущаяДатаСеанса());
		Таблицы	= Запрос.ВыполнитьПакет();
		ДанныеЗагрузить(Таблицы.Получить(2));
		ДанныеЗагрузить(Таблицы.Получить(1));
	Иначе
		Выборка	= ТЧ.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОбновить()
	ДанныеЗагрузить();
	
	Команда	= Неопределено;
	График.Очистить();
	Для Каждого Детали Из Отчет.Данные Цикл
		Если ЗначениеЗаполнено(Детали.Игрок) Тогда
			Прервать;
		Иначе
			Если Команда <> Детали.Команда Тогда
				Точка	= График.УстановитьТочку(Детали.Команда);
				Точка.Расшифровка	= Детали.Команда;

				Команда	=  Детали.Команда;
			КонецЕсли;
			График.УстановитьЗначение(Точка, График.УстановитьСерию(Детали.Группа), Детали.Количество);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ИгрокиПолучить(Отбор)
	Ответ	= Новый СписокЗначений;
	Данные	= Отчет.Данные.НайтиСтроки(Отбор);
	Для Каждого Детали Из Данные Цикл
		Если Ответ.Количество() = 11 Тогда
			Прервать;
		ИначеЕсли ЗначениеЗаполнено(Детали.Игрок) Тогда
			Ответ.Добавить(Детали.Игрок, СтрШаблон("%1 (%2)", Детали.Игрок, Детали.Количество));
		КонецЕсли;
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если _Команды.Количество() = 0 Тогда
		КомандыЗагрузить(Истина);
	КонецЕсли;
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандыЗагрузить(Топ6=Ложь)
	Если ТипЗнч(Топ6) = Тип("СправочникСсылка.Лига") Тогда
		_Команды.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Топ6, Истина));
	ИначеЕсли ТипЗнч(Топ6) = Тип("СправочникСсылка.Чемпионаты") Тогда
		_Команды.ЗагрузитьЗначения(СерверныйФункции.КомандыЧемпионата(Топ6));
	Иначе
		МояКоманда	= СерверныйФункции.МояКомандаПолучить();
		Если Топ6 Тогда
			Лига		= СерверныйФункции.ЛигаКоманды(МояКоманда);
			КомандыЗагрузить(Лига);
		Иначе
			Чемпионат	= СерверныйФункции.ЧемпионатПолучитьПоследнее(МояКоманда);
			КомандыЗагрузить(Чемпионат);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	КомандыЗагрузить();
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	Если ТипЗнч(ЗначениеДиаграммы) = Тип("ЗначениеДиаграммы") Тогда
		СтандартнаяОбработка	= Ложь;
		Игроки	= ИгрокиПолучить(Новый Структура("Команда,Группа", ЗначениеДиаграммы.Точка.Значение, ЗначениеДиаграммы.Серия.Значение));
		Если Игроки.Количество() = 1 Тогда
			ПослеВыбораКоманды(Игроки.Получить(0));
		ИначеЕсли Игроки.Количество() > 0 Тогда
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПослеВыбораКоманды", ЭтотОбъект), Игроки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКоманды(Элемент, ПараметрКоманды=Неопределено) Экспорт
	НаКлиенте.Показать(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КомандыПриИзменении(Элемент)
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандыОчистка(Элемент, СтандартнаяОбработка)
	Если _Команды.Количество() = 0 Тогда
		СтандартнаяОбработка	= Ложь;
		КомандыЗагрузить(Истина);
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Команда") Тогда
		_Команды.Добавить(Параметры.Команда);
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
		
		График.ТипДиаграммы	= ТипДиаграммы.КруговаяОбъемная;
		
	ИначеЕсли Параметры.Свойство("Лига") Тогда
		КомандыЗагрузить(Параметры.Лига);
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
		
	ИначеЕсли Параметры.Свойство("Чемпионат") Тогда
		КомандыЗагрузить(Параметры.Чемпионат);
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
	КонецЕсли;
КонецПроцедуры
