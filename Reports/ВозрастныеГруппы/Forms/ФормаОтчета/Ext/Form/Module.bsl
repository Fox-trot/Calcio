
&НаСервере
Процедура ДанныеЗагрузить(ТЧ=Неопределено)
	Если ТЧ = Неопределено Тогда
		Отчет.Данные.Очистить();
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	""младше 21"" КАК Группа,
		                      |	16 КАК Мин,
		                      |	20 КАК Макс
		                      |ПОМЕСТИТЬ ВТ
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""21-25"",
		                      |	21,
		                      |	25
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""26-30"",
		                      |	26,
		                      |	30
		                      |
		                      |ОБЪЕДИНИТЬ ВСЕ
		                      |
		                      |ВЫБРАТЬ
		                      |	""старше 30"",
		                      |	31,
		                      |	99
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	Игроки.Ссылка КАК Игрок,
		                      |	ПеремещенияИгроков.Команда КАК Команда,
		                      |	ВТ.Группа КАК Группа,
		                      |	РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) КАК Возраст
		                      |ИЗ
		                      |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ) КАК ПеремещенияИгроков
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		                      |		ПО ПеремещенияИгроков.Игрок = Игроки.Ссылка
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ КАК ВТ
		                      |		ПО (РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) МЕЖДУ ВТ.Мин И ВТ.Макс)
		                      |ГДЕ
		                      |	ПеремещенияИгроков.Команда В(&Команды)
		                      |	И ГОД(Игроки.ДатаРождения) >= 1900
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	Игрок
		                      |АВТОУПОРЯДОЧИВАНИЕ
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	ПеремещенияИгроков.Команда КАК Команда,
		                      |	ВТ.Группа КАК Группа,
		                      |	СУММА(1) КАК Количество
		                      |ИЗ
		                      |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, ) КАК ПеремещенияИгроков
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Игроки КАК Игроки
		                      |		ПО ПеремещенияИгроков.Игрок = Игроки.Ссылка
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ КАК ВТ
		                      |		ПО (РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ГОД) МЕЖДУ ВТ.Мин И ВТ.Макс)
		                      |ГДЕ
		                      |	ПеремещенияИгроков.Команда В(&Команды)
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	ПеремещенияИгроков.Команда,
		                      |	ВТ.Группа,
		                      |	ВТ.Мин
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	ВТ.Мин,
		                      |	Команда
		                      |АВТОУПОРЯДОЧИВАНИЕ");
		Запрос.УстановитьПараметр("Команды",		_Команды);
		Запрос.УстановитьПараметр("Дата",			ТекущаяДатаСеанса());
		Таблицы	= Запрос.ВыполнитьПакет();
		ДанныеЗагрузить(Таблицы.Получить(1));
		ДанныеЗагрузить(Таблицы.Получить(2));
	Иначе
		Выборка	= ТЧ.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ГрафикОбновитьНаСервере()
	ДанныеЗагрузить();
	
	График.Очистить();
	Для Каждого Детали Из Отчет.Данные Цикл
		Если НЕ ЗначениеЗаполнено(Детали.Игрок) Тогда
			График.УстановитьЗначение(График.УстановитьТочку(Детали.Команда), График.УстановитьСерию(Детали.Группа), Детали.Количество, ИгрокиПолучить(Новый Структура("Команда,Группа", Детали.Команда, Детали.Группа)));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ИгрокиПолучить(Отбор)
	Ответ	= Новый СписокЗначений;
	Данные	= Отчет.Данные.НайтиСтроки(Отбор);
	Для Каждого Детали Из Данные Цикл
		Если ЗначениеЗаполнено(Детали.Игрок) Тогда
			Ответ.Добавить(Детали.Игрок, СтрШаблон("%1 (%2)", Детали.Игрок, Детали.Возраст));
		КонецЕсли;
	КонецЦикла;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если _Команды.Количество() = 0 Тогда
		КомандыЗагрузить(Истина);
	КонецЕсли;
	Если ПроверитьЗаполнение() Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандыЗагрузить(Топ6=Ложь)
	МояКоманда	= СерверныйФункции.МояКомандаПолучить();
	Если Топ6 Тогда
		Лига		= СерверныйФункции.ЛигаКоманды(МояКоманда);
		_Команды.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Лига, Истина));
	Иначе
		Чемпионат	= СерверныйФункции.ЧемпионатПолучитьПоследнее(МояКоманда);
		_Команды.ЗагрузитьЗначения(СерверныйФункции.КомандыЧемпионата(Чемпионат));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	КомандыЗагрузить();
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	СтандартнаяОбработка	= Ложь;
	Если ТипЗнч(ЗначениеДиаграммы) = Тип("ЗначениеДиаграммы") Тогда
		ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПослеВыбораКоманды", ЭтотОбъект), ЗначениеДиаграммы.Расшифровка);
	ИначеЕсли ТипЗнч(ЗначениеДиаграммы) = Тип("ТочкаДиаграммы") Тогда
		ПоказатьЗначение(, ЗначениеДиаграммы.Значение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКоманды(Элемент, ПараметрКоманды) Экспорт
	Если ТипЗнч(Элемент) = Тип("ЭлементСпискаЗначений") Тогда
		ПоказатьЗначение(, Элемент.Значение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандыПриИзменении(Элемент)
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандыОчистка(Элемент, СтандартнаяОбработка)
	Если _Команды.Количество() = 0 Тогда
		СтандартнаяОбработка	= Ложь;
		КомандыЗагрузить(Истина);
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры
