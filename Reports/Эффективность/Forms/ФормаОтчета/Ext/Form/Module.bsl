
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(Отчет.Лига) Тогда
		Команда		= МояКоманда();
		Отчет.Лига	= Лига();
	КонецЕсли;
	ЛигаПриИзменении();
	Сформировать();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	Если ЗначениеЗаполнено(Отчет.Лига) Тогда
		Результат = РеквизитФормыВЗначение("Отчет").ОтчетСформировать(Отчет, Период.ДатаНачала, Период.ДатаОкончания);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ГрафикОбновить()
	Если ЗначениеЗаполнено(Команда) Тогда
		мДанные = Отчет.Данные.НайтиСтроки(Новый Структура("Команда", Команда));
		СерверныйФункции.ГрафикОбновить(График, мДанные);
		Для Каждого ТекСтрока Из мДанные Цикл
			График.Точки.Получить(0).Текст = ТекСтрока.Период;
			
			Если ТекСтрока.Проигрыш1 > 0 Или ТекСтрока.Ничья1 > 0 Или ТекСтрока.Выигрыш1 > 0 Тогда
				Точка = График.УстановитьТочку(ТекСтрока.Период1);
				График.УстановитьЗначение(Точка, 0, ТекСтрока.Проигрыш1);
				График.УстановитьЗначение(Точка, 1, ТекСтрока.Ничья1);
				График.УстановитьЗначение(Точка, 2, ТекСтрока.Выигрыш1);
			КонецЕсли;
		КонецЦикла;
	Иначе
		График.Точки.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		СформироватьНаСервере();
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция Лига()
	Возврат СерверныйФункции.ЛигаКоманды(Команда);
КонецФункции

&НаСервере
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Период.ДатаНачала	= Дата(2024, 11, 1);
	Период.ДатаОкончания= Дата(2025, 12, 29);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты") И Параметр = Отчет.Чемпионат Тогда
		ПодключитьОбработчикОжидания("КомандаСформировать", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформировать() Экспорт
	Сформировать();
КонецПроцедуры

&НаСервере
Процедура ЛигаПриИзмененииНаСервере()
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |	ТурнирнаяТаблица.Команда КАК Команда
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.Период >= &ДатаН
	      	               |	И ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига");
	Запрос.УстановитьПараметр("ДатаН",			Период.ДатаНачала);
	Запрос.УстановитьПараметр("Лига",			Отчет.Лига);
	Клубы.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
	Если Клубы.Количество() = 0 Тогда
		Команда = Неопределено;
	Иначе
		Если Клубы.НайтиПоЗначению(Команда) = Неопределено Тогда
			Команда = Неопределено;
		КонецЕсли;
		Элементы.Команда.СписокВыбора.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Отчет.Лига, Истина));
	КонецЕсли;
	
	СформироватьНаСервере();
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура КартинкиЗагрузить() Экспорт
	ОбщегоНазначения.КартинкиЗагрузить(Элементы.Команда.СписокВыбора, Кеш);
КонецПроцедуры

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент=Неопределено)
	ЛигаПриИзмененииНаСервере();
	ПодключитьОбработчикОжидания("КартинкиЗагрузить", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КомандаПриИзменении(Элемент=Неопределено)
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Команда) Тогда
		НаКлиенте.Показать(Команда);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Команды") Тогда
		СтандартнаяОбработка = Ложь;
		
		Команда = Расшифровка;
		КомандаПриИзменении();
	КонецЕсли;
КонецПроцедуры
