
Функция ОтчетСформировать(Отчет, ДатаН, ДатаК) Экспорт
	Отчет.Данные.Очистить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.Вывести(ЗаголовокПолучить());
	
	ПоказыватьРазницуМячей	= СерверныйФункции.ПоказыватьРазницуМячей();
	Итого	= Новый Структура("Место,Период,Период1", 0, "", "");
	
	Макет	= ПолучитьОбщийМакет("ТурнирнаяТаблица");
	
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ТурнирнаяТаблица.Команда КАК Команда,
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК КоличествоИгр,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Противник.КоличествоОчков = 0
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Выигрыш,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = Противник.КоличествоОчков
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Ничья,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = 0
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Проигрыш,
	      	               |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	      	               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
	      	               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
	      	               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) - СУММА(Противник.КоличествоГолов) КАК Разница
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	      	               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	      	               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.Период МЕЖДУ &ДатаН И &ДатаК
	      	               |	И ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	ТурнирнаяТаблица.Команда
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	КоличествоОчков УБЫВ");
	Запрос.УстановитьПараметр("ДатаН",			ДатаН);
	Запрос.УстановитьПараметр("ДатаК",			ДатаК);
	Запрос.УстановитьПараметр("Лига",			Лига);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаИ|Команды"));
		Итого.Период = СтрШаблон("%1-%2", Формат(ДатаН, "ДФ=d.M.yy"), Формат(ДатаК, "ДФ=d.M.yyyy"));
		ОбластьМакета = Макет.ПолучитьОбласть("ШапкаИ|История");
		ОбластьМакета.Параметры.Заполнить(Итого);
		ТабДокумент.Присоединить(ОбластьМакета);
		
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
		               |	ТурнирнаяТаблица.Тур КАК Тур
		               |ПОМЕСТИТЬ Туры
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Период < &ДатаН
		               |	И ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания,
		               |	ТурнирнаяТаблица.Тур
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания УБЫВ,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Период) УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ТурнирнаяТаблица.Команда КАК Команда,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК КоличествоИгр,
		               |	СУММА(ВЫБОР
		               |			КОГДА Противник.КоличествоОчков = 0
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Выигрыш,
		               |	СУММА(ВЫБОР
		               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = Противник.КоличествоОчков
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Ничья,
		               |	СУММА(ВЫБОР
		               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = 0
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Проигрыш,
		               |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
		               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) - СУММА(Противник.КоличествоГолов) КАК Разница,
		               |	МИНИМУМ(ТурнирнаяТаблица.Период) КАК ПериодМини,
		               |	МАКСИМУМ(ТурнирнаяТаблица.Период) КАК ПериодМакс
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
		               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур В
		               |			(ВЫБРАТЬ
		               |				Туры.Тур
		               |			ИЗ
		               |				Туры)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Команда
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	КоличествоОчков УБЫВ";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "11", ФорматЧГ(Выборка.КоличествоИгр));
		История	= Запрос.Выполнить().Выгрузить();
		Если История.Количество() > 0 Тогда
			сПериод	= Новый Структура("ПериодМини,ПериодМакс", ДатаН, ДатаН - Сутки());
			Для Каждого ТекСтрока Из История Цикл
				сПериод.ПериодМини	= Мин(сПериод.ПериодМини, ТекСтрока.ПериодМини);
			КонецЦикла;
			Итого.Период1 = СтрШаблон("%1-%2", Формат(сПериод.ПериодМини, "ДФ=d.M.yy"), Формат(сПериод.ПериодМакс, "ДФ=d.M.yyyy"));
			ОбластьМакета.Параметры.Период = Итого.Период1;
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		Выборка.Сбросить();
		
		Пока Выборка.Следующий() Цикл
			Итого.Место	= Итого.Место + 1;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Детали1|Начало");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			Если НЕ ПоказыватьРазницуМячей Тогда
				ОбластьМакета.Параметры.Разница	= СтрШаблон("%1-%2", Выборка.КоличествоЗабито, Выборка.КоличествоПропущено);
			КонецЕсли;
			ОбластьМакета.Параметры.Место	= Итого.Место;
			ТабДокумент.Вывести(ОбластьМакета);
			
			НовСтрока = Отчет.Данные.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
			ЗаполнитьЗначенияСвойств(НовСтрока, Итого);
			
			Если История.Количество() > 0 Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Детали1|История");
				мИстория = История.НайтиСтроки(Новый Структура("Команда", Выборка.Команда));
				Для Каждого ТекСтрока Из мИстория Цикл
					ОбластьМакета.Параметры.Заполнить(ТекСтрока);
					Если НЕ ПоказыватьРазницуМячей Тогда
						ОбластьМакета.Параметры.Разница	= СтрШаблон("%1-%2", ТекСтрока.КоличествоЗабито, ТекСтрока.КоличествоПропущено);
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(НовСтрока, Новый Структура("Выигрыш1,Ничья1,Проигрыш1", ТекСтрока.Выигрыш, ТекСтрока.Ничья, ТекСтрока.Проигрыш));
					Прервать;
				КонецЦикла;
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции

Функция ЗаголовокПолучить()
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат", Лига, Лига));
КонецФункции
