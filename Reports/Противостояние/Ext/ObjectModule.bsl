
Функция ОтчетСформировать() Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Лига	= Чемпионат.Владелец;
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |	МатчКоманды.Ссылка КАК Ссылка,
	      	               |	МатчКоманды.КоличествоГолов КАК КоличествоГолов
	      	               |ПОМЕСТИТЬ Матчи
	      	               |ИЗ
	      	               |	Документ.Матч.Команды КАК МатчКоманды
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	      	               |			ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
	      	               |		ПО МатчКоманды.Ссылка = ТурнирнаяТаблица.Регистратор
	      	               |ГДЕ
	      	               |	МатчКоманды.Команда = &Команда
	      	               |	И Туры.Владелец = &Чемпионат
	      	               |;
	      	               |
	      	               |////////////////////////////////////////////////////////////////////////////////
	      	               |ВЫБРАТЬ
	      	               |	ЧемпионатыКоманды.Команда КАК Команда,
	      	               |	МАКСИМУМ(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки ЕСТЬ NULL
	      	               |				ТОГДА -1
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 2
	      	               |					И ТурнирнаяТаблица.КоличествоОчков = 0
	      	               |				ТОГДА 3
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 2
	      	               |					И ТурнирнаяТаблица.КоличествоОчков = 1
	      	               |				ТОГДА 1
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 2
	      	               |					И ТурнирнаяТаблица.КоличествоОчков > 1
	      	               |				ТОГДА 0
	      	               |			ИНАЧЕ -1
	      	               |		КОНЕЦ) КАК H,
	      	               |	МАКСИМУМ(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки ЕСТЬ NULL
	      	               |				ТОГДА -1
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 1
	      	               |					И ТурнирнаяТаблица.КоличествоОчков = 0
	      	               |				ТОГДА 3
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 1
	      	               |					И ТурнирнаяТаблица.КоличествоОчков = 1
	      	               |				ТОГДА 1
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки = 1
	      	               |					И ТурнирнаяТаблица.КоличествоОчков > 1
	      	               |				ТОГДА 0
	      	               |			ИНАЧЕ -1
	      	               |		КОНЕЦ) КАК A,
	      	               |	СУММА(ЕСТЬNULL(Матчи.КоличествоГолов, 0)) КАК КоличествоГолов,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.НомерСтроки ЕСТЬ NULL
	      	               |				ТОГДА 0
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = 0
	      	               |				ТОГДА 3
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = 1
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК КоличествоОчков,
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК КоличествоИгр
	      	               |ИЗ
	      	               |	Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	      	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Матчи КАК Матчи
	      	               |			ПО ТурнирнаяТаблица.Регистратор = Матчи.Ссылка
	      	               |		ПО ЧемпионатыКоманды.Команда = ТурнирнаяТаблица.Команда
	      	               |ГДЕ
	      	               |	ЧемпионатыКоманды.Ссылка = &Чемпионат
	      	               |	И ЧемпионатыКоманды.Команда <> &Команда
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	ЧемпионатыКоманды.Команда
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	КоличествоОчков УБЫВ,
	      	               |	КоличествоИгр УБЫВ,
	      	               |	КоличествоГолов УБЫВ,
	      	               |	Команда
	      	               |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Чемпионат",				Чемпионат);
	Запрос.УстановитьПараметр("Команда",				Команда);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Макет  = ПолучитьМакет("Макет");
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Заполнить(Новый Структура("Команда,Чемпионат,Лига", Команда, Чемпионат, Лига));
	Изображение	= СерверныйФункции.ИзображениеПолучить(Лига);
	Если Изображение.Вид <> ВидКартинки.Пустая Тогда
		ОбластьМакета.Рисунки.Получить(0).Картинка = Изображение;
	КонецЕсли;
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка"));
	ТабДокумент.ФиксацияСверху = 3;
	Итого	= Новый Структура("H,A,КоличествоГолов,КоличествоИгр,КоличествоОчков", 0, 0, 0, 0, 0);
	Пока Выборка.Следующий() Цикл
		ОбластьМакета = Макет.ПолучитьОбласть("Детали|Начало");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ТабДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("Детали|" + ?(Выборка.H=0, "LL", ?(Выборка.H=1, "DD", ?(Выборка.H=3, "WW", "NN"))));
		ТабДокумент.Присоединить(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("Детали|" + ?(Выборка.A=0, "LL", ?(Выборка.A=1, "DD", ?(Выборка.A=3, "WW", "NN"))));
		ТабДокумент.Присоединить(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("Детали|Конец");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ТабДокумент.Присоединить(ОбластьМакета);
		Если Выборка.H > 0 Тогда
			Итого.H	= Итого.H + Выборка.H;
		КонецЕсли;
		Если Выборка.A > 0 Тогда
			Итого.A	= Итого.A + Выборка.A;
		КонецЕсли;
		Итого.КоличествоГолов	= Итого.КоличествоГолов + Выборка.КоличествоГолов;
		Итого.КоличествоИгр	 	= Итого.КоличествоИгр + Выборка.КоличествоИгр;
		Итого.КоличествоОчков	= Итого.КоличествоОчков + Выборка.КоличествоОчков;
	КонецЦикла;
	ОбластьМакета = Макет.ПолучитьОбласть("Итого");
	ОбластьМакета.Параметры.Заполнить(Итого);
	ТабДокумент.Вывести(ОбластьМакета);
	
	Возврат ТабДокумент;
КонецФункции
