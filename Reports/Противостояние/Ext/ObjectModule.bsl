
Функция ОтчетСформировать(Объект, ПоказыватьРазницуМячей=Ложь) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	Объект.Данные.Очистить();
	
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	Противник.Команда КАК Команда,
	      	               |	МАКСИМУМ(ВЫБОР
	      	               |			КОГДА Матчи.НомерСтроки = 1
	      	               |				ТОГДА Матчи.Регистратор
	      	               |			ИНАЧЕ NULL
	      	               |		КОНЕЦ) КАК МатчД,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Матчи.НомерСтроки = 1
	      	               |				ТОГДА Матчи.КоличествоГолов
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Дома,
	      	               |	МАКСИМУМ(ВЫБОР
	      	               |			КОГДА Матчи.НомерСтроки = 2
	      	               |				ТОГДА Противник.Регистратор
	      	               |			ИНАЧЕ NULL
	      	               |		КОНЕЦ) КАК МатчВ,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Матчи.НомерСтроки = 2
	      	               |				ТОГДА Матчи.КоличествоГолов
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Выезд,
	      	               |	СУММА(Матчи.КоличествоГолов) КАК КоличествоЗабито,
	      	               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
	      	               |	СУММА(Матчи.КоличествоОчков) КАК КоличествоОчков,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Матчи.КоличествоОчков > Противник.КоличествоОчков
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Выигрыш,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Матчи.КоличествоОчков = Противник.КоличествоОчков
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Ничья,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Матчи.КоличествоОчков > Противник.КоличествоОчков
	      	               |				ТОГДА 0
	      	               |			ИНАЧЕ 1
	      	               |		КОНЕЦ) КАК Проигрыш,
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Противник.Регистратор) КАК КоличествоИгр
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК Противник
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Матчи
	      	               |		ПО Противник.Регистратор = Матчи.Регистратор
	      	               |ГДЕ
	      	               |	Противник.Команда В
	      	               |			(ВЫБРАТЬ
	      	               |				ЧемпионатыКоманды.Команда КАК Команда
	      	               |			ИЗ
	      	               |				Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	      	               |			ГДЕ
	      	               |				ЧемпионатыКоманды.Ссылка В
	      	               |					(ВЫБРАТЬ ПЕРВЫЕ 3
	      	               |						ЧемпионатыКоманды.Ссылка КАК Ссылка
	      	               |					ИЗ
	      	               |						Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	      	               |					ГДЕ
	      	               |						(ЧемпионатыКоманды.Ссылка.Владелец.Страна.Ссылка ЕСТЬ NULL) = ЛОЖЬ
	      	               |						И ЧемпионатыКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
	      	               |						И ЧемпионатыКоманды.Команда = &Команда
	      	               |					УПОРЯДОЧИТЬ ПО
	      	               |						ЧемпионатыКоманды.Ссылка.ДатаОкончания УБЫВ)
	      	               |				И ЧемпионатыКоманды.Команда <> &Команда)
						   //|	И Противник.Тур.Владелец В
						   //|			(ВЫБРАТЬ
						   //|				ЧемпионатыКоманды.Ссылка КАК Ссылка
						   //|			ИЗ
						   //|				Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
						   //|			ГДЕ
						   //|				ЧемпионатыКоманды.Команда = &Команда
						   //|				И ЧемпионатыКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
						   //|				И ЧемпионатыКоманды.Ссылка.Владелец.Страна В
						   //|					(ВЫБРАТЬ
						   //|						Команды.Город.Владелец КАК Ссылка
						   //|					ИЗ
						   //|						Справочник.Команды КАК Команды
						   //|					ГДЕ
						   //|						Команды.Ссылка = &Команда))
	      	               |	И Матчи.Команда = &Команда
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	Противник.Команда
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Команда
	      	               |АВТОУПОРЯДОЧИВАНИЕ");
	Если ЗначениеЗаполнено(Чемпионат) Тогда
		Запрос.Текст = "ВЫБРАТЬ
		               |	ЧемпионатыКоманды.Команда КАК Команда,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА Матчи.НомерСтроки = 1
		               |				ТОГДА Матчи.Регистратор
		               |			ИНАЧЕ NULL
		               |		КОНЕЦ) КАК МатчД,
		               |	СУММА(ВЫБОР
		               |			КОГДА Матчи.НомерСтроки ЕСТЬ NULL
		               |				ТОГДА -1
		               |			КОГДА Матчи.НомерСтроки = 1
		               |				ТОГДА Матчи.КоличествоОчков
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Дома,
		               |	МАКСИМУМ(ВЫБОР
		               |			КОГДА Матчи.НомерСтроки = 2
		               |				ТОГДА Матчи.Регистратор
		               |			ИНАЧЕ NULL
		               |		КОНЕЦ) КАК МатчВ,
		               |	СУММА(ВЫБОР
		               |			КОГДА Матчи.НомерСтроки ЕСТЬ NULL
		               |				ТОГДА -1
		               |			КОГДА Матчи.НомерСтроки = 2
		               |				ТОГДА Матчи.КоличествоОчков
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Выезд,
		               |	СУММА(ЕСТЬNULL(Матчи.КоличествоГолов, 0)) КАК КоличествоЗабито,
		               |	СУММА(ЕСТЬNULL(Противник.КоличествоГолов, 0)) КАК КоличествоПропущено,
		               |	СУММА(ЕСТЬNULL(Матчи.КоличествоОчков, 0)) КАК КоличествоОчков,
		               |	СУММА(ВЫБОР
		               |			КОГДА Матчи.КоличествоОчков > Противник.КоличествоОчков
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Выигрыш,
		               |	СУММА(ВЫБОР
		               |			КОГДА Матчи.КоличествоОчков = Противник.КоличествоОчков
		               |				ТОГДА 1
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Ничья,
		               |	СУММА(ВЫБОР
		               |			КОГДА Матчи.КоличествоОчков > Противник.КоличествоОчков
		               |				ТОГДА 0
		               |			ИНАЧЕ 1
		               |		КОНЕЦ) КАК Проигрыш,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Противник.Регистратор) КАК КоличествоИгр
		               |ИЗ
		               |	Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |				МатчКоманды.Регистратор КАК Регистратор,
		               |				МатчКоманды.НомерСтроки КАК НомерСтроки,
		               |				МатчКоманды.КоличествоОчков КАК КоличествоОчков,
		               |				МатчКоманды.КоличествоГолов КАК КоличествоГолов
		               |			ИЗ
		               |				РегистрНакопления.ТурнирнаяТаблица КАК МатчКоманды
		               |					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |					ПО МатчКоманды.Регистратор = ТурнирнаяТаблица.Регистратор
		               |						И МатчКоманды.НомерСтроки <> ТурнирнаяТаблица.НомерСтроки
		               |			ГДЕ
		               |				МатчКоманды.Команда = &Команда
		               |				И ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		               |				И ТурнирнаяТаблица.Тур.ОлимпийскаяСистема = ЛОЖЬ) КАК Матчи
		               |			ПО Противник.Регистратор = Матчи.Регистратор
		               |		ПО ЧемпионатыКоманды.Команда = Противник.Команда
		               |ГДЕ
		               |	ЧемпионатыКоманды.Ссылка = &Чемпионат
		               |	И ЧемпионатыКоманды.Команда <> &Команда
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ЧемпионатыКоманды.Команда,
		               |	ЧемпионатыКоманды.Ссылка.Владелец.ОлимпийскаяСистема
		               |
		               |ИМЕЮЩИЕ
		               |	(МАКСИМУМ(ЧемпионатыКоманды.Ссылка.Владелец.ОлимпийскаяСистема) = ИСТИНА
		               |			И КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Противник.Регистратор) > 0
		               |		ИЛИ МАКСИМУМ(ЧемпионатыКоманды.Ссылка.Владелец.ОлимпийскаяСистема) = ЛОЖЬ)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	КоличествоОчков УБЫВ,
		               |	КоличествоИгр,
		               |	КоличествоЗабито УБЫВ,
		               |	Дома УБЫВ,
		               |	Команда
		               |АВТОУПОРЯДОЧИВАНИЕ";
	КонецЕсли;
	Запрос.УстановитьПараметр("Чемпионат",				Чемпионат);
	Запрос.УстановитьПараметр("Команда",				Команда);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Лига	= Чемпионат.Владелец;
	ТабДокумент.Вывести(ЗаголовокПолучить(Лига));
	
	Макет  = ПолучитьОбщийМакет("ТурнирнаяТаблица");
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("ШапкаП"));
	ТабДокумент.ФиксацияСверху	= 3;
	
	Итого	= Новый Структура("НомерСтроки,Дома,Выезд,КоличествоЗабито,КоличествоПропущено,КоличествоИгр,КоличествоОчков", 0, 0, 0, 0, 0, 0, 0);
	Пока Выборка.Следующий() Цикл
		Итого.НомерСтроки	= Итого.НомерСтроки + 1;
		Если ЗначениеЗаполнено(Чемпионат) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ДеталиП|НачалоП");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.НомерСтроки;
			ТабДокумент.Вывести(ОбластьМакета);
			Присоединить(ТабДокумент, Макет, Выборка.Дома, Выборка.МатчД);
			Присоединить(ТабДокумент, Макет, Выборка.Выезд, Выборка.МатчВ);
			ОбластьМакета = Макет.ПолучитьОбласть("ДеталиП|КонецП");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			Если НЕ ПоказыватьРазницуМячей Тогда
				ОбластьМакета.Параметры.КоличествоЗабито	= СтрШаблон("%1-%2", Выборка.КоличествоЗабито, Выборка.КоличествоПропущено);
			КонецЕсли;
			ТабДокумент.Присоединить(ОбластьМакета);
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("ДеталиП1");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.НомерСтроки;
			Если НЕ ПоказыватьРазницуМячей Тогда
				ОбластьМакета.Параметры.КоличествоЗабито	= СтрШаблон("%1-%2", Выборка.КоличествоЗабито, Выборка.КоличествоПропущено);
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		Если Выборка.Дома > 0 Тогда
			Итого.Дома	= Итого.Дома + Выборка.Дома;
		КонецЕсли;
		Если Выборка.Выезд > 0 Тогда
			Итого.Выезд	= Итого.Выезд + Выборка.Выезд;
		КонецЕсли;
		Итого.КоличествоЗабито		= Итого.КоличествоЗабито + Выборка.КоличествоЗабито;
		Итого.КоличествоПропущено	= Итого.КоличествоПропущено + Выборка.КоличествоПропущено;
		Итого.КоличествоИгр	 		= Итого.КоличествоИгр + Выборка.КоличествоИгр;
		Итого.КоличествоОчков		= Итого.КоличествоОчков + Выборка.КоличествоОчков;
		
		Если ЗначениеЗаполнено(Чемпионат) Тогда
			ЗаполнитьЗначенияСвойств(Объект.Данные.Добавить(),
				Новый Структура("Команда,Выигрыш,Ничья,Проигрыш,КоличествоЗабито,КоличествоПропущено",
				Выборка.Команда,
				?(Выборка.Дома>1, 1, 0) + ?(Выборка.Выезд>1, 1, 0),
				?(Выборка.Дома=1, 1, 0) + ?(Выборка.Выезд=1, 1, 0),
				?(Выборка.Дома=0, 1, 0) + ?(Выборка.Выезд=0, 1, 0),
				Выборка.КоличествоЗабито, Выборка.КоличествоПропущено));
		Иначе
			ЗаполнитьЗначенияСвойств(Объект.Данные.Добавить(), Выборка);
		КонецЕсли;
	КонецЦикла;
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда
		Объект.Данные.Сортировать(?(ПоказыватьРазницуМячей, "Проигрыш Убыв", "КоличествоЗабито Убыв"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Чемпионат) Тогда
		Выборка	= КомментарииПолучить();
		Пока Выборка.Следующий() Цикл
			Итого.КоличествоОчков	= Итого.КоличествоОчков + Выборка.КоличествоОчков;
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("ИтогоП");
		ОбластьМакета.Параметры.Заполнить(Итого);
		Если НЕ ПоказыватьРазницуМячей Тогда
			ОбластьМакета.Параметры.КоличествоЗабито	= СтрШаблон("%1-%2", Итого.КоличествоЗабито, Итого.КоличествоПропущено);
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакета);
		
		Выборка.Сбросить();
		ОбластьМакета = Неопределено;
		Пока Выборка.Следующий() Цикл
			Если ОбластьМакета = Неопределено Тогда
				ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
				ОбластьМакета = Макет.ПолучитьОбласть("Комментарий");
			КонецЕсли;
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТабДокумент;
КонецФункции

Процедура Присоединить(ТабДокумент, Макет, Значение, Регистратор)
	Если Значение >= 0 Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ДеталиП|" + ?(Значение>1, "WW", ?(Значение=0, "LL", "DD")));
		ОбластьМакета.Параметры.Заполнить(Новый Структура("Регистратор", Регистратор));
	Иначе
		ОбластьМакета = Макет.ПолучитьОбласть("ДеталиП|NN");
	КонецЕсли;
	ТабДокумент.Присоединить(ОбластьМакета);
КонецПроцедуры

Функция ЗаголовокПолучить(Лига)
	Если ЗначениеЗаполнено(Чемпионат) Тогда
		Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат,Команда", Лига, Чемпионат, Команда));
	КонецЕсли;
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат,ВыводитьЧемпионат", Команда, Команда, Истина));
КонецФункции

Функция КомментарииПолучить()
	Возврат СерверныйФункции.КомментарииПолучить(Чемпионат, Команда);
КонецФункции
