
&НаСервере
Процедура СформироватьНаСервере()
	Если ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Результат = РеквизитФормыВЗначение("Отчет").ОтчетСформировать(Отчет, ПоказыватьРазницуМячей, Элементы.График.Видимость, Сравнить И Элементы.Сравнить.Видимость);
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УстановитьСерию(Чемпионат)
	Серия = ?(ЗначениеЗаполнено(Чемпионат), График.УстановитьСерию(ФорматЧГ(Чемпионат.ДатаОкончания)), Неопределено);
	Если Отчет.Чемпионат = Чемпионат Тогда
		Цвет = СерверныйФункции.ЦветКоманды(СерверныйФункции.МояКомандаПолучить());
		Если ЗначениеЗаполнено(Цвет) Тогда
			Серия.Цвет			= Цвет;
			Серия.ПриоритетЦвета= Истина;
		КонецЕсли;
	Иначе
		Серия.Расшифровка = Чемпионат;
	КонецЕсли;
	Возврат Серия;
КонецФункции

&НаСервере
Процедура ГрафикОбновить()
	Если Элементы.График.Видимость Тогда
		График.Очистить();
		Если Сравнить И НЕ ТурнирнаяТаблицаКакНаBBC() Тогда
			ОбщегоНазначения.УстановитьЗначение(График.ТипДиаграммы, ТипДиаграммы.ГистограммаГоризонтальная);
			График.СоединениеТочек = ТипСоединенияТочекДиаграммы.Соединять;
			Серия				= УстановитьСерию(Отчет.Чемпионат);
			ЧемпионатПрошлый	= РеквизитФормыВЗначение("Отчет").ЧемпионатПрошлый();
			СерияП				= УстановитьСерию(ЧемпионатПрошлый);
			Для Каждого Детали Из Отчет.Данные Цикл
				Точка = График.УстановитьТочку(Детали.Команда);
				Точка.Расшифровка = Детали.Команда;
				График.УстановитьЗначение(Точка, Серия, ?(ПоказыватьРазницуМячей, Детали.КоличествоОчков, Детали.КоличествоЗабито));
				Если ЗначениеЗаполнено(ЧемпионатПрошлый) Тогда
					График.УстановитьЗначение(Точка, СерияП, ?(ПоказыватьРазницуМячей, Детали.Количество, Детали.КоличествоГолов));
				КонецЕсли;
			КонецЦикла;
		Иначе
			ОбщегоНазначения.УстановитьЗначение(График.ТипДиаграммы, ТипДиаграммы.ГистограммаСНакоплением);
			СерверныйФункции.ГрафикОбновить(График, Отчет.Данные, НЕ ПоказыватьРазницуМячей);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		СформироватьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура НастройкиДефолт()
	Если НЕ ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Отчет.Чемпионат = ЧемпионатПолучитьПоследнее();
	КонецЕсли;
	ВидимостьУстановить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Чемпионат") Тогда
		Отчет.Чемпионат	= Параметры.Чемпионат;
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
	КонецЕсли;
	Если Параметры.Свойство("График") Тогда
		ГрафикВидимость	= Истина;
	Иначе
		Элементы.График.Видимость	= Ложь;
	КонецЕсли;
	ПоказыватьРазницуМячей	= ПоказыватьРазницуМячей();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ПустаяСтрока(КлючУникальности) Тогда
		ПоложениеКоманднойПанели	= ПоложениеКоманднойПанелиФормы.Нет;
		Элементы.Сравнить.Видимость	= Ложь;
	КонецЕсли;
	НастройкиДефолт();
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты") И Параметр = Отчет.Чемпионат Тогда
		ПодключитьОбработчикОжидания("КомандаСформировать", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатОчистка(Элемент, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		СтандартнаяОбработка	= Ложь;
		Отчет.Чемпионат = ЧемпионатПолучитьПоследнее();
		СформироватьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЧемпионатПриИзмененииНаСервере()
	ВидимостьУстановить();
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент)
	ЧемпионатПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВидимостьУстановить()
	Если ТурнирнаяТаблицаКакНаBBC() Тогда
		Элементы.ПоказыватьРазницуМячей.Видимость	= Ложь;
		Элементы.ТипМатчей.Видимость 				= ГрафикВидимость;
		Элементы.График.Видимость					= ГрафикВидимость;
		Элементы.Сравнить.Видимость					= Ложь;
		
		ПоказыватьРазницуМячей	= Ложь;
		Сравнить				= Ложь;
		
	ИначеЕсли ОлимпийскаяСистема() Тогда
		Элементы.ПоказыватьРазницуМячей.Видимость	= Ложь;
		Элементы.ТипМатчей.Видимость 				= Ложь;
		Элементы.График.Видимость					= Ложь;
		Элементы.Сравнить.Видимость					= Ложь;
		
		Сравнить				= Ложь;
		
	Иначе
		Элементы.ПоказыватьРазницуМячей.Видимость	= Истина;
		Элементы.ТипМатчей.Видимость 				= ГрафикВидимость;
		Элементы.График.Видимость					= ГрафикВидимость;
		//Элементы.Сравнить.Видимость					= Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОлимпийскаяСистема()
	Возврат Отчет.Чемпионат.Владелец.ОлимпийскаяСистема;
КонецФункции

&НаКлиенте
Процедура ПослеВыбораМатча(Элемент, ПараметрКоманды=Неопределено) Экспорт
	НаКлиенте.Показать(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	Если ТипЗнч(ЗначениеДиаграммы) = Тип("ЗначениеДиаграммы") Тогда
		СтандартнаяОбработка	= Ложь;
		Матчи	= МатчиПолучить(Новый Структура("Команда,Результат", ЗначениеДиаграммы.Точка.Значение, ЗначениеДиаграммы.Серия.Значение));
		Если Матчи.Количество() = 1 Тогда
			ПослеВыбораМатча(Матчи.Получить(0));
		ИначеЕсли Матчи.Количество() > 0 Тогда
			ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПослеВыбораМатча", ЭтотОбъект), Матчи);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция МатчиПолучить(Отбор)
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |	ТурнирнаяТаблица.Регистратор КАК Значение,
	      	               |	Матч.Счет КАК Представление,
	      	               |	ТурнирнаяТаблица.Период КАК Период
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
	      	               |		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.Команда = &Команда
	      	               |	И ТурнирнаяТаблица.КоличествоОчков = &КоличествоОчков
	      	               |	И ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Период УБЫВ");
	Запрос.УстановитьПараметр("Чемпионат",			Отчет.Чемпионат);
	Запрос.УстановитьПараметр("Команда",			Отбор.Команда);
	Запрос.УстановитьПараметр("КоличествоОчков",	?(СтрСравнить(Отбор.Результат, "Проигрыш")=0, 0, 1));
	Если СтрСравнить(Отбор.Результат, "Выигрыш") = 0 Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	ТурнирнаяТаблица.Регистратор КАК Значение,
		               |	Матч.Счет КАК Представление,
		               |	ТурнирнаяТаблица.Период КАК Период
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
		               |		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
		               |ГДЕ
		               |	ТурнирнаяТаблица.Команда = &Команда
		               |	И ТурнирнаяТаблица.КоличествоОчков >= 2
		               |	И ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период УБЫВ";
	КонецЕсли;
	Возврат СерверныйФункции.Выборка2СписокЗначений(Запрос.Выполнить().Выбрать());
КонецФункции

&НаСервереБезКонтекста
Функция ПоказыватьРазницуМячей()
	Возврат СерверныйФункции.ПоказыватьРазницуМячей();
КонецФункции

&НаСервере
Функция ТурнирнаяТаблицаКакНаBBC()
	Возврат РеквизитФормыВЗначение("Отчет").ТурнирнаяТаблицаКакНаBBC();
КонецФункции

&НаСервереБезКонтекста
Функция ЧемпионатПолучитьПоследнее()
	Возврат СерверныйФункции.ЧемпионатПолучитьПоследнее();
КонецФункции

&НаКлиенте
Процедура КомандаСформировать() Экспорт
	Сформировать();
КонецПроцедуры
