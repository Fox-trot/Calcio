
Функция ОтчетСформировать(Объект, ПоказыватьРазницуМячей, ОставшиесяМатчи=Ложь) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Лига	= Чемпионат.Владелец;
	Выборка = ТурнирнаяТаблица();
	
	ТабДокумент.Вывести(ЗаголовокПолучить(Лига));
	
	Макет  = ПолучитьОбщийМакет("ТурнирнаяТаблица");
	//ТабДокумент.ФиксацияСлева	= 10;
	
	Объект.Данные.Очистить();
	Итого		= Новый Структура("НомерСтроки", 0);
	Если ТурнирнаяТаблицаКакНаBBC() Тогда
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка1"));
		//ТабДокумент.ФиксацияСверху	= 2 + Выборка.Количество();
		Пока Выборка.Следующий() Цикл
			Итого.НомерСтроки	= Итого.НомерСтроки + 1;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Детали1|Начало");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.НомерСтроки;
			ТабДокумент.Вывести(ОбластьМакета);
			
			Пятерка		= 1;
			Матчи = МатчиПоследние5(Выборка.Команда);
			Для Каждого Детали Из Матчи Цикл
				ОбластьМакета = Макет.ПолучитьОбласть("Детали1|" + Детали.Результат);
				ОбластьМакета.Параметры.Заполнить(Детали);
				ТабДокумент.Присоединить(ОбластьМакета);
				Пятерка		= Пятерка + 1;
			КонецЦикла;
			Для Шаг = Пятерка По 5 Цикл
				ОбластьМакета = Макет.ПолучитьОбласть("Детали1|NN");
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(Объект.Данные.Добавить(), Выборка);
		КонецЦикла;
	Иначе
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка"));
		Если Лига.ОлимпийскаяСистема Тогда
			НаВылет			= Выборка.Количество();
			Пятерка			= -1;
		Иначе
			НаВылет			= Выборка.Количество() - Лига.КоличествоРотация;
			Пятерка			= 0;
		КонецЕсли;
		Пока Выборка.Следующий() Цикл
			Итого.НомерСтроки	= Итого.НомерСтроки + 1;
			
			Если Итого.НомерСтроки > Лига.Квалификация Тогда
				Если Итого.НомерСтроки > НаВылет Тогда
					ОбластьМакета = ПолучитьОбласть(Макет, 5);
					
				ИначеЕсли Пятерка = Выборка.КоличествоОчков Тогда
					ОбластьМакета = ПолучитьОбласть(Макет, 5);

				Иначе
					ОбластьМакета = ПолучитьОбласть(Макет);
				КонецЕсли;
				
			Иначе
				ОбластьМакета = ПолучитьОбласть(Макет, 5);
				Если Пятерка >= 0 Тогда
					Пятерка = Выборка.КоличествоОчков;
				КонецЕсли;
			КонецЕсли;
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.НомерСтроки;
			Если НЕ ПоказыватьРазницуМячей Тогда
				ОбластьМакета.Параметры.Разница	= СтрШаблон("%1-%2", Выборка.КоличествоЗабито, Выборка.КоличествоПропущено);
			КонецЕсли;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			ЗаполнитьЗначенияСвойств(Объект.Данные.Добавить(), Выборка);
		КонецЦикла;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Комментарий");
		Выборка = КомментарииПолучить();
		Пока Выборка.Следующий() Цикл
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		Если ОставшиесяМатчи Тогда
			Выборка = ОставшиесяМатчи();
			Если Выборка.Количество() > 0 Тогда
				ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
				ОбластьМакета = Макет.ПолучитьОбласть("Матчи");
				Пока Выборка.Следующий() Цикл
					ОбластьМакета.Параметры.Заполнить(Выборка);
					ТабДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ТабДокумент;
КонецФункции

Функция ПолучитьОбласть(Макет, Параметр=0)
	Возврат Макет.ПолучитьОбласть(?(Параметр = 0, "Детали", "Пятерка"));
КонецФункции

Функция ЗаголовокПолучить(Лига)
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат", Лига, Чемпионат));
КонецФункции

Функция КомментарииПолучить()
	Возврат СерверныйФункции.КомментарииПолучить(Чемпионат);
КонецФункции

Функция МатчиПоследние5(Команда)
	Возврат СерверныйФункции.МатчиПоследние5(Команда, Чемпионат);
КонецФункции

Функция ТурнирнаяТаблица()
	Возврат СерверныйФункции.ТурнирнаяТаблица(Чемпионат, ТипМатчей);
КонецФункции

Функция ТурнирнаяТаблицаКакНаBBC()
	Возврат СерверныйФункции.ТурнирнаяТаблицаКакНаBBC();
КонецФункции

Функция ОставшиесяМатчи()
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	Матч.Ссылка.Дата КАК Дата,
	      	               |	Матч.Ссылка.Счет КАК Счет,
	      	               |	Матч.Ссылка.Ссылка КАК Матч
	      	               |ИЗ
	      	               |	Документ.Матч.Команды КАК Матч
	      	               |ГДЕ
	      	               |	Матч.Ссылка.Дата МЕЖДУ &ДатаН И &ДатаК
	      	               |	И Матч.Ссылка.Проведен = ЛОЖЬ
	      	               |	И Матч.Ссылка.ПометкаУдаления = ЛОЖЬ
	      	               |	И Матч.Ссылка.Тур.Владелец = &Владелец
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	Матч.Ссылка.Дата,
	      	               |	Матч.Ссылка.Счет,
	      	               |	Матч.Ссылка.Ссылка
	      	               |
	      	               |ИМЕЮЩИЕ
	      	               |	МАКСИМУМ(Матч.НомерСтроки) = 2
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Дата,
	      	               |	Счет");
	Запрос.УстановитьПараметр("ДатаН",				НачалоДня(ТекущаяУниверсальнаяДата()));
	Запрос.УстановитьПараметр("ДатаК",				КонецДня(Запрос.Параметры.ДатаН + Сутки(2)));
	Запрос.УстановитьПараметр("Владелец",			Чемпионат);
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции
