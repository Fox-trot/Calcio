
Функция ОтчетСформировать(Объект, ПоказыватьРазницуМячей, ОставшиесяМатчи=Ложь, Знач Сравнить=Ложь) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Лига	= Чемпионат.Владелец;
	
	ТабДокумент.Вывести(ЗаголовокПолучить(Лига));
	Макет  = ПолучитьОбщийМакет("ТурнирнаяТаблица");
	
	Если Лига.ОлимпийскаяСистема Тогда
		Туры = ТурнирнаяТаблица(Истина);
		
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
		Делитель	= 4;
		Пусто		= Макет.ПолучитьОбласть("Детали_|Пусто");
		Пока Туры.Следующий() Цикл
			ОбластьМакета = Макет.ПолучитьОбласть("Детали_|Тур");
			ОбластьМакета.Параметры.Заполнить(Туры.Тур);
			ТабДокумент.Вывести(ОбластьМакета);
			
			Выборка		= Туры.Выбрать();
			Итератор	= 1;
			Шагомер		= 0;
			Пока Выборка.Следующий() Цикл
				Шагомер		= Шагомер + 1;
				
				ТабДокумент.Присоединить(Пусто);
				ОбластьМакета = Макет.ПолучитьОбласть("Детали_|Матч");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				Если Выборка.Пенальти + Выборка.Пенальти2 > 0 Тогда
					ОбластьМакета.Параметры.КоличествоГолов = СтрШаблон("%1/%2", Формат(Выборка.КоличествоГолов, "ЧН=0; ЧГ=0"), Формат(Выборка.Пенальти, "ЧН=0; ЧГ=0"));
					ОбластьМакета.Параметры.КоличествоГолов2 = СтрШаблон("%1/%2", Формат(Выборка.КоличествоГолов2, "ЧН=0; ЧГ=0"), Формат(Выборка.Пенальти2, "ЧН=0; ЧГ=0"));
				КонецЕсли;
				ТабДокумент.Присоединить(ОбластьМакета);
				
				Если Выборка.Количество() = Шагомер Тогда
					//
				ИначеЕсли Итератор = Делитель Тогда
					Итератор	= 1;
					ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
					ТабДокумент.Вывести(Макет.ПолучитьОбласть("Детали_|Тур"));
				Иначе
					Итератор	= Итератор + 1;
				КонецЕсли;
			КонецЦикла;
			ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
		КонецЦикла;
		
		Если НЕ ЕстьКвалификация() Тогда Возврат ТабДокумент; КонецЕсли;
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
	КонецЕсли;
		
	Объект.Данные.Очистить();
	Выборка = ТурнирнаяТаблица();
	Итого		= Новый Структура("Место,Делитель", 0, 5);
	Если ТурнирнаяТаблицаКакНаBBC() Тогда
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка1"));
		//ТабДокумент.ФиксацияСверху	= 2 + Выборка.Количество();
		
		Пока Выборка.Следующий() Цикл
			Итого.Место	= Итого.Место + 1;
			
			ОбластьМакета = Макет.ПолучитьОбласть("Детали1|Начало");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.Место;
			ТабДокумент.Вывести(ОбластьМакета);
			
			Пятерка		= 1;
			Матчи = МатчиПоследние5(Выборка.Команда);
			Для Каждого Детали Из Матчи Цикл
				ОбластьМакета = Макет.ПолучитьОбласть("Детали1|" + Детали.Результат);
				ОбластьМакета.Параметры.Заполнить(Детали);
				ТабДокумент.Присоединить(ОбластьМакета);
				Пятерка		= Пятерка + 1;
			КонецЦикла;
			Для Шаг = Пятерка По Итого.Делитель Цикл
				ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Детали1|NN"));
			КонецЦикла;
			
			ЗаполнитьЗначенияСвойств(Объект.Данные.Добавить(), Выборка);
		КонецЦикла;
	Иначе
		
		ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка|Основная"));
		Если Сравнить Тогда
			ПрошлыйСезон	= ПрошлыйСезон();
			Сравнить		= (ПрошлыйСезон.Количество() > 0);
			Если Сравнить Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Шапка2|Год");
				ОбластьМакета.Параметры.Заполнить(ПрошлыйСезон.Получить(0));
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
		КонецЕсли;
		НаВылет			= Выборка.Количество() - Лига.КоличествоРотация;
		Пятерка			= 0;
		Пока Выборка.Следующий() Цикл
			Итого.Место	= Итого.Место + 1;
			
			Выделен	= 0;
			Если Итого.Место > Лига.Квалификация Тогда
				Если Итого.Место > НаВылет Тогда
					Выделен	= 5;
				ИначеЕсли Пятерка = Выборка.КоличествоОчков Тогда
					Выделен	= 5;
				КонецЕсли;
				
			Иначе
				Выделен	= 5;
				Если Пятерка >= 0 Тогда
					Пятерка = Выборка.КоличествоОчков;
				КонецЕсли;
			КонецЕсли;
			ОбластьМакета = ПолучитьОбласть(Макет, Выделен);
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ОбластьМакета.Параметры.Место	= Итого.Место;
			Если НЕ ПоказыватьРазницуМячей Тогда
				ОбластьМакета.Параметры.Разница	= Разница(Выборка);
			КонецЕсли;
			
			ТабДокумент.Вывести(ОбластьМакета);
			
			НовСтрока = Объект.Данные.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
			
			Если Сравнить Тогда
				ОбластьМакета = Макет.ПолучитьОбласть(?(Выделен = 0, "Детали", "Пятерка") + "|Год");
				Прошлое	= ПрошлыйСезон.НайтиСтроки(Новый Структура("Команда", Выборка.Команда));
				Если Прошлое.Количество() = 0 Тогда
					//
				Иначе
					ТекСтрока = Прошлое.Получить(0);
					ОбластьМакета.Параметры.Заполнить(ТекСтрока);
					Если НЕ ПоказыватьРазницуМячей Тогда
						ОбластьМакета.Параметры.Разница = Разница(ТекСтрока);
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(НовСтрока, Новый Структура("Количество,КоличествоГолов", ТекСтрока.КоличествоОчков, ТекСтрока.КоличествоЗабито));
				КонецЕсли;
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
		
		Выборка = КомментарииПолучить();
		ОбластьМакета = Неопределено;
		Пока Выборка.Следующий() Цикл
			Если ОбластьМакета = Неопределено Тогда
				ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
				ОбластьМакета = Макет.ПолучитьОбласть("Комментарий");
			КонецЕсли;
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		Если ОставшиесяМатчи И Чемпионат.ДатаОкончания > НачалоМесяца(ТекущаяДатаСеанса()) Тогда
			Выборка = ОставшиесяМатчи();
			Если Выборка.Количество() > 0 Тогда
				Утро	= НачалоДня(ТекущаяДатаСеанса());
				Сегодня	= КонецДня(ТекущаяДатаСеанса());
				ТабДокумент.Вывести(Макет.ПолучитьОбласть("Пробел"));
				Пока Выборка.Следующий() Цикл
					ОбластьМакета = Макет.ПолучитьОбласть(?(Выборка.Дата > Сегодня Или Выборка.Дата = Утро , "Матчи", "Сегодня") + ?(Выборка.МояКоманда, "1", ""));
					ОбластьМакета.Параметры.Заполнить(Выборка);
					ТабДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ Лига.ОлимпийскаяСистема Тогда
		ТабДокумент.ФиксацияСверху	= 3;
	КонецЕсли;
	//ТабДокумент.ФиксацияСлева	= ?(ТурнирнаяТаблицаКакНаBBC(), 13, 11);
	
	Возврат ТабДокумент;
КонецФункции

Функция Разница(Данные)
	Возврат СтрШаблон("%1-%2", Данные.КоличествоЗабито, Данные.КоличествоПропущено);
КонецФункции

Функция ПолучитьОбласть(Макет, Параметр=0)
	Возврат Макет.ПолучитьОбласть(?(Параметр = 0, "Детали|Основная", "Пятерка|Основная"));
КонецФункции

Функция ЗаголовокПолучить(Лига)
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат", Лига, Чемпионат));
КонецФункции

Функция КомментарииПолучить()
	Возврат СерверныйФункции.КомментарииПолучить(Чемпионат);
КонецФункции

Функция МатчиПоследние5(Команда)
	Возврат СерверныйФункции.МатчиПоследние5(Команда, Чемпионат);
КонецФункции

Функция ТурнирнаяТаблица(Олимпийская=Ложь)
	Возврат ?(Олимпийская, СерверныйФункции.ТурнирнаяТаблица2(Чемпионат), СерверныйФункции.ТурнирнаяТаблица(Чемпионат, ТипМатчей));
КонецФункции

Функция ТурнирнаяТаблицаКакНаBBC() Экспорт
	Возврат (НЕ Чемпионат.Владелец.ОлимпийскаяСистема И СерверныйФункции.ТурнирнаяТаблицаКакНаBBC());
КонецФункции

Функция ОставшиесяМатчи()
	Запрос	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 5
	      	               |	ВЫБОР
	      	               |		КОГДА Матч.Ссылка.Дата >= &ДатаК
	      	               |			ТОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ)
	      	               |		КОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ) = Матч.Ссылка.Дата
	      	               |			ТОГДА Матч.Ссылка.Дата
	      	               |		ИНАЧЕ ДОБАВИТЬКДАТЕ(Матч.Ссылка.Дата, СЕКУНДА, &Сдвиг)
	      	               |	КОНЕЦ КАК Дата,
	      	               |	ВЫБОР
	      	               |		КОГДА Матч.Ссылка.Дата >= &ДатаК
	      	               |			ТОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ)
	      	               |		ИНАЧЕ Матч.Ссылка.Дата
	      	               |	КОНЕЦ КАК Период,
	      	               |	Матч.Ссылка.Счет КАК Счет,
	      	               |	Матч.Ссылка КАК Матч,
	      	               |	МАКСИМУМ(Матч.Команда = &Команда) КАК МояКоманда
	      	               |ИЗ
	      	               |	Документ.Матч.Команды КАК Матч
	      	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	      	               |			ЧемпионатыКоманды.Команда КАК Команда
	      	               |		ИЗ
	      	               |			Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	      	               |		ГДЕ
	      	               |			ЧемпионатыКоманды.Ссылка = &Чемпионат) КАК Топ
	      	               |		ПО Матч.Команда = Топ.Команда
	      	               |ГДЕ
	      	               |	Матч.Ссылка.Дата >= &ДатаН
	      	               |	И Матч.Ссылка.Проведен = ЛОЖЬ
	      	               |	И Матч.Ссылка.ПометкаУдаления = ЛОЖЬ
	      	               |	И Матч.Ссылка.Тур.Владелец = &Чемпионат
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	ВЫБОР
	      	               |		КОГДА Матч.Ссылка.Дата >= &ДатаК
	      	               |			ТОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ)
	      	               |		КОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ) = Матч.Ссылка.Дата
	      	               |			ТОГДА Матч.Ссылка.Дата
	      	               |		ИНАЧЕ ДОБАВИТЬКДАТЕ(Матч.Ссылка.Дата, СЕКУНДА, &Сдвиг)
	      	               |	КОНЕЦ,
	      	               |	ВЫБОР
	      	               |		КОГДА Матч.Ссылка.Дата >= &ДатаК
	      	               |			ТОГДА НАЧАЛОПЕРИОДА(Матч.Ссылка.Дата, ДЕНЬ)
	      	               |		ИНАЧЕ Матч.Ссылка.Дата
	      	               |	КОНЕЦ,
	      	               |	Матч.Ссылка.Счет,
	      	               |	Матч.Ссылка,
	      	               |	Топ.Команда
	      	               |
	      	               |ИМЕЮЩИЕ
	      	               |	МАКСИМУМ(Матч.НомерСтроки) = 2
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Период,
	      	               |	МояКоманда УБЫВ,
	      	               |	Топ.Команда ЕСТЬ NULL");
	Запрос.УстановитьПараметр("ДатаН",				НачалоДня(ТекущаяУниверсальнаяДата()));
	Запрос.УстановитьПараметр("ДатаК",				НачалоДня(ТекущаяУниверсальнаяДата() + Сутки()));
	Запрос.УстановитьПараметр("Чемпионат",			Чемпионат);
	Запрос.УстановитьПараметр("Команда",			МояКоманда());
	Запрос.УстановитьПараметр("Сдвиг",				ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата());
	Возврат Запрос.Выполнить().Выбрать();
КонецФункции

Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

Функция ЕстьКвалификация()
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Туры.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |ГДЕ
	                      |	Туры.Владелец = &Чемпионат
	                      |	И Туры.ОлимпийскаяСистема = ЛОЖЬ");
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Возврат (НЕ Запрос.Выполнить().Пустой());
КонецФункции

Функция ПрошлыйСезон()
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	ТурнирнаяТаблица.Команда КАК Команда,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК КоличествоИгр,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) КАК КоличествоЗабито,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоОчков), 0) КАК КоличествоОчков,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА Противник.КоличествоОчков = 0
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Выигрыш,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = Противник.КоличествоОчков
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Ничья,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = 0
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Проигрыш,
	      	               |	ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) КАК КоличествоПропущено,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) - ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) КАК Разница,
	      	               |	МАКСИМУМ(ГОД(Прошлое.ДатаОкончания)) КАК Год
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	      	               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	      	               |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |			ТурнирнаяТаблица.Тур КАК Тур,
	      	               |			ТурнирнаяТаблица.Тур.Владелец.ДатаОкончания КАК ДатаОкончания
	      	               |		ИЗ
	      	               |			РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ГДЕ
	      	               |			ТурнирнаяТаблица.Тур.Владелец В
	      	               |					(ВЫБРАТЬ ПЕРВЫЕ 1
	      	               |						Чемпионаты2.Ссылка
	      	               |					ИЗ
	      	               |						Справочник.Чемпионаты КАК Чемпионаты
	      	               |							ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты КАК Чемпионаты2
	      	               |							ПО
	      	               |								Чемпионаты.Владелец = Чемпионаты2.Владелец
	      	               |									И Чемпионаты.ДатаОкончания > Чемпионаты2.ДатаОкончания
	      	               |					ГДЕ
	      	               |						Чемпионаты.Ссылка = &Чемпионат
	      	               |						И Чемпионаты2.ПометкаУдаления = ЛОЖЬ
	      	               |					УПОРЯДОЧИТЬ ПО
	      	               |						Чемпионаты2.ДатаОкончания УБЫВ)
	      	               |			И ТурнирнаяТаблица.Тур.Код В
	      	               |					(ВЫБРАТЬ
	      	               |						ТурнирнаяТаблица.Тур.Код
	      	               |					ИЗ
	      	               |						РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |					ГДЕ
	      	               |						ТурнирнаяТаблица.Тур.Владелец = &Чемпионат)) КАК Прошлое
	      	               |		ПО ТурнирнаяТаблица.Тур = Прошлое.Тур
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.НомерСтроки = &НомерСтроки
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	ТурнирнаяТаблица.Команда
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	КоличествоОчков УБЫВ,
	      	               |	Разница УБЫВ,
	      	               |	КоличествоЗабито УБЫВ,
	      	               |	Команда
	      	               |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Чемпионат",			Чемпионат);
	Запрос.УстановитьПараметр("НомерСтроки",		ТипМатчей);
	Если НЕ (ТипМатчей = 1 Или ТипМатчей = 2) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТурнирнаяТаблица.НомерСтроки = &НомерСтроки", "ИСТИНА");
	КонецЕсли;
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ЧемпионатПрошлый() Экспорт
	Запрос	= Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	      	               |	Чемпионаты2.Ссылка КАК Ссылка
	      	               |ИЗ
	      	               |	Справочник.Чемпионаты КАК Чемпионаты
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты КАК Чемпионаты2
	      	               |		ПО Чемпионаты.Владелец = Чемпионаты2.Владелец
	      	               |			И Чемпионаты.ДатаОкончания > Чемпионаты2.ДатаОкончания
	      	               |ГДЕ
	      	               |	Чемпионаты.Ссылка = &Чемпионат
	      	               |	И Чемпионаты2.ПометкаУдаления = ЛОЖЬ
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Чемпионаты2.ДатаОкончания УБЫВ");
	Запрос.УстановитьПараметр("Чемпионат",			Чемпионат);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда Возврат Выборка.Ссылка; КонецЕсли;
	Возврат Справочники.Чемпионаты.ПустаяСсылка();
КонецФункции
