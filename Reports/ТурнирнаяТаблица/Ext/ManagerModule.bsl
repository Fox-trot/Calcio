
Функция ОтчетСформировать(Знач Чемпионат) Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТурнирнаяТаблица.Команда КАК Команда,
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК КоличествоИгр,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК Забито,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА ТурнирнаяТаблица.КоличествоОчков > Игра.КоличествоОчков
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК Выигрыш,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА ТурнирнаяТаблица.КоличествоОчков = Игра.КоличествоОчков
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК Ничья,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА ТурнирнаяТаблица.КоличествоОчков < Игра.КоличествоОчков
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК Проигрыш,
	                      |	СУММА(Игра.КоличествоГолов) КАК Пропущено
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Игра
	                      |		ПО ТурнирнаяТаблица.Регистратор = Игра.Регистратор
	                      |			И ТурнирнаяТаблица.НомерСтроки <> Игра.НомерСтроки
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Регистратор.Тур.Владелец = &Чемпионат
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) УБЫВ,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) - СУММА(Игра.КоличествоГолов) УБЫВ,
	                      |	Забито УБЫВ");
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Макет  = ПолучитьМакет("Макет");
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.Чемпионат	= Чемпионат;
	ТабДокумент.Вывести(ОбластьМакета);
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка"));
	
	НаВылет		= Выборка.Количество() - Чемпионат.Владелец.КоличествоРотация;
	НомерСтроки	= 0;
	Пятерка		= 0;
	Пока Выборка.Следующий() Цикл
		НомерСтроки	= НомерСтроки + 1;
		
		Если НомерСтроки > 5 Тогда
			Если НомерСтроки > НаВылет Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Пятерка");
				
			ИначеЕсли Пятерка = Выборка.КоличествоОчков Тогда
				ОбластьМакета = Макет.ПолучитьОбласть("Пятерка");
				ТабДокумент.ФиксацияСверху = 3 + НомерСтроки;
				
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Детали");
			КонецЕсли;
			
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("Пятерка");
			ТабДокумент.ФиксацияСверху = 3 + НомерСтроки;
			
			Пятерка = Выборка.КоличествоОчков;
		КонецЕсли;
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.Место	= НомерСтроки;
		ОбластьМакета.Параметры.Мячи	= Строка(Выборка.Забито) + "-" + Строка(Выборка.Пропущено);
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;
	
	Возврат ТабДокумент;
КонецФункции
