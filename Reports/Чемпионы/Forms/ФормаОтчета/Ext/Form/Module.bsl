
&НаСервере
Процедура ГрафикОбновитьНаСервере()
	Если НЕ ЗначениеЗаполнено(Лига()) Тогда Возврат; КонецЕсли;
	
	Отчет.Данные.Очистить();
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 11
	                      |	Туры.Владелец КАК Ссылка,
	                      |	ГОД(Туры.Владелец.ДатаОкончания) КАК Год
	                      |ПОМЕСТИТЬ Чемпионаты
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	                      |		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
	                      |ГДЕ
	                      |	ТИПЗНАЧЕНИЯ(ТурнирнаяТаблица.Регистратор) = ТИП(Документ.Матч)
	                      |	И Туры.Владелец.Владелец = &Лига
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Туры.Владелец,
	                      |	ГОД(Туры.Владелец.ДатаОкончания)
	                      |
	                      |ИМЕЮЩИЕ
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= (КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) - 1) * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) И
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда)
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Год УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Чемпионаты.Ссылка КАК Чемпионат,
	                      |	Чемпионаты.Год КАК Год,
	                      |	ТурнирнаяТаблица.Команда КАК Команда,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов - Противник.КоличествоГолов) КАК Разница,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
	                      |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	                      |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Чемпионаты КАК Чемпионаты
	                      |			ПО Туры.Владелец = Чемпионаты.Ссылка
	                      |		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	                      |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	                      |			И ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда,
	                      |	Чемпионаты.Год,
	                      |	Чемпионаты.Ссылка
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Год,
	                      |	КоличествоОчков УБЫВ,
	                      |	Разница УБЫВ,
	                      |	КоличествоЗабито УБЫВ");
	Запрос.УстановитьПараметр("Лига",			Лига());
	Запрос.УстановитьПараметр("Дата",			ТекущаяДатаСеанса());
	Если Отчет.Лига.ОлимпийскаяСистема Тогда
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
		               |	Чемпионаты.Ссылка КАК Ссылка,
		               |	ГОД(Чемпионаты.Ссылка.ДатаОкончания) КАК Год,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур) КАК Тур
		               |ПОМЕСТИТЬ Чемпионаты
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК Чемпионаты
		               |			ПО Туры.Владелец = Чемпионаты.Ссылка
		               |		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
		               |ГДЕ
		               |	Чемпионаты.Ссылка.Владелец = &Лига
		               |	И Чемпионаты.Ссылка.ДатаОкончания <= &Дата
		               |	И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	Чемпионаты.Ссылка,
		               |	ГОД(Чемпионаты.Ссылка.ДатаОкончания)
		               |
		               |ИМЕЮЩИЕ
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) И
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда)
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Год УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Чемпионаты.Ссылка КАК Чемпионат,
		               |	Чемпионаты.Год КАК Год,
		               |	ТурнирнаяТаблица.Команда КАК Команда,
		               |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов - Противник.КоличествоГолов) КАК Разница,
		               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
		               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено
		               |ИЗ
		               |	Чемпионаты КАК Чемпионаты
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
		               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |			ПО Туры.Ссылка = ТурнирнаяТаблица.Тур
		               |		ПО Чемпионаты.Ссылка = Туры.Владелец
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
		               |		ПО (ТурнирнаяТаблица.Регистратор = Противник.Регистратор)
		               |			И (ТурнирнаяТаблица.НомерСтроки <> Противник.НомерСтроки)
		               |ГДЕ
		               |	ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ТурнирнаяТаблица.Команда,
		               |	Чемпионаты.Год,
		               |	Чемпионаты.Ссылка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Год,
		               |	КоличествоОчков УБЫВ,
		               |	Разница УБЫВ,
		               |	КоличествоЗабито УБЫВ";
	КонецЕсли;
	Выборка		= Запрос.Выполнить().Выбрать();
	Чемпионаты	= Новый Массив;
	
	Если ПростойОтчет Тогда
		Пока Выборка.Следующий() Цикл
			Если Чемпионаты.Найти(Выборка.Чемпионат) = Неопределено Тогда
				Чемпионаты.Добавить(Выборка.Чемпионат);
				
				ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
			КонецЕсли;
		КонецЦикла;

	Иначе
		Кондуит		= Новый Массив;
		Пока Выборка.Следующий() Цикл
			Если Чемпионаты.Найти(Выборка.Чемпионат) = Неопределено Тогда
				Чемпионаты.Добавить(Выборка.Чемпионат);
				Если Кондуит.Найти(Выборка.Команда) = Неопределено Тогда
					Кондуит.Добавить(Выборка.Команда);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Запрос.Текст	= "ВЫБРАТЬ
		            	  |	ГОД(Туры.Владелец.ДатаОкончания) КАК Год,
		            	  |	ТурнирнаяТаблица.Команда КАК Команда,
		            	  |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
		            	  |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
		            	  |		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Команда В(&Команды)
		            	  |	И Туры.Владелец В(&Чемпионаты)
		            	  |
		            	  |СГРУППИРОВАТЬ ПО
		            	  |	ТурнирнаяТаблица.Команда,
		            	  |	ГОД(Туры.Владелец.ДатаОкончания)
		            	  |
		            	  |УПОРЯДОЧИТЬ ПО
		            	  |	Год,
		            	  |	Команда
		            	  |АВТОУПОРЯДОЧИВАНИЕ";
		Запрос.УстановитьПараметр("Чемпионаты",		Чемпионаты);
		Запрос.УстановитьПараметр("Команды",		Кондуит);
		Выборка		= Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция Лига()
	Если НЕ ЗначениеЗаполнено(Отчет.Лига) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		                      |	Чемпионаты.Владелец КАК Лига
		                      |ИЗ
		                      |	Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты КАК Чемпионаты
		                      |		ПО ЧемпионатыКоманды.Ссылка = Чемпионаты.Ссылка
		                      |ГДЕ
		                      |	ЧемпионатыКоманды.Команда = &Команда
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	Чемпионаты.Владелец.ОлимпийскаяСистема,
		                      |	Чемпионаты.ДатаОкончания УБЫВ");
		Запрос.УстановитьПараметр("Команда",			СерверныйФункции.МояКомандаПолучить());
		Выборка	= Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Отчет.Лига	= Выборка.Лига;
		КонецЕсли;
	КонецЕсли;
	Возврат Отчет.Лига;
КонецФункции

&НаСервере
Процедура СформироватьНаСервере()
	Если ЗначениеЗаполнено(Отчет.Лига) Тогда
		Результат = РеквизитФормыВЗначение("Отчет").ОтчетСформировать();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьДоступностьУстановить()
	Элементы.ТипГрафика.Доступность					= (НЕ ПростойОтчет);
	Элементы.ОтображатьТаблицуДанных.Доступность	= (НЕ ПростойОтчет);
	Элементы.График.Видимость						= (НЕ ПростойОтчет);
	Элементы.Результат.Видимость					= ПростойОтчет;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЦветКоманды(Команда)
	Возврат СерверныйФункции.ЦветКоманды(Команда);
КонецФункции

&НаКлиенте
Процедура ГрафикОбновить(ТолькоГрафик=Ложь)
	Если НЕ ТолькоГрафик Тогда
		ГрафикОбновитьНаСервере();
	КонецЕсли;
	Если ПростойОтчет Тогда
		СформироватьНаСервере();
	Иначе
		График.Очистить();
		Год			= 0;
		мКоманды	= Новый Соответствие;
		Для Каждого Детали Из Отчет.Данные Цикл
			Серия		= мКоманды.Получить(Детали.Команда);
			Если Серия = Неопределено Тогда
				Серия		= График.УстановитьСерию(Детали.Команда);
				Серия.Расшифровка	= Детали.Команда;
				Цвет				= ЦветКоманды(Детали.Команда);
				Если ЗначениеЗаполнено(Цвет) Тогда
					Серия.Цвет			= Цвет;
				КонецЕсли;
				
				мКоманды.Вставить(Детали.Команда, Серия);
			КонецЕсли;
			Если Год <> Детали.Год Тогда
				Год			= Детали.Год;
				Точка		= График.УстановитьТочку(ФорматЧГ(Детали.Год));
			КонецЕсли;
			График.УстановитьЗначение(Точка, Серия, ?(ТипГрафика = 1, Детали.КоличествоЗабито, Детали.КоличествоОчков));
		КонецЦикла;
	КонецЕсли;
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент)
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ГрафикОбновить();
	ОтображатьТаблицуДанныхПриИзменении();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Лига") Тогда
		Отчет.Лига		= Параметры.Лига;
		ПростойОтчет	= Истина;
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьТаблицуДанныхПриИзменении(Элемент=Неопределено)
	График.ОбластьПостроения.ОтображатьТаблицуДанных	= ОтображатьТаблицуДанных;
КонецПроцедуры

&НаКлиенте
Процедура ПростойОтчетПриИзменении(Элемент)
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ТипГрафикаПриИзменении(Элемент)
	ГрафикОбновить(Истина);
КонецПроцедуры
