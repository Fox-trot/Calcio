
&НаСервере
Процедура ГрафикОбновить()
	График.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Команда",		Отчет.Команда);
	Запрос.УстановитьПараметр("Чемпионат",		Отчет.Чемпионат);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		Если ТипГрафика = 0 Тогда
			Запрос.Текст = "ВЫБРАТЬ
			               |	ТурнирнаяТаблица.Регистратор.Тур КАК Тур,
			               |	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков
			               |ИЗ
			               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			               |ГДЕ
			               |	ТурнирнаяТаблица.Команда = &Команда
			               |	И ТурнирнаяТаблица.Регистратор.Тур.Владелец = &Чемпионат
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	ТурнирнаяТаблица.Регистратор.Тур.Код";
			Детали	= Запрос.Выполнить().Выбрать();
			Итого	= 0;
			Серия	= График.УстановитьСерию("Количество очков");
			Пока Детали.Следующий() Цикл
				Итого = Итого + Детали.КоличествоОчков;
				График.УстановитьЗначение(График.УстановитьТочку(Детали.Тур), Серия, Итого);
			КонецЦикла;
		Иначе
			Запрос.Текст = "ВЫБРАТЬ
			               |	ТурнирнаяТаблица.Регистратор.Тур КАК Тур,
			               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоЗабито,
			               |	Противник.КоличествоГолов КАК КоличествоПропущено
			               |ИЗ
			               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
			               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
			               |ГДЕ
			               |	ТурнирнаяТаблица.Команда = &Команда
			               |	И ТурнирнаяТаблица.Регистратор.Тур.Владелец = &Чемпионат
			               |	И Противник.Команда <> &Команда
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	ТурнирнаяТаблица.Регистратор.Тур.Код";
			Детали	= Запрос.Выполнить().Выбрать();
			Для Итератор=0 По 1 Цикл
				Итого	= 0;
				Серия	= График.УстановитьСерию(?(Итератор = 0, "Забито", "Пропущено"));
				Детали.Сбросить();
				Пока Детали.Следующий() Цикл
					Итого = Итого + ?(Итератор = 0, Детали.КоличествоЗабито, Детали.КоличествоПропущено);
					График.УстановитьЗначение(График.УстановитьТочку(Детали.Тур), Серия, Итого);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;

	Иначе
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТурнирнаяТаблица.Регистратор.Тур КАК Тур,
		               |	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков,
		               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоГолов,
		               |	ТурнирнаяТаблица.Команда КАК Команда
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Регистратор.Тур.Владелец = &Чемпионат
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Команда,
		               |	ТурнирнаяТаблица.Регистратор.Тур.Код
		               |ИТОГИ ПО
		               |	Команда";
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			Серия	= График.УстановитьСерию(Выборка.Команда);
			
			Итого	= 0;
			Детали	= Выборка.Выбрать();
			Пока Детали.Следующий() Цикл
				Итого = Итого + ?(ТипГрафика = 0, Детали.КоличествоОчков, Детали.КоличествоГолов);
				График.УстановитьЗначение(График.УстановитьТочку(Детали.Тур), Серия, Итого);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент)
	ГрафикОбновить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Чемпионат") Тогда
		ЭтаФорма.АвтоматическоеСохранениеДанныхВНастройках = АвтоматическоеСохранениеДанныхФормыВНастройках.НеИспользовать;
		Отчет.Чемпионат	= Параметры.Чемпионат;
	
	ИначеЕсли Параметры.Свойство("Команда") Тогда
		ЭтаФорма.АвтоматическоеСохранениеДанныхВНастройках = АвтоматическоеСохранениеДанныхФормыВНастройках.НеИспользовать;
		Отчет.Команда	= Параметры.Команда;
		Если ПустаяСтрока(Отчет.Чемпионат) Тогда
			Отчет.Чемпионат = СерверныйФункции.ЧемпионатПолучитьПоследнее(Отчет.Команда);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	Если ПустаяСтрока(Отчет.Команда) Тогда
		Отчет.Команда	= ЗначениеДиаграммы.Значение;
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый Структура("Чемпионат", Отчет.Чемпионат);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		ДанныеВыбора.Вставить("ТекущаяСтрока", Отчет.Команда);
	КонецЕсли;
	ОткрытьФорму("Справочник.Команды.Форма.ФормаВыбора", ДанныеВыбора, Элемент);
КонецПроцедуры
