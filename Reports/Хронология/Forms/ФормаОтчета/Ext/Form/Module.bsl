
&НаСервере
Процедура ГрафикОбновитьНаСервере()
	График.Очистить();
	График.ТипДиаграммы	= ?(ТипГрафика = 2, ТипДиаграммы.Гистограмма, ТипДиаграммы.График);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Команда",		Отчет.Команда);
	Запрос.УстановитьПараметр("Чемпионат",		Отчет.Чемпионат);
	График.ОбластьЛегенды.Расположение	= ?(ЗначениеЗаполнено(Отчет.Команда), РасположениеЛегендыДиаграммы.Нет, РасположениеЛегендыДиаграммы.Авто);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		Если ТипГрафика = 2 Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТурнирнаяТаблица.Период КАК Период,
			|	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков
			|ПОМЕСТИТЬ ТурнирнаяТаблица
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			|		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
			|ГДЕ
			|	ТурнирнаяТаблица.Команда = &Команда
			|	И ТИПЗНАЧЕНИЯ(ТурнирнаяТаблица.Регистратор) = ТИП(Документ.Матч)
			|	И Туры.Владелец = &Чемпионат
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТурнирнаяТаблица.Период КАК Период,
			|	ЕСТЬNULL(ТурнирнаяТаблица1.Период, ТурнирнаяТаблица.Период) КАК Период1
			|ПОМЕСТИТЬ Комбинации
			|ИЗ
			|	ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица1
			|		ПО ТурнирнаяТаблица.Период >= ТурнирнаяТаблица1.Период
			|			И (ТурнирнаяТаблица1.КоличествоОчков > 0)
			|ГДЕ
			|	ТурнирнаяТаблица.КоличествоОчков > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Комбинации.Период1 КАК Период1,
			|	МАКСИМУМ(Комбинации.Период) КАК Период
			|ПОМЕСТИТЬ Окна
			|ИЗ
			|	Комбинации КАК Комбинации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО (ТурнирнаяТаблица.КоличествоОчков = 0)
			|			И (ТурнирнаяТаблица.Период МЕЖДУ Комбинации.Период1 И Комбинации.Период)
			|ГДЕ
			|	ВЫБОР
			|			КОГДА Комбинации.Период = Комбинации.Период1
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ТурнирнаяТаблица.Период ЕСТЬ NULL
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	Комбинации.Период1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Окна.Период КАК Период,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Окна.Период1) КАК Количество
			|ПОМЕСТИТЬ Макси
			|ИЗ
			|	Окна КАК Окна
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО (ТурнирнаяТаблица.Период МЕЖДУ Окна.Период1 И Окна.Период)
			|
			|СГРУППИРОВАТЬ ПО
			|	Окна.Период
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Окна.Период1) > 3
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Окна.Период КАК Период,
			|	Макси.Количество КАК Количество
			|ИЗ
			|	Окна КАК Окна
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Макси КАК Макси
			|		ПО Окна.Период = Макси.Период
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО (ТурнирнаяТаблица.Период МЕЖДУ Окна.Период И Окна.Период1)
			|ГДЕ
			|	ВЫБОР
			|			КОГДА Окна.Период = Окна.Период1
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ТурнирнаяТаблица.Период ЕСТЬ NULL
			|		КОНЕЦ
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период";
			Детали	= Запрос.Выполнить().Выбрать();
			Серия	= График.УстановитьСерию(Отчет.Команда);
			Пока Детали.Следующий() Цикл
				Точка				= График.УстановитьТочку(Детали.Период);
				Точка.Текст			= Формат(Детали.Период, "ДФ=dd.MM");
				График.УстановитьЗначение(Точка, Серия, Детали.Количество);
			КонецЦикла;
			
		ИначеЕсли ТипГрафика = 1 Тогда
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Матч.Дата КАК Период,
			|	ТурнирнаяТаблица.Регистратор КАК Матч,
			|	Матч.Представление КАК Подсказка,
			|	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоЗабито,
			|	Противник.КоличествоГолов КАК КоличествоПропущено
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
			|		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			|		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
			|		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
			|ГДЕ
			|	ТурнирнаяТаблица.Команда = &Команда
			|	И Туры.Владелец = &Чемпионат
			|	И Противник.Команда <> &Команда
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период";
			Детали	= Запрос.Выполнить().Выбрать();
			Для Итератор = 0 По 2 Цикл
				Итого	= 0;
				Забито	= 0;
				Серия	= График.УстановитьСерию(?(Итератор=0, "Забито", ?(Итератор=1, "Пропущено", "Средняя результативность")));
				Детали.Сбросить();
				Пока Детали.Следующий() Цикл
					Точка				= График.УстановитьТочку(Детали.Период);
					Точка.Текст			= Формат(Детали.Период, "ДФ=dd.MM");
					Если Итератор = 2 Тогда
						Забито	= Забито + Детали.КоличествоЗабито;
						Итого	= Итого + 1;
						График.УстановитьЗначение(Точка, Серия, Окр(Забито / Итого, 2));
					Иначе
						Итого = Итого + ?(Итератор = 0, Детали.КоличествоЗабито, Детали.КоличествоПропущено);
						Если ?(Итератор = 0, Детали.КоличествоЗабито = 0, Детали.КоличествоПропущено = 0) Тогда
							График.УстановитьЗначение(Точка, Серия, Итого, Детали.Матч);
						Иначе
							График.УстановитьЗначение(Точка, Серия, Итого, Детали.Матч, Детали.Подсказка);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	Матч.Дата КАК Период,
			|	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков,
			|	ТурнирнаяТаблица.Регистратор КАК Матч,
			|	Матч.Представление КАК Подсказка
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			|		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
			|		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
			|ГДЕ
			|	ТурнирнаяТаблица.Команда = &Команда
			|	И Туры.Владелец = &Чемпионат
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период";
			Детали	= Запрос.Выполнить().Выбрать();
			Для Итератор = 0 По 1 Цикл
				Итого	= 0;
				Серия	= График.УстановитьСерию(?(Итератор = 0, "Набрано", "Упущено"));
				Детали.Сбросить();
				Пока Детали.Следующий() Цикл
					Точка				= График.УстановитьТочку(Детали.Период);
					Точка.Текст			= Формат(Детали.Период, "ДФ=dd.MM");
					Итого = Итого + ?(Итератор = 0, Детали.КоличествоОчков, 3 - Детали.КоличествоОчков);
					Если ?(Итератор = 0, Детали.КоличествоОчков = 0, Детали.КоличествоОчков = 3) Тогда
						График.УстановитьЗначение(Точка, Серия, Итого, Детали.Матч);
					Иначе
						График.УстановитьЗначение(Точка, Серия, Итого, Детали.Матч, Детали.Подсказка);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;

	ИначеЕсли ТипГрафика = 2 Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТурнирнаяТаблица.Период КАК Период,
			|	ТурнирнаяТаблица.Команда КАК Команда,
			|	ТурнирнаяТаблица.КоличествоОчков КАК КоличествоОчков
			|ПОМЕСТИТЬ ТурнирнаяТаблица
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			|		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
			|ГДЕ
			|	Туры.Владелец = &Чемпионат
			|	И ТИПЗНАЧЕНИЯ(ТурнирнаяТаблица.Регистратор) = ТИП(Документ.Матч)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТурнирнаяТаблица.Период КАК Период,
			|	ЕСТЬNULL(ТурнирнаяТаблица1.Период, ТурнирнаяТаблица.Период) КАК Период1,
			|	ТурнирнаяТаблица.Команда КАК Команда
			|ПОМЕСТИТЬ Комбинации
			|ИЗ
			|	ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица1
			|		ПО ТурнирнаяТаблица.Команда = ТурнирнаяТаблица1.Команда
			|			И ТурнирнаяТаблица.Период >= ТурнирнаяТаблица1.Период
			|			И (ТурнирнаяТаблица1.КоличествоОчков > 0)
			|ГДЕ
			|	ТурнирнаяТаблица.КоличествоОчков > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Комбинации.Команда КАК Команда,
			|	Комбинации.Период1 КАК Период1,
			|	МАКСИМУМ(Комбинации.Период) КАК Период
			|ПОМЕСТИТЬ Окна
			|ИЗ
			|	Комбинации КАК Комбинации
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО Комбинации.Команда = ТурнирнаяТаблица.Команда
			|			И (ТурнирнаяТаблица.КоличествоОчков = 0)
			|			И (ТурнирнаяТаблица.Период МЕЖДУ Комбинации.Период1 И Комбинации.Период)
			|ГДЕ
			|	ВЫБОР
			|			КОГДА Комбинации.Период = Комбинации.Период1
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ТурнирнаяТаблица.Период ЕСТЬ NULL
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	Комбинации.Команда,
			|	Комбинации.Период1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Окна.Период КАК Период,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Окна.Период1) КАК Количество,
			|	Окна.Команда КАК Команда
			|ПОМЕСТИТЬ Макси
			|ИЗ
			|	Окна КАК Окна
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО Окна.Команда = ТурнирнаяТаблица.Команда
			|			И (ТурнирнаяТаблица.Период МЕЖДУ Окна.Период1 И Окна.Период)
			|
			|СГРУППИРОВАТЬ ПО
			|	Окна.Период,
			|	Окна.Команда
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Окна.Период1) > 3
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 9
			|	МАКСИМУМ(Макси.Количество) КАК Количество,
			|	Окна.Команда КАК Команда
			|ИЗ
			|	Окна КАК Окна
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Макси КАК Макси
			|		ПО Окна.Период = Макси.Период
			|			И Окна.Команда = Макси.Команда
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК ТурнирнаяТаблица
			|		ПО Окна.Команда = ТурнирнаяТаблица.Команда
			|			И (ТурнирнаяТаблица.Период МЕЖДУ Окна.Период И Окна.Период1)
			|ГДЕ
			|	ВЫБОР
			|			КОГДА Окна.Период = Окна.Период1
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ТурнирнаяТаблица.Период ЕСТЬ NULL
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	Окна.Команда
			|
			|УПОРЯДОЧИТЬ ПО
			|	Количество УБЫВ,
			|	Команда
			|АВТОУПОРЯДОЧИВАНИЕ";
		Детали	= Запрос.Выполнить().Выбрать();
		Пока Детали.Следующий() Цикл
			График.УстановитьЗначение(График.УстановитьТочку(Детали.Количество), График.УстановитьСерию(Детали.Команда), Детали.Количество);
		КонецЦикла;
		
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 5
			|	ТТ.Команда КАК Команда,
			|	СУММА(ТТ.КоличествоГоловОборот) КАК КоличествоГолов,
			|	СУММА(ТТ.КоличествоОчковОборот) КАК КоличествоОчков
			|ПОМЕСТИТЬ Топ5
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица.Обороты КАК ТТ
			|ГДЕ
			|	ТТ.Тур.Владелец = &Чемпионат
			|
			|СГРУППИРОВАТЬ ПО
			|	ТТ.Команда
			|
			|УПОРЯДОЧИТЬ ПО
			|	КоличествоГолов УБЫВ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТТ.Команда КАК Команда,
			|	ТТ.КоличествоОчков КАК КоличествоОчков,
			|	ТТ.КоличествоГолов КАК КоличествоГолов,
			|	Туры.ДатаНачала КАК Период
			|ИЗ
			|	РегистрНакопления.ТурнирнаяТаблица КАК ТТ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Топ5 КАК Топ5
			|		ПО ТТ.Команда = Топ5.Команда
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			|		ПО ТТ.Тур = Туры.Ссылка
			|ГДЕ
			|	ТТ.Тур.Владелец = &Чемпионат
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период
			|ИТОГИ ПО
			|	Команда";
		Если ТипГрафика = 0 Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "Голов УБЫВ", "Очков УБЫВ");
		КонецЕсли;
		Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Туры	= Новый Массив;
		ТурыФор	= Истина;
		Пока Выборка.Следующий() Цикл
			Серия	= График.УстановитьСерию(Выборка.Команда);
			
			Итого	= 0;
			Детали	= Выборка.Выбрать();
			Если ТурыФор Тогда
				Пока Детали.Следующий() Цикл
					Туры.Добавить(Детали.Период);
				КонецЦикла;
				Детали.Сбросить();
			КонецЕсли;
			Пока Детали.Следующий() Цикл
				Точка				= График.УстановитьТочку(Детали.Период);
				Точка.Текст			= Формат(Детали.Период, "ДФ=dd.MM");
				Итого = Итого + ?(ТипГрафика = 0, Детали.КоличествоОчков, Детали.КоличествоГолов);
				График.УстановитьЗначение(Точка, Серия, Итого);
			КонецЦикла;
			Если ТурыФор Тогда
				ТурыФор = Ложь;
			Иначе
				Для Каждого ТекущийТур Из Туры Цикл
					Детали.Сбросить();
					Если НЕ Детали.НайтиСледующий(Новый Структура("Период", ТекущийТур)) Тогда
						Точка				= График.УстановитьТочку(Детали.Период);
						Точка.Текст			= Формат(Детали.Период, "ДФ=dd.MM");
						График.УстановитьЗначение(Точка, Серия, Итого);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент)
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Чемпионат") Тогда
		ЭтаФорма.АвтоматическоеСохранениеДанныхВНастройках = АвтоматическоеСохранениеДанныхФормыВНастройках.НеИспользовать;
		Отчет.Чемпионат	= Параметры.Чемпионат;
	
	ИначеЕсли Параметры.Свойство("Команда") Тогда
		ЭтаФорма.АвтоматическоеСохранениеДанныхВНастройках = АвтоматическоеСохранениеДанныхФормыВНастройках.НеИспользовать;
		Отчет.Команда	= Параметры.Команда;
		Если НЕ ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
			Отчет.Чемпионат = СерверныйФункции.ЧемпионатПолучитьПоследнее(Отчет.Команда);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	Если ПустаяСтрока(Отчет.Команда)
	И ТипЗнч(ЗначениеДиаграммы.Значение) = Тип("СправочникСсылка.Команды")
	Тогда
		Отчет.Команда	= ЗначениеДиаграммы.Значение;
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = Новый Структура("Чемпионат,ТолькоПросмотр", Отчет.Чемпионат, Истина);
	Если ЗначениеЗаполнено(Отчет.Команда) Тогда
		ДанныеВыбора.Вставить("ТекущаяСтрока", Отчет.Команда);
	Иначе
		МояКоманда = МояКоманда(Отчет.Чемпионат);
		Если ЗначениеЗаполнено(МояКоманда) Тогда
			ДанныеВыбора.Вставить("ТекущаяСтрока", МояКоманда);
		КонецЕсли;
	КонецЕсли;
	ОткрытьФорму("Справочник.Команды.ФормаВыбора", ДанныеВыбора, Элемент);
КонецПроцедуры

&НаСервереБезКонтекста
Функция МояКоманда(Чемпионат)
	Возврат СерверныйФункции.КомандаЧемпионата(Чемпионат);
КонецФункции
