
&НаСервере
Процедура ДанныеПолучить()
	Отчет.Данные.Очистить();
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 5
	                      |	Матч.Стадион КАК Стадион
	                      |ПОМЕСТИТЬ Стадионы
	                      |ИЗ
	                      |	Документ.Матч КАК Матч
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
	                      |		ПО Матч.Стадион = Стадионы.Ссылка
	                      |ГДЕ
	                      |	Матч.Проведен = ИСТИНА
	                      |	И Матч.КоличествоЗрителей >= 1
	                      |	И Матч.Тур.Владелец = &Чемпионат
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Матч.Стадион
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	СУММА(Матч.КоличествоЗрителей) УБЫВ
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА ЦЕЛ(Матч.Тур.Код / 10) * 10 = Матч.Тур.Код
	                      |			ТОГДА Матч.Тур.Наименование
	                      |		КОГДА Матч.Тур.Код = 1
	                      |			ТОГДА Матч.Тур.Наименование
	                      |		ИНАЧЕ Матч.Тур.Код
	                      |	КОНЕЦ КАК Подсказка,
	                      |	Матч.Стадион КАК Стадион,
	                      |	Матч.Ссылка КАК Матч,
	                      |	Матч.КоличествоЗрителей КАК КоличествоЗрителей
	                      |ИЗ
	                      |	Документ.Матч КАК Матч
	                      |ГДЕ
	                      |	Матч.Проведен = ИСТИНА
	                      |	И Матч.КоличествоЗрителей >= 1
	                      |	И Матч.Стадион В
	                      |			(ВЫБРАТЬ
	                      |				Стадионы.Стадион
	                      |			ИЗ
	                      |				Стадионы)
	                      |	И Матч.Тур.Владелец = &Чемпионат
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Матч.Тур.Код,
	                      |	Стадион
	                      |АВТОУПОРЯДОЧИВАНИЕ");
	Если ЗначениеЗаполнено(Отчет.Стадион) И ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Если ТипГрафика = 1 Тогда
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
			               |	Матч.Стадион КАК Стадион,
			               |	ТурнирнаяТаблица.Регистратор КАК Матч,
			               |	ТурнирнаяТаблица.Период КАК Период,
			               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоЗабито,
			               |	Противник.КоличествоГолов КАК КоличествоПропущено
			               |ПОМЕСТИТЬ ВТ
			               |ИЗ
			               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
			               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
			               |			ПО Матч.Стадион = Стадионы.Ссылка
			               |		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
			               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
			               |			И ТурнирнаяТаблица.НомерСтроки < Противник.НомерСтроки
			               |ГДЕ
			               |	Матч.Стадион = &Стадион
			               |	И Матч.КоличествоЗрителей >= 1
			               |	И ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
			               |	И ТурнирнаяТаблица.Команда = Стадионы.Владелец
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ВТ.Стадион КАК Стадион,
			               |	ВТ.Матч КАК Матч,
			               |	ВТ.Период КАК Период,
			               |	ВТ.КоличествоЗабито КАК КоличествоЗабито,
			               |	ВТ.КоличествоПропущено КАК КоличествоПропущено
			               |ИЗ
			               |	ВТ КАК ВТ
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период";
		Иначе
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
			               |	Матч.Ссылка КАК Стадион,
			               |	Матч.Ссылка КАК Матч,
			               |	Матч.Дата КАК Период,
			               |	Матч.КоличествоЗрителей КАК КоличествоЗрителей
			               |ПОМЕСТИТЬ Матчи
			               |ИЗ
			               |	Документ.Матч КАК Матч
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
			               |		ПО Матч.Стадион = Стадионы.Ссылка
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			               |		ПО Матч.Тур = Туры.Ссылка
			               |ГДЕ
			               |	Матч.Стадион = &Стадион
			               |	И Матч.Проведен = ИСТИНА
			               |	И Матч.КоличествоЗрителей >= 1
			               |	И Туры.Владелец = &Чемпионат
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период УБЫВ
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	Матчи.Стадион КАК Стадион,
			               |	Матчи.Матч КАК Матч,
			               |	Матчи.Период КАК Период,
			               |	Матчи.КоличествоЗрителей КАК КоличествоЗрителей
			               |ИЗ
			               |	Матчи КАК Матчи
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период";
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Отчет.Стадион) Тогда
		Если ТипГрафика = 1 Тогда
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
			               |	Матч.Стадион КАК Стадион,
			               |	ТурнирнаяТаблица.Регистратор КАК Матч,
			               |	ТурнирнаяТаблица.Период КАК Период,
			               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоЗабито,
			               |	Противник.КоличествоГолов КАК КоличествоПропущено
			               |ПОМЕСТИТЬ ВТ
			               |ИЗ
			               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
			               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
			               |			ПО Матч.Стадион = Стадионы.Ссылка
			               |		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
			               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
			               |			И ТурнирнаяТаблица.НомерСтроки < Противник.НомерСтроки
			               |ГДЕ
			               |	Матч.Стадион = &Стадион
			               |	И Матч.КоличествоЗрителей >= 1
			               |	И ТурнирнаяТаблица.Команда = Стадионы.Владелец
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	ВТ.Стадион КАК Стадион,
			               |	ВТ.Матч КАК Матч,
			               |	ВТ.Период КАК Период,
			               |	ВТ.КоличествоЗабито КАК КоличествоЗабито,
			               |	ВТ.КоличествоПропущено КАК КоличествоПропущено
			               |ИЗ
			               |	ВТ КАК ВТ
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период";
		Иначе
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
			               |	Матч.Ссылка КАК Стадион,
			               |	Матч.Ссылка КАК Матч,
			               |	Матч.Дата КАК Период,
			               |	Матч.КоличествоЗрителей КАК КоличествоЗрителей
			               |ПОМЕСТИТЬ Матчи
			               |ИЗ
			               |	Документ.Матч КАК Матч
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
			               |		ПО Матч.Стадион = Стадионы.Ссылка
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
			               |		ПО Матч.Тур = Туры.Ссылка
			               |ГДЕ
			               |	Матч.Стадион = &Стадион
			               |	И Матч.Проведен = ИСТИНА
			               |	И Матч.КоличествоЗрителей >= 1
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период УБЫВ
			               |;
			               |
			               |////////////////////////////////////////////////////////////////////////////////
			               |ВЫБРАТЬ
			               |	Матчи.Стадион КАК Стадион,
			               |	Матчи.Матч КАК Матч,
			               |	Матчи.Период КАК Период,
			               |	Матчи.КоличествоЗрителей КАК КоличествоЗрителей
			               |ИЗ
			               |	Матчи КАК Матчи
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Период";
		КонецЕсли;
		
	ИначеЕсли ТипГрафика = 1 Тогда
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 11
			               |	Матч.Стадион КАК Стадион,
			               |	МАКСИМУМ(ТурнирнаяТаблица.Регистратор) КАК Матч,
			               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК КоличествоЗабито,
			               |	СУММА(Противник.КоличествоГолов) КАК КоличествоПропущено,
			               |	СУММА(ТурнирнаяТаблица.КоличествоГолов + Противник.КоличествоГолов) КАК Итог
			               |ИЗ
			               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч КАК Матч
			               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Стадионы КАК Стадионы
			               |			ПО Матч.Стадион = Стадионы.Ссылка
			               |		ПО ТурнирнаяТаблица.Регистратор = Матч.Ссылка
			               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
			               |		ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
			               |			И ТурнирнаяТаблица.НомерСтроки < Противник.НомерСтроки
			               |ГДЕ
			               |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
			               |	И Матч.КоличествоЗрителей >= 1
			               |
			               |СГРУППИРОВАТЬ ПО
			               |	Матч.Стадион
			               |
			               |ИМЕЮЩИЕ
			               |	СУММА(ТурнирнаяТаблица.КоличествоГолов + Противник.КоличествоГолов) >= 1
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	Итог УБЫВ,
			               |	Стадион
			               |АВТОУПОРЯДОЧИВАНИЕ";
	КонецЕсли;
	Запрос.УстановитьПараметр("Стадион",		Отчет.Стадион);
	Запрос.УстановитьПараметр("Чемпионат",		Отчет.Чемпионат);
	Выборка	= Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Выборка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СерииЗаполнить()
	СерверныйФункции.СерииЗаполнить(График, Истина);
КонецПроцедуры

&НаСервере
Процедура ГрафикОбновить()
	График.Очистить();
	График.ТипДиаграммы	= ?(ТипГрафика = 1, ТипДиаграммы.ГистограммаСНакоплением, ТипДиаграммы.Гистограмма);
	График.БазовоеЗначение	= 0;
	БазовоеЗначение	= 0;
	
	ДанныеПолучить();
	Если ЗначениеЗаполнено(Отчет.Стадион) Тогда
		Если ТипГрафика = 1 Тогда
			СерииЗаполнить();
			Для Каждого Детали Из Отчет.Данные Цикл
				Точка			= УстановитьТочку(Детали.Период);
				График.УстановитьЗначение(Точка, 0, Детали.КоличествоЗабито, Детали.Матч);
				График.УстановитьЗначение(Точка, 1, Детали.КоличествоПропущено, Детали.Матч);
			КонецЦикла;
		Иначе
			Серия	= УстановитьСерию(Отчет.Стадион);
			
			Для Каждого Детали Из Отчет.Данные Цикл
				График.УстановитьЗначение(УстановитьТочку(Детали.Период), Серия, Детали.КоличествоЗрителей, Детали.Матч);
				БазовоеЗначение	= ?(БазовоеЗначение = 0, -Детали.КоличествоЗрителей, Макс(БазовоеЗначение, -Детали.КоличествоЗрителей));
			КонецЦикла;
			Если График.Точки.Количество() > 1 Тогда
				График.БазовоеЗначение				= Базовое(БазовоеЗначение);
				График.ПропускатьБазовоеЗначение	= (График.БазовоеЗначение > 1);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипГрафика = 1 Тогда
		СерииЗаполнить();
		Для Каждого Детали Из Отчет.Данные Цикл
			Точка	= УстановитьТочку(Детали.Стадион);
			График.УстановитьЗначение(Точка, 0, Детали.КоличествоЗабито, Детали.Матч);
			График.УстановитьЗначение(Точка, 1, Детали.КоличествоПропущено, Детали.Матч);
		КонецЦикла;
	Иначе
		Для Каждого Детали Из Отчет.Данные Цикл
			График.УстановитьЗначение(УстановитьТочку(Детали.Подсказка), УстановитьСерию(Детали.Стадион), Детали.КоличествоЗрителей, Детали.Матч);
		КонецЦикла;
	КонецЕсли;
	График.ОбластьЛегенды.Расположение	= ?(График.Серии.Количество() = 1 И ЗначениеЗаполнено(Отчет.Стадион), РасположениеЛегендыДиаграммы.Нет, РасположениеЛегендыДиаграммы.Авто);
	Если ОтображатьЗаголовок Тогда
		Заголовок	= ?(ТипГрафика=0, "Посещаемость", "Зрелищность");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция Базовое(Значение)
	Возврат ?(Значение < -999, Цел(-Значение / 1000) * 1000, 0);
КонецФункции

&НаСервереБезКонтекста
Функция Цвет(Стадион)
	Возврат СерверныйФункции.ЦветКоманды(Стадион);
КонецФункции

&НаСервере
Функция УстановитьСерию(Стадион)
	Серия	= График.УстановитьСерию(Стадион);
	Цвет	= Цвет(Стадион);
	Если ЗначениеЗаполнено(Цвет) Тогда
		Серия.Цвет			= Цвет;
	КонецЕсли;
	Возврат Серия;
КонецФункции

&НаСервере
Функция УстановитьТочку(Значение, Расшифровка=Неопределено)
	Если ТипЗнч(Значение) = Тип("Дата") Тогда
		Точка			= График.УстановитьТочку(ФорматД(Значение));
	Иначе
		Точка			= График.УстановитьТочку(Значение);
	КонецЕсли;
	Если ЗначениеЗаполнено(Расшифровка) Тогда
		Точка.Расшифровка	= Расшифровка;
	КонецЕсли;
	Возврат Точка;
КонецФункции

&НаКлиенте
Процедура ПриИзменении(Элемент)
	ГрафикОбновить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Чемпионат") Тогда
		Отчет.Чемпионат	= Параметры.Чемпионат;
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
	
	ИначеЕсли Параметры.Свойство("Стадион") Тогда
		Отчет.Стадион	= Параметры.Стадион;
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.Стадион);
		
		Элементы.Чемпионат.АвтоОтметкаНезаполненного = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Отчет.Чемпионат = ЧемпионатПолучитьПоследнее();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЧемпионатПолучитьПоследнее()
	Возврат СерверныйФункции.ЧемпионатПолучитьПоследнее(Отчет.Стадион);
КонецФункции

&НаСервере
Функция СтадионыЧемпионата()
	Возврат СерверныйФункции.СтадионыЧемпионата(Отчет.Чемпионат);
КонецФункции

&НаСервере
Процедура ЧемпионатПриИзмененииНаСервере()
	Если Элементы.Стадион.Видимость Тогда
		Элементы.Стадион.СписокВыбора.ЗагрузитьЗначения(СтадионыЧемпионата());
		Если ЗначениеЗаполнено(Отчет.Стадион) Тогда
			Если Элементы.Стадион.СписокВыбора.НайтиПоЗначению(Отчет.Стадион) = Неопределено Тогда
				Отчет.Стадион	= Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура КартинкиЗагрузить() Экспорт
	ОбщегоНазначения.КартинкиЗагрузить(Элементы.Стадион.СписокВыбора, Кеш);
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент=Неопределено)
	ЧемпионатПриИзмененииНаСервере();
	ПодключитьОбработчикОжидания("КартинкиЗагрузить", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЧемпионатПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Стадион)
	И ТипЗнч(ЗначениеДиаграммы.Значение) = Тип("СправочникСсылка.Стадионы")
	Тогда
		Отчет.Стадион	= ЗначениеДиаграммы.Значение;
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты") И Параметр = Отчет.Чемпионат Тогда
		ГрафикОбновить();
	КонецЕсли;
КонецПроцедуры
