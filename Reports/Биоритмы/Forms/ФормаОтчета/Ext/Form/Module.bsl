
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Перемещения.Период КАК Дата
	                      |ИЗ
	                      |	РегистрСведений.ПеремещенияИгроков КАК Перемещения
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Дата");
	Если Параметры.Свойство("Игрок") Тогда
		ЭтаФорма.АвтоматическоеСохранениеДанныхВНастройках = АвтоматическоеСохранениеДанныхФормыВНастройках.НеИспользовать;
		Отчет.КомандаИлиИгрок	= Параметры.Игрок;
		Отчет.НачальнаяДата		= ТекущаяДата();
		
		Элементы.КомандаИлиИгрок.Видимость = Ложь;

	ИначеЕсли Запрос.Выполнить().Пустой() Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОбновить()
	Если Год(Отчет.НачальнаяДата) < 2 Тогда
		Отчет.НачальнаяДата	= ТекущаяДата();
	КонецЕсли;
	Если Отчет.КоличествоНедель = 0 Тогда
		Отчет.КоличествоНедель = 5;
	КонецЕсли;
	График.Очистить();
	
	Выборка = ГрафикОбновитьНаСервере();
	Если Выборка.Количество() = 0 Тогда Возврат; КонецЕсли;
	
	Для тЭлемент = 1 По 3 Цикл
		Серия	= График.УстановитьСерию(Биоритмы.ФЭИ(тЭлемент));
		Серия.Цвет	= Биоритмы.Цвет(тЭлемент);
		Итера	= -1;
		Шагомер	= 0;
		Пока Шагомер < Отчет.КоличествоНедель * 7 Цикл
			Итера	= Итера + 1;
			Если Отчет.ТолькоВыходныеДни И ДеньНедели(Отчет.НачальнаяДата + Сутки(Итера)) < 6 Тогда
				Продолжить;
			КонецЕсли;

			КолИтого	= 0;
			ВесИтого	= 0;
			Для Каждого Количество Из Выборка Цикл
				КолИтого	= КолИтого + 1;
				ВесИтого	= ВесИтого + Биоритмы.ФазаРасчитать2(Количество + Итера, тЭлемент);
			КонецЦикла;
			Точка				= График.УстановитьТочку(Отчет.НачальнаяДата + Сутки(Итера));
			Точка.Текст			= Формат(Точка.Значение, "ДФ=dd.MM");
			График.УстановитьЗначение(Точка, Серия, Окр(ВесИтого / КолИтого, 1));
			
			Шагомер	= Шагомер + 1;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ГрафикОбновитьНаСервере()
	Если НЕ ЗначениеЗаполнено(Отчет.КомандаИлиИгрок) Тогда Возврат Новый Массив; КонецЕсли;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	РАЗНОСТЬДАТ(Игроки.ДатаРождения, &Дата, ДЕНЬ) КАК Количество
	      	               |ИЗ
	      	               |	Справочник.Игроки КАК Игроки
	      	               |ГДЕ
	      	               |	Игроки.Ссылка = &КомандаИлиИгрок
	      	               |	И ГОД(Игроки.ДатаРождения) >= 1900");
	Если ТипЗнч(Отчет.КомандаИлиИгрок) = Тип("СправочникСсылка.Команды") Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	РАЗНОСТЬДАТ(Перемещения.Игрок.ДатаРождения, &Дата, ДЕНЬ) КАК Количество,
		               |	Перемещения.Игрок КАК Игрок
		               |ИЗ
		               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(
		               |			,
		               |			Игрок.ПометкаУдаления = ЛОЖЬ
		               |				И ГОД(Игрок.ДатаРождения) >= 1900) КАК Перемещения
		               |ГДЕ
		               |	Перемещения.Команда = &КомандаИлиИгрок";
	КонецЕсли;
	Запрос.УстановитьПараметр("КомандаИлиИгрок",	Отчет.КомандаИлиИгрок);
	Запрос.УстановитьПараметр("Дата",				Отчет.НачальнаяДата);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
КонецФункции

&НаСервере
Процедура ЗаголовокОбновитьНаСервере()
	Заголовок = ?(ЗначениеЗаполнено(Отчет.КомандаИлиИгрок), Отчет.КомандаИлиИгрок.ПолноеНаименование(), "<..>");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Если ПустаяСтрока(Отчет.КомандаИлиИгрок) Тогда
	//	Сообщить("Добро пожаловать!");
	////ИначеЕсли ЭтаФорма.Свойство("КомандаИлиИгрок") Тогда
	////	Отчет.КомандаИлиИгрок	= Параметры.КомандаИлиИгрок;
	////	Отчет.НачальнаяДата		= ТекущаяДата();
	//КонецЕсли;
	
	//ЗаголовокОбновитьНаСервере();
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаИлиИгрокПриИзменении(Элемент)
	//ЗаголовокОбновитьНаСервере();
	ГрафикОбновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаИлиИгрокНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДанныеВыбора = Новый Структура("ТолькоПросмотр", Истина);
	Если ТипЗнч(Отчет.КомандаИлиИгрок) = Тип("СправочникСсылка.Игроки") Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Отчет.КомандаИлиИгрок) Тогда
			ДанныеВыбора.Вставить("Отбор", Новый Структура("ТекущаяКоманда", КомандаИгрока(Отчет.КомандаИлиИгрок, Отчет.НачальнаяДата)));
		КонецЕсли;
		ОткрытьФорму("Справочник.Игроки.ФормаВыбора", ДанныеВыбора, Элемент);
	ИначеЕсли ТипЗнч(Отчет.КомандаИлиИгрок) = Тип("СправочникСсылка.Команды") Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Отчет.КомандаИлиИгрок) Тогда
			ДанныеВыбора.Вставить("ТекущаяСтрока", Отчет.КомандаИлиИгрок);
		Иначе
			МояКоманда = МояКоманда();
			Если ЗначениеЗаполнено(МояКоманда) Тогда
				ДанныеВыбора.Вставить("ТекущаяСтрока", МояКоманда);
			КонецЕсли;
		КонецЕсли;
		ОткрытьФорму("Справочник.Команды.ФормаВыбора", ДанныеВыбора, Элемент);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция КомандаИгрока(Игрок, Дата)
	Возврат СерверныйФункции.КомандаИгрока(Игрок, Дата);
КонецФункции

&НаСервереБезКонтекста
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции
