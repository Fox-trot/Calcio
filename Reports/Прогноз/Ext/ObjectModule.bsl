
Функция ОтчетСформировать() Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда Возврат ТабДокумент; КонецЕсли;
	
	ТабДокумент.Вывести(ЗаголовокПолучить());
	Макет  = ПолучитьОбщийМакет("ТурнирнаяТаблица");
	
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка1|Начало"));
	
	Итого	= Новый Структура("Место", 0);
	
	Данные	= ДанныеПолучить();
	
	//Первый	= 0;
	Туры	= Новый Массив;
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|LL");
	Для Каждого Выборка Из Данные Цикл
		Если Туры.Найти(Выборка.Тур) = Неопределено Тогда
			Туры.Добавить(Выборка.Тур);
			//Если Первый = 0 Тогда Первый = Выборка.ТурКод; КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(Выборка);
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
	КонецЦикла;
	ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|NN"));
	
	Команды	= ТурнирнаяТаблица();
	Пока Команды.Следующий() Цикл
		Для Каждого Выборка Из Данные Цикл
			Если Выборка.Команда = Команды.Команда Или Выборка.Противник = Команды.Команда Тогда
				Итого.Место	= Итого.Место + 1;
				
				ОбластьМакета = Макет.ПолучитьОбласть("Детали1|Начало");
				ОбластьМакета.Параметры.Заполнить(Команды);
				ОбластьМакета.Параметры.Место	= Итого.Место;
				ТабДокумент.Вывести(ОбластьМакета);
				
				КоличествоОчков	= Команды.КоличествоОчков;
					
				Для Каждого Тур Из Туры Цикл
					мСтроки	= Данные.НайтиСтроки(Новый Структура("Команда,Тур", Команды.Команда, Тур));
					Если мСтроки.Количество() = 0 Тогда
						мСтроки	= Данные.НайтиСтроки(Новый Структура("Противник,Тур", Команды.Команда, Тур));
						Если мСтроки.Количество() = 0 Тогда
							ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Детали1|NN"));
							Продолжить;
						Иначе
							ОбластьМакета = Макет.ПолучитьОбласть("Детали2|" + Результат(мСтроки.Получить(0).КоличествоОчков, Истина));
							ОбластьМакета.Параметры.Аббревиатура	= мСтроки.Получить(0).Аббревиатура + "(A)";
							
							КоличествоОчков	= КоличествоОчков + ?(мСтроки.Получить(0).КоличествоОчков=1, 1, ?(мСтроки.Получить(0).КоличествоОчков=0, 3, 0));
						КонецЕсли;
					Иначе
						ОбластьМакета = Макет.ПолучитьОбласть("Детали2|" + Результат(мСтроки.Получить(0).КоличествоОчков));
						ОбластьМакета.Параметры.Аббревиатура	= мСтроки.Получить(0).АббревиатураП + "(H)";
						
						КоличествоОчков	= КоличествоОчков + мСтроки.Получить(0).КоличествоОчков;
					КонецЕсли;
					ОбластьМакета.Параметры.Регистратор		= мСтроки.Получить(0).Ссылка;
					ТабДокумент.Присоединить(ОбластьМакета);
				КонецЦикла;
				ОбластьМакета	= Макет.ПолучитьОбласть("Детали|NN");
				ОбластьМакета.Параметры.КоличествоОчков	= КоличествоОчков;
				ТабДокумент.Присоединить(ОбластьМакета);
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТабДокумент.ФиксацияСлева	= 11;
	
	Возврат ТабДокумент;
КонецФункции

Функция Результат(КоличествоОчков, Реверс=Ложь)
	Если Реверс Тогда Возврат ?(КоличествоОчков=0, "WW", ?(КоличествоОчков=1, "DD", "LL")); КонецЕсли;
	Возврат ?(КоличествоОчков=0, "LL", ?(КоличествоОчков=1, "DD", "WW"));
КонецФункции

Функция ДанныеПолучить()
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	МатчКоманды.Ссылка КАК Ссылка,
	      	               |	НАЧАЛОПЕРИОДА(МатчКоманды.Ссылка.Дата, ДЕНЬ) КАК Дата,
	      	               |	МатчКоманды.Ссылка.Тур КАК Тур,
	      	               |	МатчКоманды.Ссылка.Тур.Код КАК ТурКод,
	      	               |	МатчКоманды.Команда КАК Команда,
	      	               |	МатчКоманды.НомерСтроки КАК НомерСтроки,
	      	               |	ТурнирнаяТаблица.КоличествоИгр КАК КоличествоИгр
	      	               |ПОМЕСТИТЬ Клубы
	      	               |ИЗ
	      	               |	Документ.Матч.Команды КАК МатчКоманды
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	      	               |			ТурнирнаяТаблица.Команда КАК Команда,
	      	               |			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК КоличествоИгр
	      	               |		ИЗ
	      	               |			РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ГДЕ
	      	               |			ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |		
	      	               |		СГРУППИРОВАТЬ ПО
	      	               |			ТурнирнаяТаблица.Команда) КАК ТурнирнаяТаблица
	      	               |		ПО МатчКоманды.Команда = ТурнирнаяТаблица.Команда
	      	               |ГДЕ
	      	               |	МатчКоманды.Ссылка В
	      	               |			(ВЫБРАТЬ
	      	               |				МатчКоманды.Ссылка
	      	               |			ИЗ
	      	               |				Документ.Матч.Команды КАК МатчКоманды
	      	               |			ГДЕ
	      	               |				МатчКоманды.Ссылка.Тур.Владелец = &Чемпионат
	      	               |				И МатчКоманды.Ссылка.Проведен = ЛОЖЬ
	      	               |				И МатчКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
	      	               |			СГРУППИРОВАТЬ ПО
	      	               |				МатчКоманды.Ссылка
	      	               |			ИМЕЮЩИЕ
	      	               |				МАКСИМУМ(МатчКоманды.НомерСтроки) = 2)
	      	               |	И (&Все
	      	               |			ИЛИ МатчКоманды.Ссылка.Тур В
	      	               |				(ВЫБРАТЬ ПЕРВЫЕ 5
	      	               |					Туры.Ссылка КАК Ссылка
	      	               |				ИЗ
	      	               |					Справочник.Туры КАК Туры
	      	               |						ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |						ПО
	      	               |							Туры.Ссылка = ТурнирнаяТаблица.Тур
	      	               |				ГДЕ
	      	               |					Туры.Владелец = &Чемпионат
	      	               |					И Туры.ПометкаУдаления = ЛОЖЬ
	      	               |					И ТурнирнаяТаблица.Тур ЕСТЬ NULL
	      	               |				УПОРЯДОЧИТЬ ПО
	      	               |					Туры.Код))
	      	               |;
	      	               |
	      	               |////////////////////////////////////////////////////////////////////////////////
	      	               |ВЫБРАТЬ
	      	               |	ВТ.Команда КАК Команда,
	      	               |	ВТ.Игрок КАК Игрок,
	      	               |	ВТ.НомерСтроки + ВЫБОР
	      	               |		КОГДА ВТ.Фаза1 <= 23 / 4
	      	               |			ТОГДА ВТ.Фаза1 * 25 / 23
	      	               |		КОГДА ВТ.Фаза1 <= 23 / 2
	      	               |			ТОГДА (23 / 4 - (ВТ.Фаза1 - 23 / 4)) * 25 / 23
	      	               |		КОГДА ВТ.Фаза1 <= 23 / 4 * 3
	      	               |			ТОГДА -(ВТ.Фаза1 - 23 / 2) * 25 / 23
	      	               |		ИНАЧЕ -(23 / 4 - (ВТ.Фаза1 - 23 / 4 * 3)) * 25 / 23
	      	               |	КОНЕЦ КАК Фаза1,
	      	               |	ВТ.НомерСтроки + ВЫБОР
	      	               |		КОГДА ВТ.Фаза2 <= 28 / 4
	      	               |			ТОГДА ВТ.Фаза2 * 25 / 28
	      	               |		КОГДА ВТ.Фаза2 <= 28 / 2
	      	               |			ТОГДА (28 / 4 - (ВТ.Фаза2 - 28 / 4)) * 25 / 28
	      	               |		КОГДА ВТ.Фаза2 <= 28 / 4 * 3
	      	               |			ТОГДА -(ВТ.Фаза2 - 28 / 2) * 25 / 28
	      	               |		ИНАЧЕ -(28 / 4 - (ВТ.Фаза2 - 28 / 4 * 3)) * 25 / 28
	      	               |	КОНЕЦ КАК Фаза2,
	      	               |	ВТ.НомерСтроки + ВЫБОР
	      	               |		КОГДА ВТ.Фаза3 <= 33 / 4
	      	               |			ТОГДА ВТ.Фаза3 * 25 / 33
	      	               |		КОГДА ВТ.Фаза3 <= 33 / 2
	      	               |			ТОГДА (33 / 4 - (ВТ.Фаза3 - 33 / 4)) * 25 / 33
	      	               |		КОГДА ВТ.Фаза3 <= 33 / 4 * 3
	      	               |			ТОГДА -(ВТ.Фаза3 - 33 / 2) * 25 / 33
	      	               |		ИНАЧЕ -(33 / 4 - (ВТ.Фаза3 - 33 / 4 * 3)) * 25 / 33
	      	               |	КОНЕЦ КАК Фаза3,
	      	               |	ВТ.Тур КАК Тур
	      	               |ПОМЕСТИТЬ ВТ
	      	               |ИЗ
	      	               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |		Клубы.Команда КАК Команда,
	      	               |		ВЫБОР
	      	               |			КОГДА Клубы.НомерСтроки = 11
	      	               |				ТОГДА 9
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ КАК НомерСтроки,
	      	               |		Игроки.Игрок КАК Игрок,
	      	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 23 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 23) КАК Фаза1,
	      	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 28 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 28) КАК Фаза2,
	      	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 33 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 33) КАК Фаза3,
	      	               |		Клубы.Тур КАК Тур
	      	               |	ИЗ
	      	               |		РегистрСведений.ПеремещенияИгроков.СрезПоследних(, Игрок.ПометкаУдаления = ЛОЖЬ) КАК Игроки
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Клубы КАК Клубы
	      	               |			ПО Игроки.Команда = Клубы.Команда) КАК ВТ
	      	               |;
	      	               |
	      	               |////////////////////////////////////////////////////////////////////////////////
	      	               |ВЫБРАТЬ
	      	               |	Матчи.Ссылка КАК Ссылка,
	      	               |	Матчи.Тур КАК Тур,
	      	               |	Матчи.ТурКод КАК ТурКод,
	      	               |	Матчи.Команда КАК Команда,
	      	               |	Матчи.Команда.Аббревиатура КАК Аббревиатура,
	      	               |	ВЫБОР
	      	               |		КОГДА ВТ.Фаза1 + ВТ.Фаза2 + ВТ.Фаза3 - ВТ2.Фаза1 - ВТ2.Фаза2 - ВТ2.Фаза3 >= 1
	      	               |			ТОГДА 3
	      	               |		КОГДА ВТ.Фаза1 + ВТ.Фаза2 + ВТ.Фаза3 - ВТ2.Фаза1 - ВТ2.Фаза2 - ВТ2.Фаза3 <= -3
	      	               |			ТОГДА 0
	      	               |		ИНАЧЕ 1
	      	               |	КОНЕЦ КАК КоличествоОчков,
	      	               |	Противники.Команда КАК Противник,
	      	               |	Противники.Команда.Аббревиатура КАК АббревиатураП,
	      	               |	Матчи.КоличествоИгр КАК КоличествоИгр
	      	               |ИЗ
	      	               |	Клубы КАК Матчи
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	      	               |			ВТ.Тур КАК Тур,
	      	               |			ВТ.Команда КАК Команда,
	      	               |			СУММА(ВТ.Фаза1) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза1,
	      	               |			СУММА(ВТ.Фаза2) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза2,
	      	               |			СУММА(ВТ.Фаза3) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза3
	      	               |		ИЗ
	      	               |			ВТ КАК ВТ
	      	               |		
	      	               |		СГРУППИРОВАТЬ ПО
	      	               |			ВТ.Команда,
	      	               |			ВТ.Тур) КАК ВТ
	      	               |		ПО Матчи.Тур = ВТ.Тур
	      	               |			И Матчи.Команда = ВТ.Команда
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	      	               |		ПО Матчи.Тур = Туры.Ссылка
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Клубы КАК Противники
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	      	               |				ВТ.Тур КАК Тур,
	      	               |				ВТ.Команда КАК Команда,
	      	               |				СУММА(ВТ.Фаза1) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза1,
	      	               |				СУММА(ВТ.Фаза2) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза2,
	      	               |				СУММА(ВТ.Фаза3) / КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТ.Игрок) КАК Фаза3
	      	               |			ИЗ
	      	               |				ВТ КАК ВТ
	      	               |			
	      	               |			СГРУППИРОВАТЬ ПО
	      	               |				ВТ.Команда,
	      	               |				ВТ.Тур) КАК ВТ2
	      	               |			ПО Противники.Тур = ВТ2.Тур
	      	               |				И Противники.Команда = ВТ2.Команда
	      	               |		ПО Матчи.Ссылка = Противники.Ссылка
	      	               |			И Матчи.НомерСтроки <> Противники.НомерСтроки
	      	               |ГДЕ
	      	               |	Матчи.НомерСтроки = 1
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Тур
	      	               |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Чемпионат",			Чемпионат);
	Запрос.УстановитьПараметр("Все",				Все);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ЗаголовокПолучить()
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат,ВыводитьЧемпионат", Чемпионат.Владелец, Чемпионат, Ложь));
КонецФункции

Функция ТурнирнаяТаблица()
	Возврат СерверныйФункции.ТурнирнаяТаблица(Чемпионат);
КонецФункции
