
Функция ОтчетСформировать(Отчет=Неопределено, Все=Ложь) Экспорт
	Если Отчет = Неопределено Тогда Отчет = ЭтотОбъект; КонецЕсли;
	Все		= (Все И Чемпионат.Владелец.ОлимпийскаяСистема = Ложь);
	Отчет.Данные.Очистить();
	
	ТабДокумент = Новый ТабличныйДокумент;
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда Возврат ТабДокумент; КонецЕсли;
	
	ТабДокумент.Вывести(ЗаголовокПолучить());
	Макет	= ПолучитьОбщийМакет("ТурнирнаяТаблица");
	
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка1|Начало"));
	
	ПоказыватьРазницуМячей = ПоказыватьРазницуМячей();
	Итого	= Новый Структура("Место,КоличествоОчков", 0, 0);
	
	Выборка	= ДанныеПолучить(Все);
	
	Туры	= Новый Массив;
	ТурМакс	= 1;
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка|LL");
	Для Каждого ТекущаяСтрока Из Выборка Цикл
		Если Туры.Найти(ТекущаяСтрока.Тур) = Неопределено Тогда
			Туры.Добавить(ТекущаяСтрока.Тур);
			
			ОбластьМакета.Параметры.Заполнить(ТекущаяСтрока);
			ТабДокумент.Присоединить(ОбластьМакета);
			
			ТурМакс	= Макс(ТурМакс, ТекущаяСтрока.ТурКод);
		КонецЕсли;
	КонецЦикла;
	ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Шапка|NN"));
	
	Команды	= ТурнирнаяТаблица();
	
	ТурМин	= 99;
	Пока Команды.Следующий() Цикл
		ТурМин = Мин(ТурМин, Команды.КоличествоИгр);
	КонецЦикла;
	Команды.Сбросить();
	
	Пока Команды.Следующий() Цикл
		Итого.Место	= Итого.Место + 1;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Детали1|Начало");
		ОбластьМакета.Параметры.Заполнить(Команды);
		Если НЕ ПоказыватьРазницуМячей Тогда
			ОбластьМакета.Параметры.Разница	= СтрШаблон("%1-%2", Команды.КоличествоЗабито, Команды.КоличествоПропущено);
		КонецЕсли;
		ОбластьМакета.Параметры.Место	= Итого.Место;
		ТабДокумент.Вывести(ОбластьМакета);
		
		Итого.КоличествоОчков	= Команды.КоличествоОчков;
		Пустота			= Истина;
		Для Каждого ТекущаяСтрока Из Выборка Цикл
			Если ТекущаяСтрока.Команда = Команды.Команда Или ТекущаяСтрока.Противник = Команды.Команда Тогда
				Пустота	= Ложь;
				Для Каждого Тур Из Туры Цикл
					мСтроки	= Выборка.НайтиСтроки(Новый Структура("Команда,Тур", Команды.Команда, Тур));
					Если мСтроки.Количество() = 0 Тогда
						мСтроки	= Выборка.НайтиСтроки(Новый Структура("Противник,Тур", Команды.Команда, Тур));
						Если мСтроки.Количество() = 0 Тогда
							ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Детали|NN"));
							Продолжить;
						Иначе
							ТекСтрока	= мСтроки.Получить(0);
							ОбластьМакета = Макет.ПолучитьОбласть("Детали2|" + Результат(ТекСтрока.КоличествоОчков, Истина));
							Если ТекСтрока.КоличествоЗабито < 0 Тогда
								ОбластьМакета.Параметры.Аббревиатура	= ?(ПустаяСтрока(ТекСтрока.Аббревиатура), ВРег(Лев(ТекСтрока.Команда, 3)), ТекСтрока.Аббревиатура) + "(A)";
								Итого.КоличествоОчков = Итого.КоличествоОчков + ?(ТекСтрока.КоличествоОчков=1, 1, ?(ТекСтрока.КоличествоОчков=0, 3, 0));
							Иначе
								ОбластьМакета.Параметры.Аббревиатура	= СтрШаблон("%1-%2", ТекСтрока.КоличествоПропущено, ТекСтрока.КоличествоЗабито);
							КонецЕсли;
						КонецЕсли;
					Иначе
						ТекСтрока	= мСтроки.Получить(0);
						ОбластьМакета = Макет.ПолучитьОбласть("Детали2|" + Результат(ТекСтрока.КоличествоОчков));
						Если ТекСтрока.КоличествоЗабито < 0 Тогда
							ОбластьМакета.Параметры.Аббревиатура	= ?(ПустаяСтрока(ТекСтрока.АббревиатураП), ВРег(Лев(ТекСтрока.Противник, 3)), ТекСтрока.АббревиатураП) + "(H)";
							Итого.КоличествоОчков = Итого.КоличествоОчков + ТекСтрока.КоличествоОчков;
						Иначе
							ОбластьМакета.Параметры.Аббревиатура	= СтрШаблон("%1-%2", ТекСтрока.КоличествоЗабито, ТекСтрока.КоличествоПропущено);
						КонецЕсли;
					КонецЕсли;
					ОбластьМакета.Параметры.Регистратор		= ТекСтрока.Ссылка;
					ТабДокумент.Присоединить(ОбластьМакета);
				КонецЦикла;
				ОбластьМакета	= Макет.ПолучитьОбласть("Детали|NN");
				ОбластьМакета.Параметры.КоличествоОчков	= Итого.КоличествоОчков;
				ТабДокумент.Присоединить(ОбластьМакета);
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Пустота Тогда
			ОбластьМакета	= Макет.ПолучитьОбласть("Детали|NN");
			Для Итератор = 1 По Туры.Количество() Цикл
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЦикла;
			ОбластьМакета.Параметры.КоличествоОчков	= 0;
			ТабДокумент.Присоединить(ОбластьМакета);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Новый Структура("Команда,Тур,КоличествоОчков",
			Команды.Команда, ТурМин, Команды.КоличествоОчков));
		ЗаполнитьЗначенияСвойств(Отчет.Данные.Добавить(), Новый Структура("Команда,Тур,КоличествоОчков",
			Команды.Команда, ТурМакс, Итого.КоличествоОчков - Команды.КоличествоОчков));
	КонецЦикла;
	
	ТабДокумент.ФиксацияСлева	= 8;
	
	Возврат ТабДокумент;
КонецФункции

Функция Результат(КоличествоОчков, Реверс=Ложь)
	Если Реверс Тогда Возврат ?(КоличествоОчков=0, "WW", ?(КоличествоОчков=1, "DD", "LL")); КонецЕсли;
	Возврат ?(КоличествоОчков=0, "LL", ?(КоличествоОчков=1, "DD", "WW"));
КонецФункции

Функция ДанныеПолучить(Все)
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 5
	      	               |	Матч.Тур КАК Тур,
	      	               |	Матч.Тур.Код КАК ТурКод
	      	               |ИЗ
	      	               |	Документ.Матч КАК Матч
	      	               |ГДЕ
	      	               |	Матч.Дата >= &Дата
	      	               |	И Матч.ПометкаУдаления = ЛОЖЬ
	      	               |	И Матч.Проведен = ЛОЖЬ
	      	               |	И Матч.Тур.Владелец = &Чемпионат
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	ТурКод");
	Если Все Тогда
		Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		               |	Матч.Тур КАК Тур
		               |ИЗ
		               |	Документ.Матч КАК Матч
		               |ГДЕ
		               |	Матч.Дата >= &Дата
		               |	И Матч.ПометкаУдаления = ЛОЖЬ
		               |	И Матч.Проведен = ЛОЖЬ
		               |	И Матч.Тур.Владелец = &Чемпионат";
	КонецЕсли;
	Запрос.УстановитьПараметр("Чемпионат",			Чемпионат);
	Запрос.УстановитьПараметр("Дата",				ДобавитьМесяц(ТекущаяУниверсальнаяДата(), -1));
	Запрос.УстановитьПараметр("Туры",				Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
	Запрос.Текст = "ВЫБРАТЬ
	               |	МатчКоманды.Ссылка КАК Ссылка,
	               |	НАЧАЛОПЕРИОДА(МатчКоманды.Ссылка.Дата, ДЕНЬ) КАК Дата,
	               |	МатчКоманды.Ссылка.Тур КАК Тур,
	               |	МатчКоманды.Ссылка.Тур.Код КАК ТурКод,
	               |	МатчКоманды.Команда КАК Команда,
	               |	МатчКоманды.НомерСтроки КАК НомерСтроки
	               |ПОМЕСТИТЬ Клубы
	               |ИЗ
	               |	Документ.Матч.Команды КАК МатчКоманды
	               |ГДЕ
	               |	МатчКоманды.Ссылка В
	               |			(ВЫБРАТЬ
	               |				МатчКоманды.Ссылка
	               |			ИЗ
	               |				Документ.Матч.Команды КАК МатчКоманды
	               |			ГДЕ
	               |				МатчКоманды.Ссылка.Тур В (&Туры)
	               |				И МатчКоманды.Ссылка.Проведен = ЛОЖЬ
	               |				И МатчКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
	               |			СГРУППИРОВАТЬ ПО
	               |				МатчКоманды.Ссылка
	               |			ИМЕЮЩИЕ
	               |				МАКСИМУМ(МатчКоманды.НомерСтроки) = 2)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ПеремещенияИгроков.Игрок КАК Игрок,
	               |	ПеремещенияИгроков.Команда КАК Команда,
	               |	ВЫБОР
	               |		КОГДА МатчСоставыКоманд.Игрок ЕСТЬ NULL
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Порядок
	               |ПОМЕСТИТЬ Игроки
	               |ИЗ
	               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, Игрок.ПометкаУдаления = ЛОЖЬ) КАК ПеремещенияИгроков
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Матч.СоставыКоманд КАК МатчСоставыКоманд
	               |		ПО ПеремещенияИгроков.Игрок = МатчСоставыКоманд.Игрок
	               |			И ПеремещенияИгроков.Команда = МатчСоставыКоманд.Команда
	               |			И (МатчСоставыКоманд.Игрок.ПометкаУдаления = ЛОЖЬ)
	               |			И (МатчСоставыКоманд.Ссылка В
	               |				(ВЫБРАТЬ
	               |					МАКСИМУМ(ЕСТЬNULL(МатчСоставыКоманд.Ссылка.Дата, ТурнирнаяТаблица.Регистратор))
	               |				ИЗ
	               |					РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	               |						ЛЕВОЕ СОЕДИНЕНИЕ Документ.Матч.СоставыКоманд КАК МатчСоставыКоманд
	               |						ПО
	               |							ТурнирнаяТаблица.Команда = МатчСоставыКоманд.Команда
	               |								И ТурнирнаяТаблица.Период < МатчСоставыКоманд.Ссылка.Дата
	               |								И ТурнирнаяТаблица.Тур.Владелец = МатчСоставыКоманд.Ссылка.Тур.Владелец
	               |				ГДЕ
	               |					ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	               |				СГРУППИРОВАТЬ ПО
	               |					ТурнирнаяТаблица.Команда))
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Клубы КАК Клубы
	               |		ПО ПеремещенияИгроков.Команда = Клубы.Команда
	               |ГДЕ
	               |	ПеремещенияИгроков.Команда В
	               |			(ВЫБРАТЬ
	               |				ПеремещенияИгроков.Команда
	               |			ИЗ
	               |				РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, Игрок.ПометкаУдаления = ЛОЖЬ
	               |					И ГОД(Игрок.ДатаРОждения) >= 1900) КАК ПеремещенияИгроков
	               |			СГРУППИРОВАТЬ ПО
	               |				ПеремещенияИгроков.Команда
	               |			ИМЕЮЩИЕ
	               |				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПеремещенияИгроков.Игрок) >= 11)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Команда КАК Команда,
	               |	ВТ.Игрок КАК Игрок,
	               |	ВТ.Корреляция КАК Корреляция,
	               |	ВЫБОР
	               |		КОГДА ВТ.Фаза1 <= 23 / 4
	               |			ТОГДА ВТ.Фаза1 * 25 / 23
	               |		КОГДА ВТ.Фаза1 <= 23 / 2
	               |			ТОГДА (23 / 4 - (ВТ.Фаза1 - 23 / 4)) * 25 / 23
	               |		КОГДА ВТ.Фаза1 <= 23 / 4 * 3
	               |			ТОГДА -(ВТ.Фаза1 - 23 / 2) * 25 / 23
	               |		ИНАЧЕ -(23 / 4 - (ВТ.Фаза1 - 23 / 4 * 3)) * 25 / 23
	               |	КОНЕЦ КАК Фаза1,
	               |	ВЫБОР
	               |		КОГДА ВТ.Фаза2 <= 28 / 4
	               |			ТОГДА ВТ.Фаза2 * 25 / 28
	               |		КОГДА ВТ.Фаза2 <= 28 / 2
	               |			ТОГДА (28 / 4 - (ВТ.Фаза2 - 28 / 4)) * 25 / 28
	               |		КОГДА ВТ.Фаза2 <= 28 / 4 * 3
	               |			ТОГДА -(ВТ.Фаза2 - 28 / 2) * 25 / 28
	               |		ИНАЧЕ -(28 / 4 - (ВТ.Фаза2 - 28 / 4 * 3)) * 25 / 28
	               |	КОНЕЦ КАК Фаза2,
	               |	ВЫБОР
	               |		КОГДА ВТ.Фаза3 <= 33 / 4
	               |			ТОГДА ВТ.Фаза3 * 25 / 33
	               |		КОГДА ВТ.Фаза3 <= 33 / 2
	               |			ТОГДА (33 / 4 - (ВТ.Фаза3 - 33 / 4)) * 25 / 33
	               |		КОГДА ВТ.Фаза3 <= 33 / 4 * 3
	               |			ТОГДА -(ВТ.Фаза3 - 33 / 2) * 25 / 33
	               |		ИНАЧЕ -(33 / 4 - (ВТ.Фаза3 - 33 / 4 * 3)) * 25 / 33
	               |	КОНЕЦ КАК Фаза3,
	               |	ВТ.Тур КАК Тур
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |		Клубы.Команда КАК Команда,
	               |		ВЫБОР
	               |			КОГДА Клубы.НомерСтроки = 1
	               |				ТОГДА 10
	               |			ИНАЧЕ 9
	               |		КОНЕЦ КАК Корреляция,
	               |		Игроки.Игрок КАК Игрок,
	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 23 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 23) КАК Фаза1,
	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 28 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 28) КАК Фаза2,
	               |		РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) - 33 * ЦЕЛ(РАЗНОСТЬДАТ(Игроки.Игрок.ДатаРождения, Клубы.Дата, ДЕНЬ) / 33) КАК Фаза3,
	               |		Клубы.Тур КАК Тур
	               |	ИЗ
	               |		Игроки КАК Игроки
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Клубы КАК Клубы
	               |			ПО Игроки.Команда = Клубы.Команда
	               |	ГДЕ
	               |		(Игроки.Команда, Игроки.Порядок) В
	               |				(ВЫБРАТЬ
	               |					Игроки.Команда,
	               |					МАКСИМУМ(Игроки.Порядок)
	               |				ИЗ
	               |					Игроки
	               |				СГРУППИРОВАТЬ ПО
	               |					Игроки.Команда)) КАК ВТ
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Матчи.Ссылка КАК Ссылка,
	               |	Матчи.Тур КАК Тур,
	               |	Матчи.ТурКод КАК ТурКод,
	               |	Матчи.Команда КАК Команда,
	               |	Матчи.Команда.Аббревиатура КАК Аббревиатура,
	               |	ВЫБОР
	               |		КОГДА ВТ.Фаза1 + ВТ.Фаза2 + ВТ.Фаза3 + ВТ.Корреляция - ВТ2.Фаза1 - ВТ2.Фаза2 - ВТ2.Фаза3 - ВТ2.Корреляция >= 4
	               |			ТОГДА 3
	               |		КОГДА ВТ.Фаза1 + ВТ.Фаза2 + ВТ.Фаза3 + ВТ.Корреляция - ВТ2.Фаза1 - ВТ2.Фаза2 - ВТ2.Фаза3 - ВТ2.Корреляция <= -4
	               |			ТОГДА 0
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК КоличествоОчков,
	               |	-1 КАК КоличествоЗабито,
	               |	Противники.Команда КАК Противник,
	               |	Противники.Команда.Аббревиатура КАК АббревиатураП,
	               |	-1 КАК КоличествоПропущено
	               |ИЗ
	               |	Клубы КАК Матчи
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			ВТ.Тур КАК Тур,
	               |			ВТ.Команда КАК Команда,
	               |			СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза1) КАК Фаза1,
	               |			СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза2 * 1.2) КАК Фаза2,
	               |			СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза3 * 1.1) КАК Фаза3,
	               |			СУММА(ВЫБОР
	               |					КОГДА ВТ.Фаза1 > 0
	               |							И ВТ.Фаза2 > 0
	               |							И ВТ.Фаза3 > 0
	               |						ТОГДА 1
	               |					КОГДА ВТ.Фаза1 < 0
	               |							И ВТ.Фаза2 < 0
	               |							И ВТ.Фаза3 < 0
	               |						ТОГДА -1
	               |					ИНАЧЕ 0
	               |				КОНЕЦ) КАК Корреляция
	               |		ИЗ
	               |			ВТ КАК ВТ
	               |		
	               |		СГРУППИРОВАТЬ ПО
	               |			ВТ.Команда,
	               |			ВТ.Тур) КАК ВТ
	               |		ПО Матчи.Тур = ВТ.Тур
	               |			И Матчи.Команда = ВТ.Команда
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Клубы КАК Противники
	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |				ВТ.Тур КАК Тур,
	               |				ВТ.Команда КАК Команда,
	               |				СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза1) КАК Фаза1,
	               |				СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза2 * 1.2) КАК Фаза2,
	               |				СРЕДНЕЕ(ВТ.Корреляция + ВТ.Фаза3 * 1.1) КАК Фаза3,
	               |				СУММА(ВЫБОР
	               |						КОГДА ВТ.Фаза1 > 0
	               |								И ВТ.Фаза2 > 0
	               |								И ВТ.Фаза3 > 0
	               |							ТОГДА 1
	               |						КОГДА ВТ.Фаза1 < 0
	               |								И ВТ.Фаза2 < 0
	               |								И ВТ.Фаза3 < 0
	               |							ТОГДА -1
	               |						ИНАЧЕ 0
	               |					КОНЕЦ) КАК Корреляция
	               |			ИЗ
	               |				ВТ КАК ВТ
	               |			
	               |			СГРУППИРОВАТЬ ПО
	               |				ВТ.Команда,
	               |				ВТ.Тур) КАК ВТ2
	               |			ПО Противники.Тур = ВТ2.Тур
	               |				И Противники.Команда = ВТ2.Команда
	               |		ПО Матчи.Ссылка = Противники.Ссылка
	               |ГДЕ
	               |	Матчи.НомерСтроки = 1
	               |	И Противники.НомерСтроки = 2
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТурнирнаяТаблица.Регистратор,
	               |	ТурнирнаяТаблица.Тур,
	               |	ТурнирнаяТаблица.Тур.Код,
	               |	ТурнирнаяТаблица.Команда,
	               |	ТурнирнаяТаблица.Команда.Аббревиатура,
	               |	ТурнирнаяТаблица.КоличествоОчков,
	               |	ТурнирнаяТаблица.КоличествоГолов,
	               |	Противники.Команда,
	               |	Противники.Команда.Аббревиатура,
	               |	Противники.КоличествоГолов
	               |ИЗ
	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противники
	               |		ПО ТурнирнаяТаблица.Регистратор = Противники.Регистратор
	               |ГДЕ
	               |	ТурнирнаяТаблица.Тур В(&Туры)
	               |	И ТурнирнаяТаблица.НомерСтроки = 1
	               |	И Противники.НомерСтроки = 2
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТурКод,
	               |	КоличествоЗабито УБЫВ";
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ЗаголовокПолучить()
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат,ВыводитьЧемпионат", Чемпионат.Владелец, Чемпионат, Ложь));
КонецФункции

Функция ТурнирнаяТаблица()
	Возврат СерверныйФункции.ТурнирнаяТаблица(Чемпионат);
КонецФункции

Функция ПоказыватьРазницуМячей()
	Возврат СерверныйФункции.ПоказыватьРазницуМячей();
КонецФункции
