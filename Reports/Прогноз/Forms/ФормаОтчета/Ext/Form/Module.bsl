
&НаСервере
Процедура СформироватьНаСервере()
	Если ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Результат = РеквизитФормыВЗначение("Отчет").ОтчетСформировать(Отчет, Все);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ГрафикОбновитьНаСервере(Пошагам=Ложь)
	Если ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Если ?(Пошагам, Список.Количество() = 0, Истина) Тогда
			График.Очистить();
			
			ТЧ = Отчет.Данные.Выгрузить(, "Команда,КоличествоОчков");
			ТЧ.Свернуть("Команда", "КоличествоОчков");
			ТЧ.Сортировать("КоличествоОчков УБЫВ");
			Для Каждого ТекСтрока Из ТЧ Цикл
				Точка = График.УстановитьТочку(ТекСтрока.Команда);
			КонецЦикла;
		КонецЕсли;
		
		Если Пошагам Тогда
			Поисковик = Новый Структура("Команда");
			Для Итератор = 0 По 11 Цикл
				Поисковик.Команда = Неопределено;
				Для Каждого ТекСтрока Из Отчет.Данные Цикл
					Если Список.НайтиПоЗначению(ТекСтрока.Команда) = Неопределено Тогда
						Список.Добавить(ТекСтрока.Команда, ТекСтрока.Команда);
						
						ЗаполнитьЗначенияСвойств(Поисковик, ТекСтрока);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				мСтроки = Отчет.Данные.НайтиСтроки(Поисковик);
				Для Каждого ТекСтрока Из мСтроки Цикл
					Точка = График.УстановитьТочку(ТекСтрока.Команда);
					Точка.Расшифровка = ТекСтрока.Команда;
					График.УстановитьЗначение(Точка, График.УстановитьСерию(ТекСтрока.Тур), ТекСтрока.КоличествоОчков);
				КонецЦикла;
			КонецЦикла;
		Иначе
			Для Каждого ТекСтрока Из Отчет.Данные Цикл
				Точка = График.УстановитьТочку(ТекСтрока.Команда);
				Точка.Расшифровка = ТекСтрока.Команда;
				График.УстановитьЗначение(Точка, График.УстановитьСерию(ТекСтрока.Тур), ТекСтрока.КоличествоОчков);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОбновить() Экспорт
	Если Отчет.Данные.Количество() = 2 * Список.Количество() Тогда
		ОтключитьОбработчикОжидания("ГрафикОбновить");
	Иначе
		ГрафикОбновитьНаСервере(Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		СформироватьНаСервере();
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЧемпионатПолучитьПоследнее()
	Возврат СерверныйФункции.ЧемпионатПолучитьПоследнее();
КонецФункции

&НаКлиенте
Процедура ЧемпионатОчистка(Элемент, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		СтандартнаяОбработка	= Ложь;
		Отчет.Чемпионат = ЧемпионатПолучитьПоследнее();
		СформироватьНаСервере();
		ГрафикОбновитьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВидимостьУстановить()
	Элементы.Все.Видимость	= (Отчет.Чемпионат.Владелец.ОлимпийскаяСистема = Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент=Неопределено)
	ВидимостьУстановить();
	СформироватьНаСервере();
	ГрафикОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидимостьУстановить();
	СформироватьНаСервере();
	ПодключитьОбработчикОжидания("ГрафикОбновить", 1);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЧемпионатыЗагрузить();
	Если Параметры.Свойство("Чемпионат") Тогда
		Отчет.Чемпионат	= Параметры.Чемпионат;
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЧемпионатыЗагрузить()
	Чемпионаты.ЗагрузитьЗначения(СерверныйФункции.ЧемпионатыТекущие(Истина));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИгрокиПолучить(Клуб)
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	      	               |	ПеремещенияИгроков.Игрок КАК Игрок,
	      	               |	ПеремещенияИгроков.Команда КАК Команда,
	      	               |	ВЫБОР
	      	               |		КОГДА МатчСоставыКоманд.Игрок ЕСТЬ NULL
	      	               |			ТОГДА 1
	      	               |		ИНАЧЕ 2
	      	               |	КОНЕЦ КАК Порядок
	      	               |ПОМЕСТИТЬ Игроки
	      	               |ИЗ
	      	               |	РегистрСведений.ПеремещенияИгроков.СрезПоследних(&Дата, Игрок.ПометкаУдаления = ЛОЖЬ) КАК ПеремещенияИгроков
	      	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Матч.СоставыКоманд КАК МатчСоставыКоманд
	      	               |		ПО ПеремещенияИгроков.Игрок = МатчСоставыКоманд.Игрок
	      	               |			И ПеремещенияИгроков.Команда = МатчСоставыКоманд.Команда
	      	               |			И (МатчСоставыКоманд.Игрок.ПометкаУдаления = ЛОЖЬ)
	      	               |			И (МатчСоставыКоманд.Ссылка В
	      	               |				(ВЫБРАТЬ
	      	               |					МАКСИМУМ(ЕСТЬNULL(МатчСоставыКоманд.Ссылка.Дата, ТурнирнаяТаблица.Регистратор))
	      	               |				ИЗ
	      	               |					РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |						ЛЕВОЕ СОЕДИНЕНИЕ Документ.Матч.СоставыКоманд КАК МатчСоставыКоманд
	      	               |						ПО
	      	               |							ТурнирнаяТаблица.Команда = МатчСоставыКоманд.Команда
	      	               |								И ТурнирнаяТаблица.Период < МатчСоставыКоманд.Ссылка.Дата
	      	               |								И ТурнирнаяТаблица.Тур.Владелец = МатчСоставыКоманд.Ссылка.Тур.Владелец
	      	               |				ГДЕ
	      	               |					ТурнирнаяТаблица.Команда = &Команда
	      	               |					И ТурнирнаяТаблица.Тур.Владелец = &Чемпионат))
	      	               |ГДЕ
	      	               |	ПеремещенияИгроков.Команда = &Команда
	      	               |;
	      	               |
	      	               |////////////////////////////////////////////////////////////////////////////////
	      	               |ВЫБРАТЬ
	      	               |	Игроки.Игрок КАК Игрок
	      	               |ИЗ
	      	               |	Игроки КАК Игроки
	      	               |ГДЕ
	      	               |	Игроки.Порядок В
	      	               |			(ВЫБРАТЬ
	      	               |				МАКСИМУМ(Игроки.Порядок)
	      	               |			ИЗ
	      	               |				Игроки)
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Игрок
	      	               |АВТОУПОРЯДОЧИВАНИЕ");
	Запрос.УстановитьПараметр("Команда",			Клуб);
	Запрос.УстановитьПараметр("Чемпионат",			СерверныйФункции.ЧемпионатПолучитьПоследнее(Клуб));
	Запрос.УстановитьПараметр("Дата",				НачалоДня(ТекущаяУниверсальнаяДата()));
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
КонецФункции

&НаКлиенте
Процедура ПослеВыбораИгрока(Элемент, ПараметрКоманды=Неопределено) Экспорт
	НаКлиенте.Показать(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ГрафикОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Команды") Тогда
		СтандартнаяОбработка = Ложь;
		
		Игроки.ЗагрузитьЗначения(ИгрокиПолучить(Расшифровка));
		Игроки.Добавить(Расшифровка);
		ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ПослеВыбораИгрока", ЭтотОбъект), Игроки);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Команды") Тогда
		Игроки.ЗагрузитьЗначения(ИгрокиПолучить(Расшифровка));
		Если Игроки.Количество() > 0 Тогда
			СтандартнаяОбработка = Ложь;
			Игроки.Добавить(Расшифровка);
			Игроки.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ПослеВыбораИгрока", ЭтотОбъект), "Выберите игрока");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
