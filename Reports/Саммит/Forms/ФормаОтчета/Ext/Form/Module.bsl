
&НаСервере
Процедура СформироватьНаСервере()
	Если ЗначениеЗаполнено(Отчет.Чемпионат) Тогда
		Результат = РеквизитФормыВЗначение("Отчет").ОтчетСформировать(Отчет, ПоказыватьРазницуМячей);
		ГрафикОбновить();
		ХронологияОбновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ХронологияОбновить()
	Если Лига = Отчет.Чемпионат.Владелец Тогда Возврат; КонецЕсли;
	
	Хронология.Очистить();
	
	Лига	= Отчет.Чемпионат.Владелец;
	
	Хронология.Вывести(РеквизитФормыВЗначение("Отчет").ЗаголовокПолучить(Лига, Лига));
	Хронология.ФиксацияСлева	= 8;
	Макет  = РеквизитФормыВЗначение("Отчет").МакетПолучить();
	
	Запрос	= Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 11
	      	               |	Туры.Владелец КАК Чемпионат,
	      	               |	ГОД(Туры.Владелец.ДатаОкончания) КАК Год
	      	               |ПОМЕСТИТЬ Чемпионаты
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	      	               |		ПО ТурнирнаяТаблица.Тур = Туры.Ссылка
	      	               |ГДЕ
	      	               |	Туры.Владелец.Владелец = &Лига
	      	               |	И ГОД(Туры.Владелец.ДатаОкончания) >= 2000
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Год УБЫВ
	      	               |;
	      	               |
	      	               |////////////////////////////////////////////////////////////////////////////////
	      	               |ВЫБРАТЬ
	      	               |	Чемпионаты.Год КАК Год,
	      	               |	ТурнирнаяТаблица.Команда КАК Команда,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.Регистратор ЕСТЬ NULL
	      	               |				ТОГДА 0
	      	               |			КОГДА ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК КоличествоИгр,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) КАК КоличествоЗабито,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоОчков), 0) КАК КоличествоОчков,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ЕСТЬNULL(ТурнирнаяТаблица.КоличествоОчков, 0) > ЕСТЬNULL(Противник.КоличествоОчков, 0)
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Выигрыш,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ЕСТЬNULL(ТурнирнаяТаблица.КоличествоОчков, 1) = ЕСТЬNULL(Противник.КоличествоОчков, 0)
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Ничья,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ЕСТЬNULL(ТурнирнаяТаблица.КоличествоОчков, 0) < ЕСТЬNULL(Противник.КоличествоОчков, 0)
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Проигрыш,
	      	               |	ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) КАК КоличествоПропущено,
	      	               |	ЕСТЬNULL(СУММА(ТурнирнаяТаблица.КоличествоГолов), 0) - ЕСТЬNULL(СУММА(Противник.КоличествоГолов), 0) КАК Разница
	      	               |ИЗ
	      	               |	Чемпионаты КАК Чемпионаты
	      	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противник
	      	               |				ПО ТурнирнаяТаблица.Регистратор = Противник.Регистратор
	      	               |					И (ЕСТЬNULL(ТурнирнаяТаблица.НомерСтроки, 0) <> ЕСТЬNULL(Противник.НомерСтроки, 0))
	      	               |			ПО Туры.Ссылка = ТурнирнаяТаблица.Тур
	      	               |		ПО Чемпионаты.Чемпионат = Туры.Владелец
	      	               |
	      	               |СГРУППИРОВАТЬ ПО
	      	               |	ТурнирнаяТаблица.Команда,
	      	               |	Чемпионаты.Год
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Год УБЫВ,
	      	               |	КоличествоОчков УБЫВ,
	      	               |	Разница УБЫВ,
	      	               |	КоличествоЗабито УБЫВ");
	Запрос.УстановитьПараметр("Лига",				Лига);
	Выборка	= Запрос.Выполнить().Выбрать();
	
	//Хронология.НачатьАвтогруппировкуКолонок();
	Область = Макет.ПолучитьОбласть("Шапка|Начало");
	Хронология.Вывести(Область, 1);
	
	Область	= Макет.ПолучитьОбласть("Шапка|Год");
	Годы	= Новый Массив;
	ГруппаМ	= 1;
	Пока Выборка.Следующий() Цикл
		Если Годы.Найти(Выборка.Год) = Неопределено Тогда
			ГруппаМ = ГруппаМ + 1;
			Область.Параметры.Заполнить(Выборка);
			Хронология.Присоединить(Область, ГруппаМ);
			
			Годы.Добавить(Выборка.Год);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из Лига.Команды Цикл
		Область = Макет.ПолучитьОбласть("Детали|Начало");
		Область.Параметры.Заполнить(ТекСтрока);
		Хронология.Вывести(Область, 1);
		ГруппаТ	= 1;
		Для Каждого Год Из Годы Цикл
			Место	= 0;
			Мимо	= Истина;
			Выборка.Сбросить();
			Пока Выборка.Следующий() Цикл
				Если Выборка.Год = Год Тогда
					Место	= Место + 1;
					Если Выборка.Команда = ТекСтрока.Команда Тогда
						Мимо	= Ложь;
						ГруппаТ = ГруппаТ + 1;
						Область	= Макет.ПолучитьОбласть("Детали|Год");
						Область.Параметры.Заполнить(Выборка);
						Область.Параметры.Место	= Место;
						Хронология.Присоединить(Область, ГруппаТ);
						
						Если ГруппаТ = ГруппаМ Тогда
							Прервать;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если Мимо Тогда
				ГруппаТ = ГруппаТ + 1;
				Область	= Макет.ПолучитьОбласть("Детали|Год");
				//Область.Параметры.Место	= "X";
				Хронология.Присоединить(Область, ГруппаТ);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	//Хронология.ЗакончитьАвтогруппировкуКолонок();
КонецПроцедуры

&НаСервере
Функция ГрафикиПолучить(КакЭлементы=Ложь)
	мГрафики = Новый Массив;
	Если КакЭлементы Тогда
		мГрафики.Добавить(Элементы.ГрафикРаздельно1);
		мГрафики.Добавить(Элементы.ГрафикРаздельно2);
		мГрафики.Добавить(Элементы.ГрафикРаздельно3);
		мГрафики.Добавить(Элементы.ГрафикРаздельно4);
		мГрафики.Добавить(Элементы.ГрафикРаздельно5);
		мГрафики.Добавить(Элементы.ГрафикРаздельно6);
	Иначе
		мГрафики.Добавить(ГрафикРаздельно1);
		мГрафики.Добавить(ГрафикРаздельно2);
		мГрафики.Добавить(ГрафикРаздельно3);
		мГрафики.Добавить(ГрафикРаздельно4);
		мГрафики.Добавить(ГрафикРаздельно5);
		мГрафики.Добавить(ГрафикРаздельно6);
	КонецЕсли;
	Возврат мГрафики;
КонецФункции

&НаСервере
Процедура ГрафикОбновить()
	Если Элементы.ГруппаГрафики.Видимость Тогда
		СерверныйФункции.ГрафикОбновить(График, Отчет.Данные, НЕ ПоказыватьРазницуМячей);
		
		мГрафики	= ГрафикиПолучить(Истина);
		_Команды	= Отчет.Чемпионат.Владелец.Команды;
		Для Итератор = 0 По 5 Цикл
			мГрафики.Получить(Итератор).Видимость	= (Итератор < _Команды.Количество());
		КонецЦикла;
		
		мГрафики	= ГрафикиПолучить();
		Для Каждого ТекСтрока Из _Команды Цикл
			мГрафики.Получить(ТекСтрока.НомерСтроки - 1).ОбластьЗаголовка.Текст	= ТекСтрока.Команда;
			СерверныйФункции.ГрафикОбновить(мГрафики.Получить(ТекСтрока.НомерСтроки - 1), Отчет.Данные.НайтиСтроки(Новый Структура("Команда", ТекСтрока.Команда)), НЕ ПоказыватьРазницуМячей);
			Если ТекСтрока.НомерСтроки - 1 = мГрафики.ВГраница() Тогда Прервать; КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда=Неопределено)
	Если ПроверитьЗаполнение() Тогда
		СформироватьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ТипГрафикаЗаполнить();
	ТипГрафикаПриИзменении();
	Сформировать();
КонецПроцедуры

&НаКлиенте
Процедура ЧемпионатПриИзменении(Элемент)
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты") И Параметр = Отчет.Чемпионат Тогда
		ПодключитьОбработчикОжидания("КомандаСформировать", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформировать() Экспорт
	Сформировать();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Лиги.ЗагрузитьЗначения(ЛигиПолучить());
	Если Параметры.Свойство("Чемпионат") Тогда
		Если Параметры.Чемпионат.Владелец.ОлимпийскаяСистема Тогда
			Отказ	= Истина;
		Иначе
			Отчет.Чемпионат	= Параметры.Чемпионат;
			ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("Лига") И Параметры.Лига.ОлимпийскаяСистема Тогда
		Отказ	= Истина;
	ИначеЕсли Параметры.Свойство("Лига") Тогда
		Отчет.Чемпионат	= ЧемпионатЛигиПоследний(Параметры.Лига);
		ОбщегоНазначения.ЗаголовокСкрыть(ЭтаФорма, Элементы.ГруппаПараметры);
		
	ИначеЕсли НЕ Параметры.Свойство("График") Тогда
		Элементы.ГруппаГрафики.Видимость	= Ложь;
	КонецЕсли;
	Если Элементы.ГруппаГрафики.Видимость Тогда
		СерииЗаполнить();
	КонецЕсли;
	ПоказыватьРазницуМячей	= ПоказыватьРазницуМячей();
КонецПроцедуры

&НаСервере
Процедура СерииЗаполнить()
	СерверныйФункции.СерииЗаполнить(График);
	Для Каждого Граф Из ГрафикиПолучить() Цикл
		СерверныйФункции.СерииЗаполнить(Граф);
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоказыватьРазницуМячей()
	Возврат СерверныйФункции.ПоказыватьРазницуМячей();
КонецФункции

&НаСервереБезКонтекста
Функция ЧемпионатЛигиПоследний(Параметр)
	Возврат СерверныйФункции.ЧемпионатЛигиПоследний(Параметр);
КонецФункции

&НаСервереБезКонтекста
Функция ЛигиПолучить()
	Возврат СерверныйФункции.ЛигиНациональные();
КонецФункции

&НаКлиенте
Процедура ТипГрафикаЗаполнить()
	Элементы.ТипГрафика.СписокВыбора.Добавить(0, ТипДиаграммы.ГистограммаСНакоплением);
	Элементы.ТипГрафика.СписокВыбора.Добавить(1, ТипДиаграммы.РадарныйГрафикСОбластямиИНакоплением);
КонецПроцедуры

&НаКлиенте
Процедура ТипГрафикаПриИзменении(Элемент=Неопределено)
	Если ТипГрафика = 1 Тогда
		График.ТипДиаграммы	= ТипДиаграммы.РадарныйГрафикСОбластямиИНакоплением;
	Иначе
		График.ТипДиаграммы	= ТипДиаграммы.ГистограммаСНакоплением;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьРазницуМячейПриИзменении(Элемент)
	СформироватьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ГрафикРаздельноОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
