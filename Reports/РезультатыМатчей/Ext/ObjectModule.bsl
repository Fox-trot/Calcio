
Функция ОтчетСформировать() Экспорт
	ТабДокумент = Новый ТабличныйДокумент;
	
	Лига	= Чемпионат.Владелец;
	
	ТабДокумент.Вывести(ЗаголовокПолучить(Лига));
	Макет  = ПолучитьОбщийМакет("ТурнирнаяТаблица");
	
	Выборка = СерверныйФункции.ТурнирнаяТаблица(Чемпионат);
	Данные	= ДанныеПолучить();
	
	Итого	= Новый Структура("Место", 0);
	Команды	= Новый Массив;
	ТабДокумент.Вывести(Макет.ПолучитьОбласть("Шапка|Команды"));
	Пока Выборка.Следующий() Цикл
		Команды.Добавить(Выборка.Команда);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка|Команда");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ТабДокумент.Присоединить(ОбластьМакета);
	КонецЦикла;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		Итого.Место	= Итого.Место + 1;
		ОбластьМакета = Макет.ПолучитьОбласть("Детали|Команды");
		ОбластьМакета.Параметры.Заполнить(Выборка);
		ОбластьМакета.Параметры.Место	= Итого.Место;
		ТабДокумент.Вывести(ОбластьМакета);
		
		Для Каждого Противник Из Команды Цикл
			Если Выборка.Команда = Противник Тогда
				ТабДокумент.Присоединить(Макет.ПолучитьОбласть("Детали|Команда"));
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("Детали|Команда");
				Матчи	= Данные.НайтиСтроки(Новый Структура("Команда,Противник", Выборка.Команда, Противник));
				Расшифровка = Новый Структура("Счет,Регистратор", "", Новый СписокЗначений);
				Для Каждого Матч Из Матчи Цикл
					Если ЗначениеЗаполнено(Матч.Регистратор) Или НЕ ПустаяСтрока(Расшифровка.Счет) Тогда
						Расшифровка.Счет = ?(ПустаяСтрока(Расшифровка.Счет), "", Расшифровка.Счет + Символы.ПС) + СтрШаблон("%1-%2", Матч.КоличествоЗабито, Матч.КоличествоПропущено);
						Расшифровка.Регистратор.Добавить(Матч.Регистратор);
					Иначе
						ОбластьМакета = Макет.ПолучитьОбласть("Пятерка|Команда");
						Расшифровка.Счет = ?(ПустаяСтрока(Расшифровка.Счет), "", Расшифровка.Счет + Символы.ПС) + Формат(Матч.Дата, "ДФ=dd.MM");
						Расшифровка.Регистратор.Добавить(Матч.Матч);
					КонецЕсли;
					Данные.Удалить(Матч);
				КонецЦикла;
				ОбластьМакета.Параметры.Заполнить(Расшифровка);
				ТабДокумент.Присоединить(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТабДокумент;
КонецФункции

Функция ДанныеПолучить()
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	МатчКоманды.Команда КАК Команда,
	      	               |	Противники.Команда КАК Противник,
	      	               |	МатчКоманды.Ссылка.Дата КАК Дата,
	      	               |	МатчКоманды.Ссылка КАК Матч,
	      	               |	ТурнирнаяТаблица.Регистратор КАК Регистратор,
	      	               |	МатчКоманды.КоличествоГолов КАК КоличествоЗабито,
	      	               |	Противники.КоличествоГолов КАК КоличествоПропущено
	      	               |ИЗ
	      	               |	Документ.Матч.Команды КАК МатчКоманды
	      	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ПО МатчКоманды.Ссылка = ТурнирнаяТаблица.Регистратор
	      	               |			И МатчКоманды.Команда = ТурнирнаяТаблица.Команда
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч.Команды КАК Противники
	      	               |		ПО МатчКоманды.Ссылка = Противники.Ссылка
	      	               |ГДЕ
	      	               |	МатчКоманды.Ссылка.Тур.Владелец = &Чемпионат
	      	               |	И МатчКоманды.Ссылка.ПометкаУдаления = ЛОЖЬ
	      	               |	И МатчКоманды.НомерСтроки = 1
	      	               |	И Противники.НомерСтроки = 2
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Команда,
	      	               |	Противник");
	Запрос.УстановитьПараметр("Чемпионат",		Чемпионат);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ЗаголовокПолучить(Лига)
	Возврат СерверныйФункции.ЗаголовокПолучить(Новый Структура("Лига,Чемпионат,ВыводитьЧемпионат", Лига, Чемпионат, Ложь));
КонецФункции
