
&НаКлиенте
Процедура МатчиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьЗначение(Элемент.ТекущиеДанные.Матч);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Владелец	= Справочники.ВидыСпорта.НайтиПоНаименованию("Футбол");
		//Изображение	= ИзображениеДефолтПолучить();
	Иначе
		Матчи.Параметры.УстановитьЗначениеПараметра("Ссылка", Объект.Ссылка);
		ТекущийСостав.Параметры.УстановитьЗначениеПараметра("Ссылка", Объект.Ссылка);

		Если Объект.Ссылка.Изображение.Получить() = Неопределено Тогда
			//Изображение	= ИзображениеДефолтПолучить();
		Иначе
			Изображение	= ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Изображение");
		КонецЕсли;
		
		Если ЕстьИстория() Тогда
			Элементы.Владелец.Доступность	= Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЕстьИстория()
	Возврат СерверныйФункции.ЕстьИстория(Объект.Ссылка);
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.Ссылка.Пустая() Тогда
		Элементы.Группа1.ТекущаяСтраница = Элементы.ОсновнаяИнформация;
	Иначе
		Элементы.Группа1.ТекущаяСтраница = Элементы.ГруппаМатчи;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Матчи.Параметры.УстановитьЗначениеПараметра("Ссылка", Объект.Ссылка);
	ТекущийСостав.Параметры.УстановитьЗначениеПараметра("Ссылка", Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Команды")
	И Параметр = Объект.Ссылка
	Тогда
		Элементы.Матчи.Обновить();
		Элементы.ТекущийСостав.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИсключитьНаСервере(Ссылка)
	Ответ = Ложь;
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Игроки") Тогда
		Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(НЕ Ссылка.ПометкаУдаления);
		Ответ = Истина;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура Исключить(Команда)
	Если ИсключитьНаСервере(Элементы.ТекущийСостав.ТекущиеДанные.Игрок) Тогда
		Элементы.ТекущийСостав.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СезонНаСервере()
	Сезон.Очистить();
	ТекЧемпионат = СерверныйФункции.ЧемпионатПолучитьПоследнее(Объект.Ссылка);
	Если ЗначениеЗаполнено(ТекЧемпионат) Тогда
		МестоВЧемпионате = СерверныйФункции.МестоВЧемпионате(Объект.Ссылка, ТекЧемпионат);
		Если МестоВЧемпионате <> 0 Тогда
			нСтрока = Сезон.Добавить();
			нСтрока.Ключ		= "Место";
			нСтрока.Значение	= МестоВЧемпионате;
		КонецЕсли;
	КонецЕсли;
	Стат	= СерверныйФункции.СтатистикаИгр(Объект.Ссылка, ТекЧемпионат);
	Для Каждого ТекЭлемент Из Стат Цикл
		ЗаполнитьЗначенияСвойств(Сезон.Добавить(), ТекЭлемент);
	КонецЦикла;
	Возврат Строка(ТекЧемпионат);
КонецФункции

&НаКлиенте
Процедура СезонОбновитьНаКлиенте()
	Слово = СезонНаСервере();
	Элементы.ГруппаСезон.Заголовок = ?(ПустаяСтрока(Слово), "Сезон", "Сезон (" + Слово + ")");
КонецПроцедуры

&НаКлиенте
Процедура СезонОбновить(Команда)
	СезонОбновитьНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура Группа1ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница.Имя = "ГруппаСезон" И Сезон.Количество() = 0 Тогда
		СезонОбновитьНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТекущийСоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элемент.ТекущиеДанные.Игрок);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзображение(Команда)
	Если СтрСравнить(Команда.Имя, "ВыбратьИзображение") = 0 Тогда
		НачатьПомещениеФайла(Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект), , , Истина, УникальныйИдентификатор);
	ИначеЕсли Команда.Имя = "УдалитьИзображение" Тогда
		//Изображение = ИзображениеДефолтПолучить();
		//ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	Если Результат Тогда
		Если ОбщегоНазначения.РасширениеФайлаВерно(ВыбранноеИмяФайла) Тогда
			Изображение = Адрес;
		Иначе
			ОбщегоНазначения.СообщитьОбОшибке("Данные тип файла не поддерживается");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервереИзображение(Объект, Знач Адрес="", Отказ=Ложь) Экспорт
	Если Отказ Тогда
		//
	ИначеЕсли ЭтоАдресВременногоХранилища(Адрес) Тогда
		тИзображение = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
		Если Объект.Изображение.Получить() <> тИзображение.Получить() Тогда
			Объект.Изображение = тИзображение;
		КонецЕсли;
	ИначеЕсли ПустаяСтрока(Адрес) И ЗначениеЗаполнено(Объект.Изображение.Получить()) Тогда
		Объект.Изображение = NULL;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Модифицированность Тогда
		ПередЗаписьюНаСервереИзображение(ТекущийОбъект, Изображение);
	КонецЕсли;
КонецПроцедуры
