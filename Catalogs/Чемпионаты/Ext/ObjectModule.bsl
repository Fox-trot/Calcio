
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ДатаНачала >= ДатаОкончания Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Неверно указаны даты", Отказ, Ссылка, "ДатаОкончания");
	КонецЕсли;
	Если Команды.Количество() <> Цел(Команды.Количество() / 2) * 2 Тогда
		//	https://it.wikipedia.org/wiki/S%C3%BCper_Lig_2020-2021
		ОбщегоНазначения.СообщитьОбОшибке("Возможно неверное количество команд",, Ссылка, "Команды");
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ТекЛига			= ОбъектКопирования.Владелец;
	ЧемпионатЛигиПоследний = СерверныйФункции.ЧемпионатЛигиПоследний(ТекЛига);
	Наименование	= "";
	нГод			= 0;
	Слова			= Разделить(Строка(ЧемпионатЛигиПоследний));
	Для Каждого Слово Из Слова Цикл
		Если СтрНайти("0123456789", Лев(Слово, 1)) > 0 Тогда
			нГод = СтрокаКакЧисло(Лев(Слово, 4)) + 1;
			Прервать;
		Иначе
			Наименование = Наименование + Слово + " ";
		КонецЕсли;
	КонецЦикла;
	
	Наименование = Наименование + ?(нГод = 0,
		ФорматЧГ(ТекущаяДатаСеанса()) + "/" + Формат((Год(ТекущаяДатаСеанса()) + 1) - Цел((Год(ТекущаяДатаСеанса()) + 1) / 100) * 100, "ЧН=00"),
		ФорматЧГ(нГод) + "/" + ФорматЧГ(нГод + 1));
	
	ДатаНачала		= ДобавитьМесяц(?(нГод = 0, ДатаНачала, ЧемпионатЛигиПоследний.ДатаНачала), 12);
	ДатаОкончания	= ДобавитьМесяц(?(нГод = 0, ДатаОкончания, ЧемпионатЛигиПоследний.ДатаОкончания), 12);
	
	Команды.Очистить();
	Шаг		= 0;
	Выборка	= СерверныйФункции.ТурнирнаяТаблица(ЧемпионатЛигиПоследний);
	Пока Выборка.Следующий() И Шаг < ТекЛига.КоличествоКоманд - ТекЛига.КоличествоРотация Цикл
		ЗаполнитьЗначенияСвойств(Команды.Добавить(), Выборка);
		
		Шаг = Шаг + 1;
	КонецЦикла;
	Команды.Сортировать("Команда");
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	КомандыСвернуть();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Лига") Тогда
		Владелец	= ДанныеЗаполнения;
		Команды.Загрузить(ДанныеЗаполнения.Команды.Выгрузить());
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Владелец") Тогда
			ОбработкаЗаполнения(ДанныеЗаполнения.Владелец, ТекстЗаполнения, Ложь);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура КомандыСвернуть()
	СерверныйФункции.КомандыСвернуть(Команды);
КонецПроцедуры

Процедура КомандыУпорядочить(Тимз) Экспорт
	Выборка	= ТурнирнаяТаблица();
	Если Выборка.Количество() = 0 Тогда
		Тимз.Сортировать("Команда");
	Иначе
		Тимз.Очистить();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Тимз.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ТурнирнаяТаблица()
	Возврат СерверныйФункции.ТурнирнаяТаблица(Ссылка);
КонецФункции

Функция ИгрыВсе() Экспорт
	Если ДатаОкончания > ТекущаяДатаСеанса()Тогда Возврат Ложь; КонецЕсли;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	МАКСИМУМ(ТурнирнаяТаблица.Период) КАК Количество
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |	И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
	      	               |
	      	               |ИМЕЮЩИЕ
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) = (КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) - 1) * КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда)");
	Если Владелец.ОлимпийскаяСистема Тогда
		Запрос.Текст	= "ВЫБРАТЬ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда) КАК Команда,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур) КАК Тур,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК Регистратор,
		            	  |	МАКСИМУМ(ТурнирнаяТаблица.Период) КАК Период
		            	  |ПОМЕСТИТЬ Последнее
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |;
		            	  |
		            	  |////////////////////////////////////////////////////////////////////////////////
		            	  |ВЫБРАТЬ
		            	  |	ТурнирнаяТаблица.Команда КАК Команда
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последнее КАК Последнее
		            	  |		ПО ТурнирнаяТаблица.Период = Последнее.Период
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |	И (ТурнирнаяТаблица.Команда, Последнее.Тур) В
		            	  |			(ВЫБРАТЬ
		            	  |				ТурнирнаяТаблица.Команда,
		            	  |				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур)
		            	  |			ИЗ
		            	  |				РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |			ГДЕ
		            	  |				ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |			СГРУППИРОВАТЬ ПО
		            	  |				ТурнирнаяТаблица.Команда)
		            	  |	И Последнее.Регистратор >= Последнее.Команда
		            	  |
		            	  |СГРУППИРОВАТЬ ПО
		            	  |	ТурнирнаяТаблица.Команда
		            	  |
		            	  |ИМЕЮЩИЕ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) = 1";
	КонецЕсли;
	Запрос.УстановитьПараметр("Чемпионат",		Ссылка);
	Возврат НЕ Запрос.Выполнить().Пустой();
КонецФункции

Функция Чемпион() Экспорт
	Если ДатаОкончания > ТекущаяДатаСеанса()Тогда Возврат Справочники.Команды.ПустаяСсылка(); КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	ТурнирнаяТаблица.Команда КАК Команда,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) КАК КоличествоОчков,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) - СУММА(Игра.КоличествоГолов) КАК Разница,
	                      |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК Забито
	                      |ИЗ
	                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Игра
	                      |		ПО ТурнирнаяТаблица.Регистратор = Игра.Регистратор
	                      |			И ТурнирнаяТаблица.НомерСтроки <> Игра.НомерСтроки
	                      |ГДЕ
	                      |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ТурнирнаяТаблица.Команда
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	КоличествоОчков УБЫВ,
	                      |	Разница УБЫВ,
	                      |	Забито УБЫВ");
	Если Владелец.ОлимпийскаяСистема Тогда
		Запрос.Текст	= "ВЫБРАТЬ
		            	  |	МАКСИМУМ(ТурнирнаяТаблица.Период) КАК Период,
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур) КАК Тур
		            	  |ПОМЕСТИТЬ Последнее
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |
		            	  |ИМЕЮЩИЕ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Команда)
		            	  |;
		            	  |
		            	  |////////////////////////////////////////////////////////////////////////////////
		            	  |ВЫБРАТЬ
		            	  |	ТурнирнаяТаблица.Команда КАК Команда
		            	  |ИЗ
		            	  |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Последнее КАК Последнее
		            	  |		ПО ТурнирнаяТаблица.Период = Последнее.Период
		            	  |ГДЕ
		            	  |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |	И (ТурнирнаяТаблица.Команда, Последнее.Тур) В
		            	  |			(ВЫБРАТЬ
		            	  |				ТурнирнаяТаблица.Команда,
		            	  |				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Тур)
		            	  |			ИЗ
		            	  |				РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		            	  |			ГДЕ
		            	  |				ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		            	  |			СГРУППИРОВАТЬ ПО
		            	  |				ТурнирнаяТаблица.Команда)
		            	  |
		            	  |СГРУППИРОВАТЬ ПО
		            	  |	ТурнирнаяТаблица.Команда
		            	  |
		            	  |ИМЕЮЩИЕ
		            	  |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) = 1
		            	  |
		            	  |УПОРЯДОЧИТЬ ПО
		            	  |	СУММА(ТурнирнаяТаблица.КоличествоОчков) УБЫВ";
	КонецЕсли;
	Запрос.УстановитьПараметр("Чемпионат",		Ссылка);
	Ризалт	= Запрос.Выполнить();
	Если Ризалт.Пустой() Тогда Возврат Справочники.Команды.ПустаяСсылка(); КонецЕсли;
	Выборка	= Ризалт.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Команда;
КонецФункции
