
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Команды") Тогда
		ПодключитьОбработчикОжидания("КомандыЛигиЗагрузить", 0.1, Истина);
	ИначеЕсли ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты") И Параметр = Объект.Ссылка Тогда
		Прочитать();
		ПодключитьОбработчикОжидания("ВидимостьДоступность", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РедактированиеУстановить()
	РедактированиеДоступно = Истина;
	Элементы.ФормаРазрешитьРедактирование.Пометка = Истина;
КонецПроцедуры

&НаКлиенте
Процедура КомандыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Объект.Команды.Количество() >= ЛигаКоличествоКоманд(Объект.Владелец) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЛигаКоличествоКоманд(Лига)
	Возврат СерверныйФункции.ЛигаКоличествоКоманд(Лига);
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьИстория(Ссылка, Команда=Неопределено)
	Возврат СерверныйФункции.ЕстьИстория(Ссылка, Команда);
КонецФункции

&НаКлиенте
Процедура КомандыПередУдалением(Элемент, Отказ)
	Если ЕстьИстория(Объект.Ссылка, Элемент.ТекущиеДанные.Команда) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИзображениеУстановить()
	Изображение	= СерверныйФункции.ИзображениеПолучить(?(ЗначениеЗаполнено(Чемпион), Чемпион, Объект.Владелец));
	Возврат ЗначениеЗаполнено(Изображение);
КонецФункции

&НаСервере
Функция ЧемпионУстановить()
	ОбщегоНазначения.УстановитьЗначение(Чемпион, СерверныйФункции.Чемпион(Объект.Ссылка));
	Возврат ЗначениеЗаполнено(Чемпион);
КонецФункции

&НаСервере
Процедура ВидимостьДоступностьУстановить()
	Если ЕстьИстория(Объект.Ссылка) Тогда
		Элементы.Владелец.ТолькоПросмотр	= НЕ РедактированиеДоступно;
		Элементы.Чемпион.Видимость			= ЧемпионУстановить();
		//Если Элементы.Чемпион.Видимость Тогда
		//	Элементы.Команды.ТолькоПросмотр			= Истина;
		//	Элементы.КомандаИмпортКоманд.Доступность= Ложь;
		//Иначе
			Элементы.Команды.ТолькоПросмотр			= НЕ РедактированиеДоступно;
		//	Элементы.КомандаИмпортКоманд.Доступность= РедактированиеДоступно;
		//КонецЕсли;
		//Элементы.КомандыУпорядочить.Доступность	= РедактированиеДоступно;
		Элементы.КомандыГруппаДополнительно.Доступность = РедактированиеДоступно;
	Иначе
		Элементы.Владелец.ТолькоПросмотр	= Ложь;
		Элементы.Чемпион.Видимость			= Ложь;
		Элементы.Команды.ТолькоПросмотр		= Ложь;
		Элементы.КомандыГруппаДополнительно.Доступность = Истина;
	КонецЕсли;
	Элементы.Изображение.Видимость	= ИзображениеУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьДоступность() Экспорт
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура КомандыЛигиЗагрузить() Экспорт
	КомандыЛигиЗагрузитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандыЛигиЗагрузитьНаСервере()
	Если ЗначениеЗаполнено(Объект.Владелец.Страна) Тогда
		КомандыЛиги.ЗагрузитьЗначения(СерверныйФункции.КомандыСтраны(Объект.Владелец));
	ИначеЕсли ЗначениеЗаполнено(Объект.Владелец) Тогда
		Запрос	= Новый Запрос("ВЫБРАТЬ
		      	               |	Команды.Ссылка КАК Ссылка
		      	               |ИЗ
		      	               |	Справочник.Команды КАК Команды
		      	               |ГДЕ
		      	               |	Команды.ПометкаУдаления = ЛОЖЬ
		      	               |
		      	               |УПОРЯДОЧИТЬ ПО
		      	               |	Ссылка
		      	               |АВТОУПОРЯДОЧИВАНИЕ");
		КомандыЛиги.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
		
	ИначеЕсли КомандыЛиги.Количество() > 0 Тогда
		КомандыЛиги.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	Страна	= Страна();
	КомандыЛигиЗагрузитьНаСервере();
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент=Неопределено)
	ВладелецПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриОткрытииНаСервере(Отказ)
	Если НЕ ЕстьИстория(Объект.Ссылка) Тогда
		РедактированиеУстановить();
	КонецЕсли;
	ВладелецПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПриОткрытииНаСервере(Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КомандаИмпортКоманд(Команда=Неопределено)
	УРЛ = УРЛ();
	Если Команда = Неопределено Или Объект.Команды.Количество() = 0 Тогда
		ПоказатьВводСтроки(Новый ОписаниеОповещения("Импорт", ЭтотОбъект), УРЛ, "Импорт команд");
	Иначе
		ПоказатьВопрос(Новый ОписаниеОповещения("ВопросИмпортДо", ЭтотОбъект), "Заменить текущий список?", РежимДиалогаВопрос.ДаНетОтмена, 60, КодВозвратаДиалога.Отмена, "Команды");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросИмпортДо(КодОтвета, ПараметрКоманды) Экспорт
	Если КодОтвета = КодВозвратаДиалога.Да Тогда
		КомандаОчиститьНаСервере();
		КомандаИмпортКоманд();
	ИначеЕсли КодОтвета = КодВозвратаДиалога.Нет Тогда
		КомандаИмпортКоманд();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Импорт(Гиперссылка, ПараметрКоманды) Экспорт
	Если ИмпортНаСервере(Гиперссылка) Тогда
		//
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИмпортНаСервере(Гиперссылка)
	Если ПустаяСтрока(Гиперссылка) Тогда Возврат Ложь; КонецЕсли;
	
	Итого	= 0;
	Старт	= Ложь;
	
	КоличествоКоманд	= ЛигаКоличествоКоманд(Объект.Владелец);
	сИгра	= Новый Структура("Команда");
	Если ЭтоУРЛ(Гиперссылка) Тогда
		мСлова	= СерверныйФункции.ПолучитьЭлементыПоИмени(Гиперссылка);
	Иначе
		мСлова	= Новый Массив;
		Исключения	= Разделить("матч,клуб,мячи,очки", ",");
		Для Каждого Тимз Из Разделить(Гиперссылка, "¶") Цикл
			Если СтрНайти(Тимз, Символ(9)) = 0 Тогда
				Слова = Разделить(Тимз, "-", Истина);
			Иначе
				Слова = Разделить(Тимз, Символ(9), Истина);
			КонецЕсли;
			Для Каждого Слово Из Слова Цикл
				Если СтрНайти(Слово, ":") > 0 Тогда
					//
				ИначеЕсли СтрДлина(Слово) < 3 Тогда
					//
				ИначеЕсли Исключения.Найти(НРег(Слово)) <> Неопределено Тогда
					//
				ИначеЕсли СтрокаКакЧисло(Слово) > 0 Тогда
					//
				Иначе
					мСлова.Добавить(Слово);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Старт	= Истина;
	КонецЕсли;
	Виктимз	= Новый Массив;
	Для Каждого Слово Из мСлова Цикл
		Если Объект.Команды.Количество() >= КоличествоКоманд Тогда		// финито
			Прервать;
		ИначеЕсли СтрНайти(Слово, Символ(9)) > 0 Тогда
		ИначеЕсли СтрСравнить(Прав(Слово, 1) , ".") = 0 Тогда
		ИначеЕсли СтрЧислоСтрок(Слово) > 1 Тогда
		//ИначеЕсли СтрокаКакМесяц(Слово) > 0 Тогда
		ИначеЕсли СтрокаКакЧисло(Слово) > 0 Тогда
			Если НЕ Старт Тогда
				Старт = Истина;
			КонецЕсли;
		ИначеЕсли Старт И СтрДлина(Слово) < 3 Тогда
		ИначеЕсли Старт И ЭтоСчетНаверное(Слово) Тогда
		ИначеЕсли Старт Тогда //НЕ ЗначениеЗаполнено(сИгра.Команда) Тогда
			сИгра.Команда	= КомандаНайти(Слово);
			Если НЕ ЗначениеЗаполнено(сИгра.Команда) Тогда
				Если мСлова.Количество() = КоличествоКоманд Тогда
					сИгра.Команда = СерверныйФункции.КомандыСоздать(Слово);
				ИначеЕсли СтрокаНачинаетсяЧислом(Слово) Тогда
					//
				Иначе
					Виктимз.Добавить(Слово);
				КонецЕсли;
			КонецЕсли;
			Если ЗначениеЗаполнено(сИгра.Команда) Тогда
				ЗаполнитьЗначенияСвойств(Объект.Команды.Добавить(), сИгра);
				сИгра.Команда	= Неопределено;
				Итого			= Итого + 1;
			//Иначе
			//	Сообщить(Слово);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если Объект.Команды.Количество() < КоличествоКоманд Тогда
		Для Каждого Слово Из Виктимз Цикл
			//сИгра.Команда = СерверныйФункции.КомандыСоздать(Слово);
			//ЗаполнитьЗначенияСвойств(Объект.Команды.Добавить(), сИгра);
			//Итого			= Итого + 1;
			Сообщить(Слово);
		КонецЦикла;
	КонецЕсли;
	Возврат (Итого > 0);
КонецФункции

&НаСервере
Функция КомандаНайти(Слово)
	Возврат СерверныйФункции.КомандаНайти(Слово, ?(ЗначениеЗаполнено(Страна), Страна, Неопределено));
КонецФункции

&НаСервере
Функция ЭтоСчетНаверное(Слово)
	Ответ = Ложь;
	Если СтрокаСодержитЧисло(Слово) И СтрНайти(Слово, ":") > 1 Тогда
		Попытка
			Ответ	= (ТипЗнч(Вычислить(Слово)) = Тип("Число"));
		Исключение КонецПопытки;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция Страна()
	Возврат Объект.Владелец.Страна;
КонецФункции

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	Если Объект.Ссылка.Пустая()
	И ЗначениеЗаполнено(Объект.ДатаНачала)
	Тогда
		Объект.ДатаОкончания = ДатаОкончания(Объект.ДатаНачала);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если Объект.Ссылка.Пустая() Тогда
		Слова = Разделить(Объект.Наименование);
		Для Каждого Слово Из Слова Цикл
			Если СтрокаСодержитЧисло(Слово) Тогда
				Объект.ДатаНачала		= СтрокаКакДата(Лев(Слово, 4) + "0801");
				Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
					Объект.ДатаОкончания	= ДатаОкончания(Объект.ДатаНачала);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ДатаОкончания(Дата)
	Возврат КонецМесяца(ДобавитьМесяц(Дата, 9));
КонецФункции

&НаКлиенте
Процедура Упорядочить(Команда)
	УпорядочитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура УпорядочитьНаСервере()
	РеквизитФормыВЗначение("Объект").КомандыУпорядочить(Объект.Команды);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ВидимостьДоступностьУстановить();
	ИнформацияОбновить();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИтогиПереписатьНаСервере(Ссылка)
	РегистрыСведений.Итоги.Обновить(Ссылка, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ИтогиПереписать(Команда)
	ИтогиПереписатьНаСервере(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	РедактированиеУстановить();
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаСервере
Процедура КомандаОчиститьНаСервере()
	Объект.Команды.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчистить(Команда)
	КомандаОчиститьНаСервере();
КонецПроцедуры

&НаСервере
Функция УРЛ(Команда=Неопределено)
	Если Команда = Неопределено Тогда
		Ответ = СерверныйФункции.КомментарийКакУРЛ(Объект.Комментарий);
		Если ПустаяСтрока(Ответ) Тогда
			Ответ = СерверныйФункции.КомментарийКакУРЛ(Объект.Владелец.Комментарий);
		КонецЕсли;
		УРЛ	= ?(ПустаяСтрока(Ответ), ?(СтрНайти2(Объект.Наименование, "англ") = 0, "", СтрШаблон("https://football.kulichki.net/england/%1/", ФорматЧГ(Объект.ДатаОкончания))), ?(СтрНайти(Ответ, Прав(Объект.Наименование, 4)) = 0, Ответ + СтрШаблон("/%1/", Прав(Объект.Наименование, 4)), Ответ));
	Иначе
		УРЛ	= СерверныйФункции.КомментарийКакУРЛ(Команда.Комментарий);
		Если СтрокаСодержитЧисло(УРЛ) Тогда
			Чемпионат	= СерверныйФункции.ЧемпионатЛигиПоследний(Команда);
			Если СтрНайти(УРЛ, "%1") = 0 Тогда
				//Слова	= Разделить(УРЛ, "/");
			Иначе
				Если ЗначениеЗаполнено(Чемпионат.ДатаОкончания) Тогда
					УРЛ	= СтрШаблон(УРЛ, ФорматЧГ(Чемпионат.ДатаОкончания));
				Иначе
					УРЛ	= СтрШаблон(УРЛ, ФорматЧГ(ТекущаяДатаСеанса()));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат УРЛ;
КонецФункции

&НаСервере
Процедура ИмпортИгроковНаСервере()
	Для Каждого ТекСтрока Из Объект.Команды Цикл
		УРЛ = УРЛ(ТекСтрока.Команда);
		Если ЭтоУРЛ(УРЛ) Тогда
			Ответ = ?(Год(ТекущаяДатаСеанса()) >= 2022, СерверныйФункции.ИмпортИгроковНаСервере(УРЛ, ТекСтрока.Команда), СерверныйФункции.ИмпортИгроковНаСервере2(УРЛ, ТекСтрока.Команда));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ИмпортИгроков(Команда)
	ИмпортИгроковНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.Команды.ТолькоПросмотр Тогда
		НаКлиенте.Показать(Элемент.ТекущиеДанные.Команда, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИнформацияОбновить()
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) КАК СыграноМатчей,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков >= 2
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК ПобедХозяев,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.КоличествоОчков = Противники.КоличествоОчков
	      	               |				ТОГДА 1
	      	               |			ИНАЧЕ 0
	      	               |		КОНЕЦ) КАК Ничьих,
	      	               |	СУММА(ТурнирнаяТаблица.КоличествоГолов + Противники.КоличествоГолов) КАК ЗабитоГолов,
	      	               |	СУММА(ТурнирнаяТаблица.КоличествоГолов) КАК ЗабитоГоловХозяевами,
	      	               |	СУММА(ТурнирнаяТаблица.Регистратор.КоличествоЗрителей) КАК ЗрителейВсего,
	      	               |	СУММА(ВЫБОР
	      	               |			КОГДА ТурнирнаяТаблица.Регистратор.КоличествоЗрителей = 0
	      	               |				ТОГДА 0
	      	               |			ИНАЧЕ 1
	      	               |		КОНЕЦ) КАК МатчейСоЗрителями
	      	               |ИЗ
	      	               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТурнирнаяТаблица КАК Противники
	      	               |		ПО ТурнирнаяТаблица.Регистратор = Противники.Регистратор
	      	               |ГДЕ
	      	               |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
	      	               |	И ТурнирнаяТаблица.НомерСтроки = 1
	      	               |	И Противники.НомерСтроки = 2
	      	               |
	      	               |ИМЕЮЩИЕ
	      	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТурнирнаяТаблица.Регистратор) >= 1");
	Запрос.УстановитьПараметр("Чемпионат",			Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СыграноМатчей				= Выборка.СыграноМатчей;
		Если Объект.Владелец.ОлимпийскаяСистема Тогда
			Элементы.ГруппаОлимпийскаяСистема.Видимость	= Ложь;
			Элементы.ГруппаЗабитоГолов.Видимость		= Ложь;
			Элементы.ГруппаГоловВСреднемЗаИгру.Видимость= Ложь;
		Иначе
			ПобедХозяев					= Выборка.ПобедХозяев;
			Ничьих						= Выборка.Ничьих;
			ПобедГостей					= Выборка.СыграноМатчей - Выборка.ПобедХозяев - Выборка.Ничьих;
			
			ЗабитоГоловХозяевами		= Выборка.ЗабитоГоловХозяевами;
			ЗабитоГоловГостями			= Выборка.ЗабитоГолов - Выборка.ЗабитоГоловХозяевами;
			
			ГоловВСреднемЗаИгруХозяева	= Окр(Выборка.ЗабитоГоловХозяевами / Выборка.СыграноМатчей, 1);
			ГоловВСреднемЗаИгруГости	= Окр((Выборка.ЗабитоГолов - Выборка.ЗабитоГоловХозяевами) / Выборка.СыграноМатчей, 1);
		КонецЕсли;
		ЗабитоГолов					= Выборка.ЗабитоГолов;
		ГоловВСреднемЗаИгру			= Окр(Выборка.ЗабитоГолов / Выборка.СыграноМатчей, 1);
		ЗрителейВсего				= Выборка.ЗрителейВсего;
		ЗрителейВСреднемЗаИгру		= ?(Выборка.МатчейСоЗрителями = 0, "", Окр(Выборка.ЗрителейВсего / Выборка.МатчейСоЗрителями, 0));
		
		Запрос.УстановитьПараметр("МояКоманда",				МояКоманда());
		Запрос.Текст = "ВЫБРАТЬ
		               |	ТурнирнаяТаблица.Период КАК Период,
		               |	ТурнирнаяТаблица.Регистратор КАК Регистратор,
		               |	ТурнирнаяТаблица.НомерСтроки КАК НомерСтроки,
		               |	ТурнирнаяТаблица.Команда = &МояКоманда КАК МояКоманда,
		               |	ТурнирнаяТаблица.КоличествоГолов КАК КоличествоГолов
		               |ПОМЕСТИТЬ ТурнирнаяТаблица
		               |ИЗ
		               |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |ГДЕ
		               |	ТурнирнаяТаблица.Тур.Владелец = &Чемпионат
		               |	И ТурнирнаяТаблица.Регистратор ССЫЛКА Документ.Матч
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ТурнирнаяТаблица.Регистратор КАК Регистратор
		               |ИЗ
		               |	ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК Противники
		               |		ПО ТурнирнаяТаблица.Регистратор = Противники.Регистратор
		               |ГДЕ
		               |	ТурнирнаяТаблица.НомерСтроки = 1
		               |	И Противники.НомерСтроки = 2
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТурнирнаяТаблица.КоличествоГолов + Противники.КоличествоГолов УБЫВ,
		               |	ТурнирнаяТаблица.МояКоманда
		               |		ИЛИ Противники.МояКоманда УБЫВ,
		               |	ТурнирнаяТаблица.Период УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ТурнирнаяТаблица.Регистратор КАК Регистратор
		               |ИЗ
		               |	ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК Противники
		               |		ПО ТурнирнаяТаблица.Регистратор = Противники.Регистратор
		               |ГДЕ
		               |	ТурнирнаяТаблица.НомерСтроки = 1
		               |	И Противники.НомерСтроки = 2
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	ТурнирнаяТаблица.КоличествоГолов УБЫВ,
		               |	Противники.КоличествоГолов,
		               |	ТурнирнаяТаблица.МояКоманда УБЫВ,
		               |	ТурнирнаяТаблица.Период УБЫВ
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ТурнирнаяТаблица.Регистратор КАК Регистратор
		               |ИЗ
		               |	ТурнирнаяТаблица КАК ТурнирнаяТаблица
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТурнирнаяТаблица КАК Противники
		               |		ПО ТурнирнаяТаблица.Регистратор = Противники.Регистратор
		               |ГДЕ
		               |	ТурнирнаяТаблица.НомерСтроки = 1
		               |	И Противники.НомерСтроки = 2
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Противники.КоличествоГолов УБЫВ,
		               |	ТурнирнаяТаблица.КоличествоГолов,
		               |	Противники.МояКоманда УБЫВ,
		               |	ТурнирнаяТаблица.Период УБЫВ";
		Выборка = Запрос.ВыполнитьПакет();
		СамыйКрупныйСчет			= ПостОбработка(Выборка.Получить(1));
		СамаяКрупнаяПобедаДомашняя	= ПостОбработка(Выборка.Получить(2));
		СамаяКрупнаяПобедаГостевая	= ПостОбработка(Выборка.Получить(3));
	Иначе
		СыграноМатчей				= "0";
		ПобедХозяев					= "0";
		Ничьих						= "0";
		ПобедГостей					= "0";
		ЗабитоГолов					= "0";
		ЗабитоГоловХозяевами		= "0";
		ЗабитоГоловГостями			= "0";
		ГоловВСреднемЗаИгру			= "0";
		ГоловВСреднемЗаИгруХозяева	= "0";
		ГоловВСреднемЗаИгруГости	= "0";
		ЗрителейВсего				= "0";
		ЗрителейВСреднемЗаИгру		= "0";
		СамыйКрупныйСчет			= Неопределено;
		СамаяКрупнаяПобедаДомашняя	= Неопределено;
		СамаяКрупнаяПобедаГостевая	= Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПостОбработка(РезультатЗапроса)
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Регистратор;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

&НаСервереБезКонтекста
Функция МояКоманда()
	Возврат СерверныйФункции.МояКомандаПолучить();
КонецФункции

&НаКлиенте
Процедура СсылкаНажатие(Элемент, СтандартнаяОбработка)
	Если Элемент.Имя = "СамыйКрупныйСчет" Тогда
		НаКлиенте.Показать(СамыйКрупныйСчет, СтандартнаяОбработка);
	ИначеЕсли Элемент.Имя = "СамаяКрупнаяПобедаДомашняя" Тогда
		НаКлиенте.Показать(СамаяКрупнаяПобедаДомашняя, СтандартнаяОбработка);
	ИначеЕсли Элемент.Имя = "СамаяКрупнаяПобедаГостевая" Тогда
		НаКлиенте.Показать(СамаяКрупнаяПобедаГостевая, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры
