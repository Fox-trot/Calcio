
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Команды") Тогда
		КомандыЛигиЗагрузить();
	ИначеЕсли Параметр = Объект.Ссылка Тогда
		ВидимостьДоступностьУстановить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Объект.Команды.Количество() = ЛигаКоличествоКоманд(Объект.Владелец) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЛигаКоличествоКоманд(Лига)
	Возврат СерверныйФункции.ЛигаКоличествоКоманд(Лига);
КонецФункции

&НаСервере
Функция ЕстьИстория(Ссылка, Команда=Неопределено)
	Возврат СерверныйФункции.ЕстьИстория(Ссылка, Команда);
КонецФункции

&НаКлиенте
Процедура КомандыПередУдалением(Элемент, Отказ)
	Если ЕстьИстория(Объект.Ссылка, Элемент.ТекущиеДанные.Команда) Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИзображениеУстановить()
	Изображение	= СерверныйФункции.ИзображениеПолучить(?(ЗначениеЗаполнено(Чемпион), Чемпион, Объект.Владелец));
	Возврат ЗначениеЗаполнено(Изображение);
КонецФункции

&НаСервере
Функция ЧемпионУстановить()
	Если НЕ ЗначениеЗаполнено(Чемпион) И Объект.ДатаОкончания < ТекущаяДатаСеанса() Тогда
		Чемпион	= РеквизитФормыВЗначение("Объект").Чемпион();
	КонецЕсли;
	Возврат ЗначениеЗаполнено(Чемпион);
КонецФункции

&НаСервере
Процедура ВидимостьДоступностьУстановить()
	Элементы.Чемпион.Видимость					= Ложь;
	Если ЕстьИстория(Объект.Ссылка) Тогда
		Элементы.Владелец.ТолькоПросмотр			= Истина;
		Если ИгрыВсе() Тогда
			Элементы.Команды.ТолькоПросмотр			= Истина;
			Элементы.КомандаИмпортКоманд.Видимость	= Ложь;
			Элементы.Чемпион.Видимость				= ЧемпионУстановить();
		КонецЕсли;
	КонецЕсли;
	Элементы.Изображение.Видимость	= ИзображениеУстановить();
КонецПроцедуры

&НаСервере
Функция ИгрыВсе()
	Возврат РеквизитФормыВЗначение("Объект").ИгрыВсе();
КонецФункции

&НаСервере
Процедура КомандыЛигиЗагрузить()
	Если ЗначениеЗаполнено(Объект.Владелец.Страна) Тогда
		КомандыЛиги.ЗагрузитьЗначения(СерверныйФункции.КомандыСтраны(Объект.Владелец));
	ИначеЕсли ЗначениеЗаполнено(Объект.Владелец) Тогда
		Запрос	= Новый Запрос("ВЫБРАТЬ
		      	               |	Команды.Ссылка КАК Ссылка
		      	               |ИЗ
		      	               |	Справочник.Команды КАК Команды
		      	               |ГДЕ
		      	               |	Команды.ПометкаУдаления = ЛОЖЬ
		      	               |
		      	               |УПОРЯДОЧИТЬ ПО
		      	               |	Ссылка
		      	               |АВТОУПОРЯДОЧИВАНИЕ");
		КомандыЛиги.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0));
	ИначеЕсли КомандыЛиги.Количество() > 0 Тогда
		КомандыЛиги.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент=Неопределено)
	КомандыЛигиЗагрузить();
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВладелецПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура КомандаИмпортКоманд(Команда=Неопределено)
	УРЛ = УРЛ();
	Если Команда = Неопределено Или Объект.Команды.Количество() = 0 Тогда
		ПоказатьВводСтроки(Новый ОписаниеОповещения("Импорт", ЭтотОбъект), УРЛ, "Импорт команд");
	Иначе
		ПоказатьВопрос(Новый ОписаниеОповещения("ВопросИмпортДо", ЭтотОбъект), "Заменить текущий список?", РежимДиалогаВопрос.ОКОтмена, 60, КодВозвратаДиалога.Отмена, "Команды");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция УРЛ()
	Ответ = СерверныйФункции.КомментарийКакУРЛ(Объект.Комментарий);
	Если ПустаяСтрока(Ответ) Тогда
		Ответ = СерверныйФункции.КомментарийКакУРЛ(Объект.Владелец.Комментарий);
	КонецЕсли;
	Возврат ?(ПустаяСтрока(Ответ), ?(СтрНайти2(Объект.Наименование, "англ") = 0, "", СтрШаблон("https://football.kulichki.net/england/%1/", ФорматЧГ(Объект.ДатаОкончания))), Ответ);
КонецФункции

&НаКлиенте
Процедура ВопросИмпортДо(КодОтвета, ПараметрКоманды) Экспорт
	Если КодОтвета = КодВозвратаДиалога.ОК Тогда
		КомандаИмпортКоманд();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Импорт(Гиперссылка, ПараметрКоманды) Экспорт
	Если ИмпортНаСервере(Гиперссылка) Тогда
		Если ПустаяСтрока(Объект.Комментарий) Тогда
			Объект.Комментарий = Гиперссылка;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИмпортНаСервере(Гиперссылка)
	Если ПустаяСтрока(Гиперссылка) Тогда Возврат Ложь; КонецЕсли;
	
	Итого	= 0;
	Старт	= Ложь;
	
	Объект.Команды.Очистить();
	КоличествоКоманд	= ЛигаКоличествоКоманд(Объект.Владелец);
	сИгра	= Новый Структура("Команда");
	Если ЭтоУРЛ(Гиперссылка) Тогда
		мСлова	= СерверныйФункции.ПолучитьЭлементыПоИмени(Гиперссылка);
	Иначе
		мСлова	= Новый Массив;
		Для Каждого Тимз Из Разделить(Гиперссылка, "¶") Цикл
			Для Каждого Слово Из Разделить(Тимз, "-", Истина) Цикл
				Если СтрНайти(Слово, ":") = 0
				И СтрСравнить(Слово, "матч") <> 0
				Тогда
					мСлова.Добавить(Слово);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Старт	= Истина;
	КонецЕсли;
	Для Каждого Слово Из мСлова Цикл
		Если Объект.Команды.Количество() >= КоличествоКоманд Тогда		// финито
			Прервать;
		ИначеЕсли СтрСравнить(Прав(Слово, 1) , ".") = 0 Тогда
		ИначеЕсли СтрЧислоСтрок(Слово) > 1 Тогда
		//ИначеЕсли СтрНайти("клуб,мячи,очки", НРег(Слово)) > 0 И Объект.Команды.Количество() = 0 Тогда
		//ИначеЕсли СтрокаКакМесяц(Слово) > 0 Тогда
		ИначеЕсли СтрокаКакЧисло(Слово) > 0 Тогда
			Если НЕ Старт Тогда
				Старт = Истина;
			КонецЕсли;
		ИначеЕсли Старт И СтрДлина(Слово) < 3 Тогда
		ИначеЕсли Старт И ЭтоСчетНаверное(Слово) Тогда
		ИначеЕсли Старт Тогда //НЕ ЗначениеЗаполнено(сИгра.Команда) Тогда
			сИгра.Команда	= КомандаНайти(Слово);
			Если НЕ ЗначениеЗаполнено(сИгра.Команда) Тогда
				сИгра.Команда	= СерверныйФункции.КомандыСоздать(Слово);
			КонецЕсли;
			Если ЗначениеЗаполнено(сИгра.Команда) Тогда
				ЗаполнитьЗначенияСвойств(Объект.Команды.Добавить(), сИгра);
				сИгра.Команда	= Неопределено;
				Итого			= Итого + 1;
			//Иначе
			//	Сообщить(Слово);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат (Итого > 0);
КонецФункции

&НаСервере
Функция КомандаНайти(Слово)
	Страна	= Страна();
	Возврат СерверныйФункции.КомандаНайти(Слово, ?(ЗначениеЗаполнено(Страна), Страна, Неопределено));
КонецФункции

&НаСервере
Функция ЭтоСчетНаверное(Слово)
	Ответ = Ложь;
	Попытка
		Ответ	= (ТипЗнч(Вычислить(Слово)) = Тип("Число"));
	Исключение КонецПопытки;
	Возврат Ответ;
КонецФункции

&НаСервере
Функция Страна()
	Возврат Объект.Владелец.Страна;
КонецФункции

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	Если Объект.Ссылка.Пустая()
	И ЗначениеЗаполнено(Объект.ДатаНачала)
	Тогда
		Объект.ДатаОкончания = ДатаОкончания(Объект.ДатаНачала);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	Если Объект.Ссылка.Пустая() Тогда
		Слова = Разделить(Объект.Наименование);
		Для Каждого Слово Из Слова Цикл
			Если СтрокаСодержитЧисло(Слово) Тогда
				Объект.ДатаНачала		= СтрокаКакДата(Лев(Слово, 4) + "0801");
				Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
					Объект.ДатаОкончания	= ДатаОкончания(Объект.ДатаНачала);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ДатаОкончания(Дата)
	Возврат КонецМесяца(ДобавитьМесяц(Дата, 9));
КонецФункции

&НаКлиенте
Процедура Упорядочить(Команда)
	УпорядочитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура УпорядочитьНаСервере()
	Если Модифицированность Тогда
		Объект.Команды.Сортировать("Команда");
	Иначе
		РеквизитФормыВЗначение("Объект").КомандыУпорядочить(Объект.Команды);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	КомандыЛигиЗагрузить();
КонецПроцедуры
