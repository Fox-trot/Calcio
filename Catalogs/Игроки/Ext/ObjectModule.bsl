
Процедура ПриКопировании(ОбъектКопирования)
	ДатаРождения	= Дата(1,1,1);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Амплуа") Тогда
		Амплуа	= ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Страны") Тогда
		Страна	= ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Страна") Тогда
			Страна	= ДанныеЗаполнения.Страна;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Страна) Тогда
		Страна	= СтранаОпределить(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Функция СтранаОпределить(Параметр)
	Ответ	= Справочники.Страны.ПустаяСсылка();
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Команды")
	Или ТипЗнч(Параметр) = Тип("СправочникСсылка.Города")
	Или ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты")
	Или ТипЗнч(Параметр) = Тип("СправочникСсылка.Лига")
	Тогда
		Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	Города.Владелец КАК Ссылка
	      	               |ИЗ
	      	               |	Справочник.Команды КАК Команды
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Города КАК Города
	      	               |		ПО Команды.Город = Города.Ссылка
	      	               |ГДЕ
	      	               |	Команды.Ссылка = &Параметр
	      	               |
	      	               |ОБЪЕДИНИТЬ ВСЕ
	      	               |
	      	               |ВЫБРАТЬ
	      	               |	Города.Владелец
	      	               |ИЗ
	      	               |	Справочник.Города КАК Города
	      	               |ГДЕ
	      	               |	Города.Ссылка = &Параметр
	      	               |
	      	               |ОБЪЕДИНИТЬ ВСЕ
	      	               |
	      	               |ВЫБРАТЬ
	      	               |	Лига.Страна
	      	               |ИЗ
	      	               |	Справочник.Чемпионаты КАК Чемпионаты
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Лига КАК Лига
	      	               |		ПО Чемпионаты.Владелец = Лига.Ссылка
	      	               |ГДЕ
	      	               |	Чемпионаты.Ссылка = &Параметр
	      	               |
	      	               |ОБЪЕДИНИТЬ ВСЕ
	      	               |
	      	               |ВЫБРАТЬ
	      	               |	Лига.Страна
	      	               |ИЗ
	      	               |	Справочник.Лига КАК Лига
	      	               |ГДЕ
	      	               |	Лига.Ссылка = &Параметр
	      	               |
	      	               |УПОРЯДОЧИТЬ ПО
	      	               |	Ссылка УБЫВ");
		Запрос.УстановитьПараметр("Параметр",		Параметр);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Ответ	= Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	Возврат Ответ;
КонецФункции
