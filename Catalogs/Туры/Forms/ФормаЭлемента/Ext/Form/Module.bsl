
&НаКлиенте
Процедура КодПриИзменении(Элемент=Неопределено)
	Если Объект.Ссылка.Пустая() Или ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = НаименованиеПолучить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НаименованиеПолучить()
	Возврат РеквизитФормыВЗначение("Объект").НаименованиеПолучить();
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Объект.Ссылка.Пустая() Тогда
		Если Объект.Код = 0 Тогда
			Объект.Код = КодНовыйПолучить();
		КонецЕсли;
		КодПриИзменении();
		////Если НЕ Объект.ОлимпийскаяСистема И ВладелецОлимпийскаяСистема() Тогда
		////	Объект.ОлимпийскаяСистема = Истина;
		////ИначеЕсли Объект.ОлимпийскаяСистема И НЕ ВладелецОлимпийскаяСистема() Тогда
		////	Объект.ОлимпийскаяСистема = Ложь;
		////КонецЕсли;
	КонецЕсли;
	ДоступностьВидимостьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ДоступностьВидимостьУстановить()
	Если ЕстьМатчи(Объект.Ссылка) Тогда
		Элементы.Владелец.ТолькоПросмотр					= Истина;
		//Элементы.ОлимпийскаяСистема.Доступность			= Ложь;
	Иначе
	КонецЕсли;
	Элементы.ОлимпийскаяСистема.Видимость = ВладелецОлимпийскаяСистема();
КонецПроцедуры

&НаСервере
Функция КодНовыйПолучить()
	Возврат РеквизитФормыВЗначение("Объект").КодНовыйПолучить();
КонецФункции

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	Объект.Код = КодНовыйПолучить();
	КодПриИзменении();
КонецПроцедуры

&НаСервере
Функция ЕстьМатчи(Тур)
	Если Тур.Пустая() Тогда Возврат Ложь; КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕСТЬNULL(МАКСИМУМ(Матч.Дата), &Дата) КАК Дата
	                      |ИЗ
	                      |	Документ.Матч КАК Матч
	                      |ГДЕ
	                      |	Матч.Тур = &Тур");
	Запрос.УстановитьПараметр("Тур",	Тур);
	Запрос.УстановитьПараметр("Дата",	Дата(1, 1, 1));
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат (Выборка.Следующий() И Год(Выборка.Дата) > 1);
КонецФункции

&НаСервере
Функция ВладелецОлимпийскаяСистема()
	Возврат СерверныйФункции.ЛигаОлимпийскаяСистема(Объект.Владелец);
КонецФункции

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Объект.ДатаОкончания = Макс(Объект.ДатаОкончания, Объект.ДатаНачала);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ТипЗнч(Параметр) = Тип("СправочникСсылка.Чемпионаты")
	И Параметр = Объект.Владелец
	Тогда
		ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандаДатыИсправитьНаСервере()
	Если Объект.Ссылка.Пустая() Тогда Возврат; КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	МИНИМУМ(Матч.Дата) КАК ДатаНачала,
	                      |	МАКСИМУМ(Матч.Дата) КАК ДатаОкончания
	                      |ИЗ
	                      |	Документ.Матч КАК Матч
	                      |ГДЕ
	                      |	Матч.Тур = &Тур
	                      |	И Матч.ПометкаУдаления = ЛОЖЬ");
	Запрос.УстановитьПараметр("Тур",			Объект.Ссылка);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаНачала, Выборка.ДатаНачала);
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаОкончания, Выборка.ДатаОкончания);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаДатыИсправить(Команда)
	КомандаДатыИсправитьНаСервере();
КонецПроцедуры
