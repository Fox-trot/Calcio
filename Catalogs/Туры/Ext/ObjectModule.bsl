
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ДатаНачала > ДатаОкончания Тогда
		Отказ = Истина;
	ИначеЕсли ДатаНачала < Владелец.ДатаНачала Тогда
		Отказ = Истина;
	ИначеЕсли ДатаНачала > Владелец.ДатаОкончания Тогда
		Отказ = Истина;
	ИначеЕсли ДатаОкончания < Владелец.ДатаНачала Тогда
		Отказ = Истина;
	ИначеЕсли ДатаОкончания > Владелец.ДатаОкончания Тогда
		Отказ = Истина;
	//ИначеЕсли ОлимпийскаяСистема Тогда
	//	//
	//Иначе
	//	Запрос = Новый Запрос("ВЫБРАТЬ
	//	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Команды.Команда) КАК Количество
	//	                      |ИЗ
	//	                      |	Справочник.Чемпионаты.Команды КАК Команды
	//	                      |ГДЕ
	//	                      |	Команды.Ссылка = &Владелец");
	//	Запрос.УстановитьПараметр("Владелец", Владелец);
	//	Выборка = Запрос.Выполнить().Выбрать();
	//	Если Выборка.Следующий() И Справочники.Туры.КоличествоМакс(Выборка.Количество) < Код Тогда
	//		Отказ = Истина;
	//	КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Чемпионаты") Тогда
		Владелец			= ДанныеЗаполнения;
		ОлимпийскаяСистема	= ДанныеЗаполнения.Владелец.ОлимпийскаяСистема;
		
		ТурПоследний = ТурПоследний();
		Если ТурПоследний.Код = 0 Тогда
			Код	= 1;
		Иначе
			Если ОлимпийскаяСистема Тогда
				УстановитьНовыйКод(ТурПоследний.Код + 1);
				Наименование	= НаименованиеПолучить(ТурПоследний.Финал);
			ИначеЕсли ТурПоследний.Код < Справочники.Туры.КоличествоМакс(Владелец.Команды.Количество()) Тогда
				УстановитьНовыйКод(ТурПоследний.Код + 1);
				Наименование	= НаименованиеПолучить();
				ДатаНачала		= ДатаНачала(ТурПоследний.ДатаОкончания);
				ДатаОкончания	= ДатаНачала + Сутки();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Туры") Тогда
		ОбработкаЗаполнения(ДанныеЗаполнения.Владелец, ТекстЗаполнения, Ложь);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		Если ЗначениеЗаполнено(Владелец) Тогда
			ОлимпийскаяСистема	= Владелец.Владелец.ОлимпийскаяСистема;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ДатаНачала(Знач Дата)
	Дата	= Дата + Сутки();
	Пока ДеньНедели(Дата) <> 6 Цикл
		Дата	= Дата + Сутки();
	КонецЦикла;
	Возврат Дата;
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	Код				= КодНовыйПолучить();
	Наименование	= "";
	Комментарий		= "";
	ДатаНачала		= Владелец.ДатаНачала;
	ДатаОкончания	= Владелец.ДатаОкончания;
	
	Заполнить(ОбъектКопирования.Владелец);
КонецПроцедуры

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Новый Структура("Код", Префикс));
КонецПроцедуры

Функция КодНовыйПолучить() Экспорт
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		Владелец = СерверныйФункции.ЧемпионатЛигиПоследний(Владелец.Владелец);
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕСТЬNULL(МАКСИМУМ(Туры.Код), 0) КАК Код
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |ГДЕ
	                      |	Туры.Владелец = &Чемпионат
	                      |	И Туры.ПометкаУдаления = ЛОЖЬ");
	Запрос.УстановитьПараметр("Чемпионат",						Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат ?(Выборка.Следующий(), Выборка.Код + 1, 1);
КонецФункции

Функция НаименованиеПолучить(Знач Финал=Неопределено) Экспорт
	Если НЕ ОлимпийскаяСистема Тогда
		Возврат Строка(Макс(1, Код)) + "-й тур";
	КонецЕсли;
	
	Ответ = "Финал";
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЕСТЬNULL(Туры.Код, -1) КАК Код,
	                      |	ЧемпионатыКоманды.Количество КАК Количество
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		ЧемпионатыКоманды.Ссылка КАК Ссылка,
	                      |		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда) КАК Количество
	                      |	ИЗ
	                      |		Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |	ГДЕ
	                      |		ЧемпионатыКоманды.Ссылка = &Чемпионат
	                      |		И ЧемпионатыКоманды.Ссылка.Владелец.ОлимпийскаяСистема = ИСТИНА
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		ЧемпионатыКоманды.Ссылка) КАК ЧемпионатыКоманды
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	                      |		ПО ЧемпионатыКоманды.Ссылка = Туры.Владелец
	                      |			И (Туры.ОлимпийскаяСистема = ИСТИНА)
	                      |ГДЕ
	                      |	ЧемпионатыКоманды.Количество >= 2
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	0,
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЧемпионатыКоманды.Команда)
	                      |ИЗ
	                      |	Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	                      |ГДЕ
	                      |	ЧемпионатыКоманды.Ссылка = &Чемпионат
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Код УБЫВ");
	Запрос.УстановитьПараметр("Чемпионат",			Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий()
	И НЕ (Выборка.Код = 0 И Выборка.Количество = 2)
	Тогда
		Номер = Выборка.Количество / Pow(2, Выборка.Код + 1);
		Если Номер = 2 Тогда
			Ответ = "Полуфиналы";
		ИначеЕсли Номер >= 4 Тогда
			Ответ = СтрШаблон("1/%1 финала", Окр(Номер));
		КонецЕсли;
	КонецЕсли;
	Возврат Ответ;
КонецФункции

Функция ТурПоследний()
	Ответ = Новый Структура("Код,ДатаНачала,ДатаОкончания,Финал", 0, Дата(1,1,1), Дата(1,1,1));
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	                      |	Туры.Код КАК Код,
	                      |	Туры.ДатаНачала КАК ДатаНачала,
	                      |	Туры.ДатаОкончания КАК ДатаОкончания,
	                      |	Туры.Ссылка КАК Финал
	                      |ИЗ
	                      |	Справочник.Туры КАК Туры
	                      |ГДЕ
	                      |	Туры.Владелец = &Владелец
	                      |	И Туры.ПометкаУдаления = ЛОЖЬ
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ДатаОкончания УБЫВ,
	                      |	Код УБЫВ");
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Ответ, Выборка);
	КонецЕсли;
	Возврат Ответ;
КонецФункции

Процедура ДатыУстановить(Объект) Экспорт
	Если Объект.Ссылка.Пустая() Тогда Возврат; КонецЕсли;
	Запрос	= Новый Запрос("ВЫБРАТЬ
	      	               |	МИНИМУМ(Матч.Дата) КАК ДатаНачала,
	      	               |	МАКСИМУМ(Матч.Дата) КАК ДатаОкончания
	      	               |ИЗ
	      	               |	Документ.Матч КАК Матч
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Туры КАК Туры
	      	               |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Чемпионаты.Команды КАК ЧемпионатыКоманды
	      	               |			ПО Туры.Владелец = ЧемпионатыКоманды.Ссылка
	      	               |		ПО Матч.Тур = Туры.Ссылка
	      	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Матч.Команды КАК МатчКоманды
	      	               |		ПО Матч.Ссылка = МатчКоманды.Ссылка
	      	               |ГДЕ
	      	               |	Матч.Тур = &Тур
	      	               |	И Матч.ПометкаУдаления = ЛОЖЬ
	      	               |	И МатчКоманды.Команда = ЧемпионатыКоманды.Команда
	      	               |	И МатчКоманды.НомерСтроки = 2");
	Запрос.УстановитьПараметр("Тур",			Объект.Ссылка);
	Выборка	= Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ДатаНачала) Тогда
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаНачала, Выборка.ДатаНачала);
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаОкончания, Выборка.ДатаОкончания);
	Иначе
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаНачала, Владелец.ДатаНачала);
		ОбщегоНазначения.УстановитьЗначение(Объект.ДатаОкончания, Владелец.ДатаОкончания);
	КонецЕсли;
КонецПроцедуры
