
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Если НЕ Данные.ПометкаУдаления Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(Данные.Наименование + " %1", Данные.Владелец);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
    Поля.Добавить("ПометкаУдаления");
    Поля.Добавить("Наименование");
    Поля.Добавить("Владелец");
КонецПроцедуры

Функция ТурНайти(Код, Чемпионат)
	Возврат СерверныйФункции.ТурНайти(Код, Чемпионат);
КонецФункции

Функция Заполнить(Чемпионат) Экспорт
	Если НЕ ЗначениеЗаполнено(Чемпионат) Тогда
		Возврат Ложь;
	ИначеЕсли Чемпионат.Владелец.ОлимпийскаяСистема Тогда
		Возврат Ложь;
	КонецЕсли;
	Объект	= Неопределено;
	Для Итератор = 1 По Чемпионат.Команды.Количество() * 2 - 2 Цикл
		Тур		= ТурНайти(Итератор, Чемпионат);
		Если ЗначениеЗаполнено(Тур) Тогда
			Объект	= Тур.ПолучитьОбъект();
			Если Тур.ПометкаУдаления Тогда
				Объект.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			ОбщегоНазначения.УстановитьЗначение(Объект.ДатаНачала, Чемпионат.ДатаНачала);
			ОбщегоНазначения.УстановитьЗначение(Объект.ДатаОкончания, Чемпионат.ДатаОкончания);
			Если Объект.Модифицированность() Тогда
				Объект.Записать();
			КонецЕсли;
		Иначе
			Объект	= Справочники.Туры.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(Объект, Новый Структура("Владелец,ДатаНачала,ДатаОкончания", Чемпионат, Чемпионат.ДатаНачала, Чемпионат.ДатаОкончания));
			Объект.Код			= Итератор;
			Объект.Наименование	= Объект.НаименованиеПолучить();
			Объект.Записать();
		КонецЕсли;
	КонецЦикла;
	Возврат (Объект <> Неопределено);
КонецФункции

Функция ДатыУстановить(Ссылка) Экспорт
	Ответ = Ложь;
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Туры") Тогда
		Попытка
			Объект	= Ссылка.ПолучитьОбъект();
			Объект.ДатыУстановить(Объект);
			Объект.Записать();
			
			Ответ = Истина;
		Исключение
			СерверныйФункции.Логировать("Справочники.Туры.ДатыУстановить", ОписаниеОшибки(), Ссылка);
		КонецПопытки;
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.Чемпионаты") Тогда
		Туры = СерверныйФункции.ТурыЧемпионата(Ссылка);
		Для Каждого Элемент Из Туры Цикл
			Ответ = Макс(Ответ, ДатыУстановить(Элемент));
		КонецЦикла;
	КонецЕсли;
	Возврат Ответ;
КонецФункции
