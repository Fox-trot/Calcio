
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Изображение	= СерверныйФункции.ИзображениеПолучить(Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Города") Тогда
		Объект.Город = Объект.Владелец;
	ИначеЕсли ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Команды") Тогда
		Объект.Город = Объект.Владелец.Город;
	КонецЕсли;
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент=Неопределено)
	ВладелецПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Объект.Ссылка.Пустая() Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	Стадионы.Ссылка КАК Ссылка
		                      |ИЗ
		                      |	Справочник.Стадионы КАК Стадионы
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Города КАК Города
		                      |		ПО Стадионы.Город = Города.Ссылка
		                      |ГДЕ
		                      |	Стадионы.Город = &Город
		                      |	И (Стадионы.Наименование = &Наименование
		                      |			ИЛИ Стадионы.Комментарий ПОДОБНО &Комментарий)");
		Запрос.УстановитьПараметр("Наименование",		Объект.Наименование);
		Запрос.УстановитьПараметр("Комментарий",		"%" + СокрЛП(Объект.Наименование) + "%");
		Запрос.УстановитьПараметр("Город",				Объект.Город);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Уже есть стадион с таким названием", Отказ, Объект.Ссылка, "Наименование");
		Иначе	//Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Команды") Тогда
			Запрос.Текст = "ВЫБРАТЬ
			               |	Стадионы.Ссылка КАК Ссылка
			               |ИЗ
			               |	Справочник.Стадионы КАК Стадионы
			               |ГДЕ
			               |	Стадионы.Владелец = &Владелец";
			Запрос.УстановитьПараметр("Владелец",			Объект.Владелец);
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Команды") Тогда
					ОбщегоНазначения.СообщитьОбОшибке(СтрШаблон("У команды уже есть один стадион %1", Выборка.Ссылка), Отказ, Объект.Ссылка, "Владелец");
				Иначе
					ОбщегоНазначения.СообщитьОбОшибке("У города уже есть один стадион",, Объект.Ссылка, "Владелец");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Модифицированность Тогда
		СерверныйФункции.ПередЗаписьюНаСервереИзображение(ТекущийОбъект, Изображение, Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаСервере
Процедура ВидимостьДоступностьУстановить()
	Если ТипЗнч(Объект.Владелец) = Тип("СправочникСсылка.Города") Тогда
		Элементы.Город.ТолькоПросмотр				= Истина;
	ИначеЕсли ЕстьИстория(Объект.Ссылка) Тогда
		Элементы.Город.ТолькоПросмотр				= Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьИстория(Ссылка)
	Возврат СерверныйФункции.ЕстьИстория(Ссылка);
КонецФункции

&НаКлиенте
Функция ПараметрыДиалога()
	Возврат НаКлиенте.ПараметрыДиалога();
КонецФункции

&НаКлиенте
Процедура ВыбратьИзображение(Команда)
	Если СтрСравнить(Команда.Имя, "ВыбратьИзображение") = 0 Тогда
		НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект),,,, ПараметрыДиалога());
	ИначеЕсли СтрСравнить(Команда.Имя, "УдалитьИзображение") = 0 Тогда
		Изображение = ИзображениеДефолтПолучить();
		Модифицированность	= Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИзображениеДефолтПолучить()
	Возврат СерверныйФункции.ИзображениеДефолтПолучить();
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, ДополнительныеПараметры=Неопределено) Экспорт
	Если ТипЗнч(Результат) = Тип("ОписаниеПомещенногоФайла") Тогда
		Если ОбщегоНазначения.РасширениеФайлаВерно(Результат.СсылкаНаФайл.Расширение)
		И Результат.СсылкаНаФайл.Размер() >= ОбщегоНазначения.РазмерКартинкиМинимальный()
		Тогда
			Изображение = Результат.Адрес;
			Модифицированность	= Истина;
		Иначе
			ОбщегоНазначения.СообщитьОбОшибке("Данный тип файла не поддерживается");
		КонецЕсли;
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") И НЕ ПустаяСтрока(Результат) Тогда
		Данные				= Новый ДвоичныеДанные(Результат);
		Если Данные.Размер() >= ОбщегоНазначения.РазмерКартинкиМинимальный() Тогда
			Изображение			= ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);
			Модифицированность	= Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если ЭтоУРЛ(ПараметрыПеретаскивания.Значение) Тогда
		НачатьКопированиеФайла(Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект), ПараметрыПеретаскивания.Значение, СерверныйФункции.КаталогаФайлов() + ИмяФайла(ПараметрыПеретаскивания.Значение));
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СсылкаНаФайл") Тогда
		Если ОбщегоНазначения.РасширениеФайлаВерно(ПараметрыПеретаскивания.Значение.Расширение) Тогда
			Изображение			= ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПараметрыПеретаскивания.Значение.Файл.ПолноеИмя), УникальныйИдентификатор);
			Модифицированность	= Истина;
			СтандартнаяОбработка= Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Стадион", Объект.Ссылка);
КонецПроцедуры
