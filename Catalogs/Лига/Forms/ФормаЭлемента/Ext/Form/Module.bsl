
&НаСервере
Процедура КомандыЛигиЗагрузить()
	Если ЗначениеЗаполнено(Объект.Страна) Тогда
		КомандыЛиги.ЗагрузитьЗначения(СерверныйФункции.КомандыЛиги(Объект.Ссылка));
	ИначеЕсли КомандыЛиги.Количество() > 0 Тогда
		КомандыЛиги.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЕстьИстория()
	Если НЕ ЕстьИстория Тогда
		ЕстьИстория	= СерверныйФункции.ЕстьИстория(Объект.Ссылка);
	КонецЕсли;
	Возврат ЕстьИстория;
КонецФункции

&НаКлиенте
Функция ПараметрыДиалога()
	Возврат НаКлиенте.ПараметрыДиалога();
КонецФункции

&НаКлиенте
Процедура ВыбратьИзображение(Команда)
	Если СтрСравнить(Команда.Имя, "ВыбратьИзображение") = 0 Тогда
		НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект),,,, ПараметрыДиалога());
	ИначеЕсли СтрСравнить(Команда.Имя, "УдалитьИзображение") = 0 Тогда
		Изображение = ИзображениеДефолтПолучить();
		Модифицированность	= Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ИзображениеДефолтПолучить()
	Возврат СерверныйФункции.ИзображениеДефолтПолучить();
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборФайла(Результат, ДополнительныеПараметры=Неопределено) Экспорт
	Если ТипЗнч(Результат) = Тип("ОписаниеПомещенногоФайла") Тогда
		Если ОбщегоНазначения.РасширениеФайлаВерно(Результат.СсылкаНаФайл.Расширение)
		И Результат.СсылкаНаФайл.Размер() >= ОбщегоНазначения.РазмерКартинкиМинимальный()
		Тогда
			Изображение = Результат.Адрес;
			Модифицированность	= Истина;
		Иначе
			ОбщегоНазначения.СообщитьОбОшибке("Данный тип файла не поддерживается");
		КонецЕсли;
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") И НЕ ПустаяСтрока(Результат) Тогда
		ТекДанные				= Новый ДвоичныеДанные(Результат);
		Если ТекДанные.Размер() >= ОбщегоНазначения.РазмерКартинкиМинимальный() Тогда
			Изображение			= ПоместитьВоВременноеХранилище(ТекДанные, УникальныйИдентификатор);
			Модифицированность	= Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если Модифицированность Тогда
		СерверныйФункции.ПередЗаписьюНаСервереИзображение(ТекущийОбъект, Изображение, Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаСервере
Процедура ВидимостьДоступностьУстановить()
	Если ЕстьИстория() Тогда
		Элементы.Владелец.ТолькоПросмотр			= НЕ РедактированиеДоступно;
		Элементы.Страна.ТолькоПросмотр				= НЕ РедактированиеДоступно;
		Элементы.ОлимпийскаяСистема.ТолькоПросмотр	= НЕ РедактированиеДоступно;
	Иначе
		Элементы.ОлимпийскаяСистема.Доступность		= (Объект.КоличествоРотация = 0);
	КонецЕсли;
	Элементы.КоличествоРотация.ТолькоПросмотр	= (Объект.ОлимпийскаяСистема Или НЕ ЗначениеЗаполнено(Объект.Страна));
	
	Элементы.СтраницаКоманды.Доступность		= (НЕ Объект.ОлимпийскаяСистема И ЗначениеЗаполнено(Объект.Страна));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЕстьИстория() Тогда
		РедактированиеУстановить();
	КонецЕсли;
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка.Пустая() Тогда
	Иначе
		КомандыЛигиЗагрузить();
	КонецЕсли;
	Изображение	= СерверныйФункции.ИзображениеПолучить(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ИзображениеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	Если ЭтоУРЛ(ПараметрыПеретаскивания.Значение) Тогда
		НачатьКопированиеФайла(Новый ОписаниеОповещения("ОбработатьВыборФайла", ЭтотОбъект), ПараметрыПеретаскивания.Значение, СерверныйФункции.КаталогаФайлов() + ИмяФайла(ПараметрыПеретаскивания.Значение));
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СсылкаНаФайл") Тогда
		Если ОбщегоНазначения.РасширениеФайлаВерно(ПараметрыПеретаскивания.Значение.Расширение) Тогда
			Изображение			= ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПараметрыПеретаскивания.Значение.Файл.ПолноеИмя), УникальныйИдентификатор);
			Модифицированность	= Истина;
			СтандартнаяОбработка= Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	КомандыЛигиЗагрузить();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если СтрСравнить(ИмяСобытия, "Матчи") = 0
	И Параметр = Объект
	Тогда
		ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Объект.Команды.Количество() >= 6 Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура КомандыЗаполнитьНаСервере()
	Если Объект.Ссылка.Пустая() Тогда
		//
	Иначе	//Если Объект.Команды.Количество() = 0 Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 6
		                      |	ТурнирнаяТаблица.Команда КАК Команда
		                      |ИЗ
		                      |	РегистрНакопления.ТурнирнаяТаблица КАК ТурнирнаяТаблица
		                      |ГДЕ
		                      |	ТурнирнаяТаблица.Тур.Владелец.Владелец = &Лига
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	ТурнирнаяТаблица.Команда
		                      |
		                      |УПОРЯДОЧИТЬ ПО
		                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ГОД(ТурнирнаяТаблица.Период)) УБЫВ,
		                      |	СУММА(ТурнирнаяТаблица.КоличествоОчков) УБЫВ");
		Запрос.УстановитьПараметр("Лига",			Объект.Ссылка);
		Объект.Команды.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандыЗаполнить(Команда)
	КомандыЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	РедактированиеУстановить();
	ВидимостьДоступностьУстановить();
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеУстановить()
	РедактированиеДоступно = Истина;
	Элементы.ФормаРазрешитьРедактирование.Пометка = Истина;
КонецПроцедуры
