
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Лига") Тогда
		Владелец	= ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Страны") Тогда
		Страна	= ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Владелец") Тогда
			ОбработкаЗаполнения(ДанныеЗаполнения.Владелец, ТекстЗаполнения, Ложь);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		Владелец = СерверныйФункции.Футбол();
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ЗначениеЗаполнено(Страна) Тогда
		Команды.Свернуть("Команда");
	Иначе
		Команды.Очистить();
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Команды.Очистить();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ЗначениеЗаполнено(Страна) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ЛигаКоманды.Команда КАК Команда
		                      |ИЗ
		                      |	Справочник.Лига.Команды КАК ЛигаКоманды
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Команды КАК Команды
		                      |			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Города КАК Города
		                      |			ПО Команды.Город = Города.Ссылка
		                      |		ПО ЛигаКоманды.Команда = Команды.Ссылка
		                      |ГДЕ
		                      |	ЛигаКоманды.Ссылка = &Лига
		                      |	И Города.Владелец <> &Страна");
		Запрос.УстановитьПараметр("Лига",			Ссылка);
		Запрос.УстановитьПараметр("Страна",			Страна);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрШаблон("Команда %1 указана неверно", Выборка.Команда), Отказ);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
